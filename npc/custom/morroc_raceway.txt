//===== eAthena Script =======================================
//= 夢羅克賽跑場
//===== By: ==================================================
//= acky (god@acky.com)
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= Any eAthena Version
//===== Description: =========================================
//= Lets players race around Morroc (pvp_y_1-5.gat)
//===== Additional Comments: =================================
//= If there are more than 3 players, at least 3 people
//= must finish before a new race can be started.
//=
//= If there are less than 3 players, at least 1 person
//= must finish before a new race can be started.
//=
//= Removed permanent global variables
//= 1.2 Removed Duplicates [Silent]
//===== 中文化: =================================
//= 守護永恆2006/02/16
//============================================================

//Warps you into race way
morocc.gat,166,105,6	script	喜歡跑步的少女#01	116,{
mes "[喜歡跑步的少女]";
mes "你想進入 ^0000FF夢洛克賽跑場^000000?";
next;
menu "是的",L_Warp,"不",-;
mes "[喜歡跑步的少女]";
mes "好的,當你想要離開的時候請告訴我.";
close;

L_Warp:
warp "pvp_y_1-5.gat",165,256;
close;
}

//Warps you out of raceway
pvp_y_1-5.gat,169,265,5	script	喜歡跑步的少女#02	116,{
mes "[喜歡跑步的少女]";
mes "歡迎來到夢洛克賽跑場!";
next;
menu "信息",-,"離開",L_Warp,"取消",L_Cancel;
mes "[喜歡跑步的少女]";
mes "首先有人必須點擊起跑員 NPC來開始賽跑";
next;
mes "[喜歡跑步的少女]";
mes "當賽跑開始時,請按照逆時針圍繞夢洛克城奔跑.";
next;
mes "[喜歡跑步的少女]";
mes "你必須經過所有的標記點,不要作弊哦!!";
close;

L_Warp:
warp "morocc.gat",165,101;

L_Cancel:
mes "[喜歡跑步的少女]";
mes "歡迎再來!";
close;
}

//Counts down and starts race
pvp_y_1-5.gat,145,269,5	script	起跑員	733,{
if ($@race != 0) goto L_Started;
if ($@counting != 0) goto L_Started;
if ($@racecount == 1) goto L_Started;
L_Menu:
mes "[起跑員]";
mes "請保持在靠我東邊的距離內.";
menu "開跑",L_Count,"取消",-;
close;

	L_Count:
		set $@counting,1;
		mes "倒計時準備中.....";
		addtimer 1000, "起跑員::OnCount1000";
		addtimer 2000, "起跑員::OnCount2000";
		addtimer 3000, "起跑員::OnCount3000";
		addtimer 4000, "起跑員::OnCount4000";
		announce strcharinfo(0) + "開始倒計時",1;
		announce "請準備就緒!!",1;
		close;


	OnCount1000:
		announce "[3]",1;
		end;

	OnCount2000:
		announce "[2]",1;
		end;

	OnCount3000:
		announce "[1]",1;
		end;

	OnCount4000:
		emotion 27;
		specialeffect 267;
		announce "[開始!!!]",1;
		set $@race,1;
		set $@position,0;
		set $@counting,0;
		set $@raceid,rand(100000,999999);
		end;


L_Started:
if ((getmapusers("pvp_y_1-5.gat") < 3) && ($@position > 0)) goto L_Menu;
if ($@position > 2) goto L_Menu;
mes "[起跑員]";
mes "賽跑進行中";
close;

OnInit:
set $@race,0;
set $@position,0;
set $@racecount,0;
end;
}

//檢查點 1
pvp_y_1-5.gat,144,262,5	script	Check Point 1	111,0,5,{
end;
OnTouch:
if (@raceid != $@raceid) goto L_Started;
if (@race == 6) goto L_Finished;
if ($@race == 1) goto L_Started;
mes "賽跑還未開始,請回到起點.";
close;
L_Started:
set @race,1;
set @raceid,$@raceid;
end;
L_Finished:
mes "你必須完成賽跑全路程.";
close;
}

//檢查點 2
pvp_y_1-5.gat,73,247,5	script	Check Point 2	111,6,6,{
end;
OnTouch:
if (@race != 1) goto L_Miss;
set @race,2;
announce "[" + strcharinfo(0) +"] 已經到達了檢查點 [1]",1;
end;
L_Miss:
mes "你錯過了檢查點,請重新來過.";
close;
}

//檢查點 3
pvp_y_1-5.gat,77,44,5	script	Check Point 3	111,6,6,{
end;
OnTouch:
if (@race != 2) goto L_Miss;
set @race,3;
announce "[" + strcharinfo(0) +"] 已經到達了檢查點 [2]",1;
end;
L_Miss:
mes "你錯過了檢查點,請重新來過.";
close;
}

//檢查點 4
pvp_y_1-5.gat,249,60,5	script	Check Point 4	111,6,6,{
end;
OnTouch:
if (@race != 3) goto L_Miss;
set @race,4;
announce "[" + strcharinfo(0) +"] 已經到達了檢查點 [3]",1;
end;
L_Miss:
mes "你錯過了檢查點,請重新來過.";
close;
}

//檢查點 5
pvp_y_1-5.gat,255,256,5	script	Check Point 5	111,6,6,{
end;
OnTouch:
if (@race != 4) goto L_Miss;
set @race,5;
announce "[" + strcharinfo(0) +"] 已經到達了檢查點 [4]",1;
end;
L_Miss:
mes "你錯過了檢查點,請重新來過.";
close;
}

//終點線
pvp_y_1-5.gat,174,244,5	script	Finish Line	111,6,6,{
end;
OnTouch:
if (@raceid != $@raceid) goto L_WrongRace;
if (@race != 5) goto L_Miss;
set @race,6;
set $@position,$@position+1;
announce "[" + strcharinfo(0) +"] 衝過了終點!!! [Position: " + $@position + "]",1;
end;
L_Miss:
mes "你錯過了檢查點,請重新來過.";
close;
L_WrongRace:
mes "你沒有參賽.";
close;
}

//檢查點旗標
pvp_y_1-5.gat,144,267,4	script	Check Point 1#01	722,{
end;
}
pvp_y_1-5.gat,144,257,4	script	Check Point 1#02	722,{
end;
}
pvp_y_1-5.gat,70,252,3	script	Check Point 2#01	722,{
end;
}
pvp_y_1-5.gat,77,243,3	script	Check Point 2#02	722,{
end;
}
pvp_y_1-5.gat,81,48,1	script	Check Point 3#01	722,{
end;
}
pvp_y_1-5.gat,72,40,1	script	Check Point 3#02	722,{
end;
}
pvp_y_1-5.gat,244,65,7	script	Check Point 4#01	722,{
end;
}
pvp_y_1-5.gat,252,57,7	script	Check Point 4#02	722,{
end;
}
pvp_y_1-5.gat,259,260,5	script	Check Point 5#01	722,{
end;
}
pvp_y_1-5.gat,251,252,5	script	Check Point 5#02	722,{
end;
}
pvp_y_1-5.gat,174,249,4	script	Finish Line#01	722,{
end;
}
pvp_y_1-5.gat,174,238,4	script	Finish Line#02	722,{
end;
}