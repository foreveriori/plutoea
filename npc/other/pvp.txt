// PvP 腳本
// 
// 中文化作者:		Dino9021
// 
// 中文版本編號:	20041204.0.1
//
//============================================================

//===== eAthena Script ======================================= 
//= PvP
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.2a
//===== Compatible With: ===================================== 
//= eAthena 0.5.2 +
//===== Description: ========================================= 
//=
//===== Additional Comments: ================================= 
//= v1.1 Fixed arena names for Nightmare mode.  Added room limit
//= check.[kobra_k88]
//= v1.2 Fixed the bug that you aways savepoint in geffen.[shadowlady]
//= v1.2a Added PvP Narrator function. Using args for Fight
//= Square Helpers. [kobra_k88]
//============================================================ 


//==========================================================================
//			PvP Narrators
//==========================================================================

// Alberta -----------------------------------------
alberta_in.gat,22,146,4	script	PvP解說員#1	84,{
	callfunc "F_PvPNarr", "alberta_in.gat", 24, 143;
	end;
}
// Payon -------------------------------------
payon_in01.gat,173,109,3	script	PvP解說員#2	84,{
	callfunc "F_PvPNarr", "payon_in01.gat", 169, 108;
	end;
}
// Prontera -------------------------------------
prt_in.gat,56,140,4	script	PvP解說員#3	84,{
	callfunc "F_PvPNarr", "prt_in.gat", 54, 137;
	end;
}
// Morocc --------------------------------------
morocc_in.gat,144,138,4	script	PvP解說員#4	84,{
	callfunc "F_PvPNarr", "morocc_in.gat", 141, 140;
	end;
}
// Geffen --------------------------------------
geffen_in.gat, 67,63,4	script	PvP解說員#5	84,{
	callfunc "F_PvPNarr", "geffen_in.gat", 64, 60;
	end;
}

//******************************************************//
//	Function: PvP Narrator
//******************************************************//
// arg(0): mapname
// arg(1): savepoint x cord.
// arg(2): savepoint y cord.
//=============================
function	script	F_PvPNarr	{
	mes "[PvP解說員]";
	mes "哈囉! 歡迎光臨! 我是負責解釋 PvP 模式的解說員!";
	M_Menu:
	next;
	menu "什麼是 PvP?",M_0, "什麼叫做 PvP 模式?",M_1, "PvP 的規則是什麼?",M_2, "儲存現在位置",M_3,"結束對話",M_End;

	M_0:
		mes "[PvP解說員]";
		mes "PvP模式就是 'Player VS Player Mode' 的簡寫..";
		mes "也就是一種在假想的世界裡，享受玩家間大戰的特別戰鬥";
		next;
		mes "[PvP解說員]";
		mes "簡單的說就是與其它玩家打鬥的模式";
		mes "只要向我身邊這位 ^3355ff守門人^000000 說一聲表示願意嘗試的話。";
		mes "他便會引導你進入為 PvP 模式所準備的打鬥場";
		next;
		mes "[PvP解說員]";
		mes "請你一定要小心, 也請好好享受戰鬥的樂趣吧。";
		mes "要進入 PvP 模式等級要達到 31 級以上。";
		next;
		mes "[PvP解說員]";
		mes "擁有參加資格以後, 選擇下列模式中任意一種 '模式' 即可。";
		next;
		mes "[PvP解說員]";
		mes "溜溜猴模式：其規則與實戰不同, 戰死後經驗值不會損失, 身上道具也不會掉落。";
		next;
		mes "[PvP解說員]";
		mes "夢魘模式：其規則與實戰是一樣的, 戰死後經驗值會損失, 身上道具也會掉落。";
		next;
		mes "[PvP解說員]";
		mes "請考慮清楚後再選擇進入。";
		goto M_Menu;
	M_1:
		mes "[PvP解說員]";
		mes "看見我身邊這位 PvP 守門人了？";
		mes "與之對話，並交納 500 Zeny 便可以移動到PvP打鬥場。";
		next;
		mes "[PvP解說員]";
		mes "打鬥模式分為溜溜猴模式和夢魘模式, 請小心選擇自己想參加的模式。";
		next;
		mes "[PvP解說員]";
		mes "這 2 種模式的打鬥場裡各有數名解說員, 他們會將 31 級以上的參加者每 10 等級再做細分。";
		next;
		mes "[PvP解說員]";
		mes "有 5 個 PvP 專用房間可供選擇, 選擇你所想去的那個房間便可移動到那裡";
		next;
		mes "[PvP解說員]";
		mes "從解說員頭上的窗口可以知道等級的限制以及此地圖正在進行打鬥的人數, 這點請你要注意。";
		next;
		mes "[PvP解說員]";
		mes "解說員頭上的窗口會出現【參加人數/總人數】這樣的資料。";
		next;
		mes "[PvP解說員]";
		mes "另一點, PvP 專用地圖是無法儲存位置的。";
		goto M_Menu;
	M_2:
		mes "[PvP解說員]";
		mes "進入PvP模式後, 畫面右下角出現的是該地圖 玩家自己的排行 / 地圖玩家人數, 相信會對你有幫助。";
		next;
		mes "[PvP解說員]";
		mes "在 PvP 模式中的經驗只有在 PvP 模式中適用, 這點要注意!";
		next;
		mes "[PvP解說員]";
		mes "一開始每位玩家的經驗基本上都是 '5', 在途中若是贏得勝利的話可增加經驗 1 , 若是失敗的話則經驗會減少 5";
		next;
		mes "[PvP解說員]";
		mes "如果經驗減到 0 以下則會被踢出 PvP 模式, 不得繼續參與。";
		mes "但是如果經驗大於 0 卻被殺死的話, 可以藉由同伴復活或回復體力繼續參與遊戲，這樣你懂了吧。";
		next;
		mes "[PvP解說員]";
		mes "打鬥的規則與外面戰鬥的規則是一樣的。";
		goto M_Menu;
	M_3:
		savepoint getarg(0),getarg(1),getarg(2);
		mes "[PvP解說員]";
		mes "已經成功存儲位置。";
		mes "非常感謝您的使用。";
		close;

	M_End:
		mes "[PVP解說員]";
		mes "這是一個玩家與玩家間的大戰";
		mes "能讓玩家在戰鬥中找到樂趣";
		mes "期待各位";
		mes "勇於前來挑戰！";
		close;
}


//==========================================================================
//			Gate Keepers
//==========================================================================

// Alberta -----------------------------------
alberta_in.gat, 26,146,4	script	PvP守門人#1::GateKeep	83,{
	mes "[PvP守門人]";
	mes "我是為您開啟PvP打鬥場之門的PvP守門人。";
	mes "有關PvP模式的說明請你去向旁邊那位解說員詢問。";
	next;
	menu	"^5533FF'PvP 溜溜猴模式'^000000 打鬥場",M_0, 
		"^FF5533'PvP 夢魘模式'^000000 打鬥場",M_1, 
		"^5533FF'PvP 決鬥 (作戰) 模式'^000000 格鬥場",M_2, "結束對話",M_End;
	M_0:
		set @mode$, "溜溜猴模式";
		set @players1, 128;
		set @players2, 128;
		set @room1$, "普隆德拉";
		set @room2$, "依斯魯得";
		set @room3$, "裴揚";
		set @room4$, "艾爾貝塔";
		set @room5$, "夢羅克";
		callsub sF_Move;
		warp "pvp_y_room.gat", 51, 23;
		end;
	M_1:
		set @mode$, "夢魘模式";
		set @players1, 64;
		set @players2, 32;
		set @room1$, "三明治 房間";
		set @room2$, "雷達鎖定 房間";
		set @room3$, "四通道 房間";
		set @room4$, "反十字 房間";
		set @room5$, "羅盤 房間";
		callsub sF_Move;
		warp "pvp_n_room.gat", 51, 23;
		end;
	M_2:

			mes "[PvP守門人]";
			mes "要進入決鬥 (作戰) 模式必須攜帶參觀卷, 你有帶來嗎?";
		next;
		menu "有,我帶來了",-, "參觀卷?!",sM_1;

			if(countitem(7029) < 1) goto sM_1;
			delitem 7029, 1;
			set @mode$, "Fighting";
			callsub sF_Move;
			warp "pvp_y_room.gat", 51, 23;
			end;

		sM_1:
			mes "[PvP守門人]";
			mes "呃? 你沒有? 那抱歉啦! 只有攜帶參觀券我才能讓你進去!";
			close;
	M_End:
		mes "[PvP守門人]";
		mes "這是一個玩家與玩家間的大戰";
		mes "能讓玩家在戰鬥中找到樂趣";
		mes "期待各位";
		mes "勇於前來挑戰！";
		close;

sF_Move:
	mes "[PvP守門人]";
	mes "入場費用是500 Zeny 您要移動到 "+@mode$+" 式打鬥場了嗎?";
	next;
	menu "移動",-, "取消",M_End;

		if(BaseLevel < 31) goto sL_LowLvl;
		if(Zeny < 500) goto sL_NdZeny;
		set Zeny, Zeny - 500;
		mes "[PvP守門人]";
		mes "那麼.......";
		next;
		return;

		sL_LowLvl:
			mes "[PvP守門人]";
			mes "您的等級不到 ^FF4444'31'^000000 級, 無法進入.";
			close;
		sL_NdZeny:
			mes "[PvP守門人]";
			mes "您的 Zeny 不夠, 入場費用是 ^FF4444'500'^000000 Zeny.";
			close;
}

// Geffen --------------------------------------------
geffen_in.gat, 63,63,4	duplicate(GateKeep)	PvP守門人#2	83
// Morroc----------------------------------------------
morocc_in.gat, 144,142,4	duplicate(GateKeep)	PvP守門人#3	83
// Payon ----------------------------------------------
payon_in01.gat,175,106,3	duplicate(GateKeep)	PvP守門人#4	83
// Prontera -------------------------------------------
prt_in.gat,52,140,4	duplicate(GateKeep)	PvP守門人#5	83



//==========================================================================//
// 		PvP Yoyo Mode Fight Square Helpers
//==========================================================================//

// LV31 ~ 40 ---------------------------------
pvp_y_room.gat,30,85,4	script	打鬥場接待員::YHelper1	105,{
	callfunc "F_PvPRoom", "y_1", 31, 40;
OnInit:
	waitingroom "LV31 ~ LV40",0;
	end;
}

// LV41 ~ 50 --------------------------------
pvp_y_room.gat,38,85,4	script	打鬥場接待員::YHelper2	105,{
	callfunc "F_PvPRoom", "y_2", 41, 50;
OnInit:
	waitingroom "LV41 ~ LV50",0;
	end;
}

// LV51 ~ 60 ---------------------------------
pvp_y_room.gat,46,85,4	script	打鬥場接待員::YHelper3	105,{
	callfunc "F_PvPRoom", "y_3", 51, 60;
OnInit:
	waitingroom "LV51 ~ LV60", 0;
	end;
}

// LV61 ~ 70 ------------------------------------
pvp_y_room.gat,54,85,4	script	打鬥場接待員::YHelper4	105,{
	callfunc "F_PvPRoom", "y_4", 61, 70;
OnInit:
	waitingroom "LV61 ~ LV70", 0;
	end;
}

// LV71 ~ 80 ------------------------------------
pvp_y_room.gat,62,85,4	script	打鬥場接待員::YHelper5	105,{
	callfunc "F_PvPRoom", "y_5", 71, 80;
OnInit:
	waitingroom "LV71 ~ LV80", 0;
	end;
}

// LV81 ~ 90 --------------------------------------
pvp_y_room.gat,70,85,4	script	打鬥場接待員::YHelper6	105,{
	callfunc "F_PvPRoom", "y_6", 81, 90;
OnInit:
	waitingroom "LV81 ~ LV90", 0;
	end;
}

// LV91 ~  --------------------------------------------
pvp_y_room.gat,78,85,4	script	打鬥場接待員::YHelper7	105,{
	callfunc "F_PvPRoom", "y_7", 91, 255;
OnInit:
	waitingroom "LV91 ~ ", 0;
	end;
}

// No Limit  -----------------------------------------
pvp_y_room.gat,86,85,4	script	打鬥場接待員::YHelper8	105,{
	callfunc "F_PvPRoom", "y_8", 0, 255;
OnInit:
	waitingroom "無等級限制", 0;
	end;
}


//==========================================================================//
// 		PvP Nightmare Mode Fight Square Helpers
//==========================================================================//
// LV31 ~ 40 -------------------------------------
pvp_n_room.gat,30,85,4	script	打鬥場接待員#N1	105,{
	callfunc "F_PvPRoom", "n_1", 31, 40;
OnInit:
	waitingroom "LV31 ~ LV40",0;
	end;
}

// LV41 ~ 50 --------------------------------------
pvp_n_room.gat,38,85,4	script	打鬥場接待員#N2	105,{
	callfunc "F_PvPRoom", "n_2", 41, 50;
OnInit:
	waitingroom "LV41 ~ LV50",0;
	end;
}

// LV51 ~ 60 -------------------------------------
pvp_n_room.gat,46,85,4	script	打鬥場接待員#N3	105,{
	callfunc "F_PvPRoom", "n_3", 51, 60;
OnInit:
	waitingroom "LV51 ~ LV60", 0;
	end;
}

// LV61 ~ 70 ------------------------------------
pvp_n_room.gat,54,85,4	script	打鬥場接待員#N4	105,{
	callfunc "F_PvPRoom", "n_4", 61, 70;
OnInit:
	waitingroom "LV61 ~ LV70", 0;
	end;
}

// LV71 ~ 80 -----------------------------------
pvp_n_room.gat,62,85,4	script	打鬥場接待員#N5	105,{
	callfunc "F_PvPRoom", "n_5", 71, 80;
OnInit:
	waitingroom "LV71 ~ LV80", 0;
	end;
}

// LV81 ~ 90 ------------------------------------
pvp_n_room.gat,70,85,4	script	打鬥場接待員#N6	105,{
	callfunc "F_PvPRoom", "n_6", 81, 90;
OnInit:
	waitingroom "LV81 ~ LV90", 0;
	end;
}

// LV91 ~  ---------------------------------------
pvp_n_room.gat,78,85,4	script	打鬥場接待員#N7	105,{
	callfunc "F_PvPRoom", "n_7", 91, 255;
OnInit:
	waitingroom "LV91 ~ ", 0;
	end;
}

// No Limit  ----------------------------------
pvp_n_room.gat,86,85,4	script	打鬥場接待員#N8	105,{
	callfunc "F_PvPRoom", "n_8", 0, 255;
OnInit:
	waitingroom "無等級限制", 0;
	end;
}


//***************************************************************//
//	Function: PvP Fight Square Helper
//***************************************************************//
// arg(0): pvp room name
// arg(1): minimum base lvl to enter arena
// arg(2): maximum base lvl allowed to enter arena
//===============================================
function	script	F_PvPRoom	{

	mes "[打鬥場接待員]";
	if(BaseLevel < getarg(1)) goto L_LowLvl;
	if(BaseLevel > getarg(2)) goto L_HiLvl;
	mes "Please choose an arena to fight in...";
	M_Menu:
	next;
	menu 	@room1$ + " [" + getmapusers("pvp_" + getarg(0) + "-1.gat") + "/" +@players1+ "]",M_Rm1, 
		@room2$ + " [" + getmapusers("pvp_" + getarg(0) + "-2.gat") + "/" +@players2+ "]",M_Rm2, 
		@room3$ + " [" + getmapusers("pvp_" + getarg(0) + "-3.gat") + "/" +@players2+ "]",M_Rm3, 
		@room4$ + " [" + getmapusers("pvp_" + getarg(0) + "-4.gat") + "/" +@players2+ "]",M_Rm4, 
		@room5$ + " [" + getmapusers("pvp_" + getarg(0) + "-5.gat") + "/" +@players2+ "]",M_Rm5, "取消",M_End;
	M_Rm1:
		if (getmapusers("pvp_" + getarg(0) + "-1.gat") == @players1) goto L_Full;
		warp "pvp_" + getarg(0) + "-1.gat",0,0;
		end;
	M_Rm2:
		if (getmapusers("pvp_" + getarg(0) + "-2.gat") == @players2) goto L_Full;
		warp "pvp_" + getarg(0) + "-2.gat",0,0;
		end;
	M_Rm3:
		if (getmapusers("pvp_" + getarg(0) + "-3.gat") == @players2) goto L_Full;
		warp "pvp_" + getarg(0) + "-3.gat",0,0;
		end;
	M_Rm4:
		if (getmapusers("pvp_" + getarg(0) + "-4.gat") == @players2) goto L_Full;
		warp "pvp_" + getarg(0) + "-4.gat",0,0;
		end;
	M_Rm5:
		if (getmapusers("pvp_" + getarg(0) + "-5.gat") == @players2) goto L_Full;
		warp "pvp_" + getarg(0) + "-5.gat",0,0;
		end;
	M_End:
		close;

	L_LowLvl:
		mes "抱歉, 要進入本打鬥場您的 Lv 必須高於 " + @lvl1 + " 才行, 請選擇一個等級比較低的打鬥場吧!";
		close;
	L_HiLvl:
		mes "抱歉, 要進入本打鬥場您的 Lv 必須低於 " + @lvl2 + " 才行, 請選擇一個等級比較高的打鬥場吧!";
		close;
	L_Full:
		mes "[打鬥場接待員]";
		mes "對不起, 本打鬥場已經額滿了! 請選擇其他打鬥場吧....";
		goto M_Menu;
}
