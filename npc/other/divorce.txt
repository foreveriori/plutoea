//========================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -----------------------------------------
// - 離婚 腳本 -
//========================
//= 版本：1.3  中文化 小笨兵
//========================


nif_in.gat,190,112,5	script	小惡魔#divorce	1109,{
	mes "[小惡魔]";
	if (getpartnerid() == 0) {
		mes "喀喀喀..人類到最後都是一個人的";
		mes "寂寞的心情只是暫時而已，";
		mes "寂寞的話養一隻寵物怎麼樣呢?";
		mes "最近我小惡魔很受歡迎喔!";
		close;
	}
	mes "看起來表情不太好喔... 有什麼";
	mes "後悔的事嗎? 你要知道我一點";
	mes "都不想幫忙覺得幸福的人，";
	mes "小惡魔對那種人";
	mes "一點興趣都沒有，喀喀喀";
	next;
	mes "[小惡魔]";
	mes "其他的擱在一邊先問你好了，";
	mes "結婚之後幸福嗎? 先不要想";
	mes "什麼愛不愛的... 我比誰都清楚";
	mes "在華麗的結婚典禮背後";
	mes "藏著無數的問題";
	next;
	mes "[小惡魔]";
	mes "沒錯，你來到這裡也是";
	mes "一種命運，我也知道是想要";
	mes "改變未來人生的意志，";
	mes "就算是承諾永恆的對象，那顆心";
	mes "也總有一天會枯萎的...!";
	next;
	mes "[小惡魔]";
	mes "方法很簡單! 所謂的婚姻也是";
	mes "契約的一種，只要拜託";
	mes "小惡魔的話牠可以很容易的";
	mes "幫你毀約的... 了解嗎?";
	mes "自己過著自由的生活呢!!";
	next;
	mes "[小惡魔]";
	mes "也不需要對方同意，";
	mes "願意的話，也許會墜入新戀情吧，";
	mes "不必要對離去的對方";
	mes "念念不忘... 要我幫你嗎?";
	mes "可以使婚姻無效喔...!";
	next;
	if(select("不，我現在很幸福","對，我想自己過生活")==1) {
		mes "[小惡魔]";
		mes "哼，哼，情侶之類的... 情侶之類的!!";
		mes "總有一天會後悔的再來找";
		mes "小惡魔呢!! 瞧你的幸福";
		mes "會維持多久!!";
		close2;
		warp "niflheim.gat",169,162;
		end;
	}
	mes "[小惡魔]";
	mes "好吧，其他的不需要，只要有";
	mes "你的心及2,500,000金幣就夠了，";
	mes "回到結婚前的生活吧，";
	mes "可以吧? 嗯? 是這樣嗎?";
	mes "是解除婚約嗎?";
	next;
	if(select("...我再考慮一下","我想要付出代價之後解除婚約")==1) {
		mes "[小惡魔]";
		mes "不必再考慮了!人類到最後都是一個人的!";
		mes "當你做好心理的調適及資金的籌款";
		mes "兩者都具備好了的時候";
		mes "再來找我吧...";
		mes "喀喀喀...";
		close;
	}
	mes "[小惡魔]";
	if (!isloggedin(getpartnerid())) {
		mes "喀喀喀...人們總是那麼笨";
		mes "你的伴侶不在線上...喀喀喀...";
		emotion e_heh;
		close;
	}
	if (Zeny < 2500000){
		mes "我說過2,500,000金幣了吧? 這是為了";
		mes "拜託小惡魔而表示的誠意呢，";
		mes "連這些錢也沒有的話就一輩子";
		mes "過著被約束的生活吧... 喀喀喀";
		close;
	}
	set Zeny, Zeny - 2500000;
	mes "在你改戀心意之前馬上幫你解除婚約!";
	mes "好的好的! 請稍待一會兒";
	next;
	specialeffect 244;	//EF_MAGICROD
	specialeffect2 259;	//EF_DEVIL (now use a custom id)
	divorce;
	percentheal -100,-100;
	mes "[小惡魔]";
	mes "喀喀喀...你現在自由啦";
	mes "感覺很放鬆吧!";
	mes "這一切都應該感謝小惡魔.....!";
	close;
}