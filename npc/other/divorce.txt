//========================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -----------------------------------------
// - 離婚 腳本 -
//========================
//= 版本：1.3  中文化 小笨兵
//========================


nif_in.gat,190,112,5	script	小惡魔#divorce	1109,{
	mes "[小惡魔]";
	if (getpartnerid() == 0) {
		mes "喀喀喀..人類到最後都是一個人的";
		mes "寂寞的心情只是暫時而已，";
		mes "寂寞的話養一隻寵物怎麼樣呢?";
		mes "最近我小惡魔很受歡迎喔!";
		close;
	}
	mes "看起來表情不太好喔";
	mes "...有什麼後悔的事嗎?";
	mes "要知道我一點都不想幫忙覺的幸福的人";
	mes "喀喀喀...";
	next;
	mes "[小惡魔]";
	mes "其他的擱在一邊先問你好了";
	mes "結婚之後幸福嗎?";
	mes "喀喀喀...";
	next;
	mes "[小惡魔]";
	mes "你來到這裡也是一種命運";
	mes "我也知道是想要改變未來人生的意志";
	mes "就算是承諾永恆的對象...";
	mes "那顆心也總有一天會枯萎的...!";
	next;
	mes "[小惡魔]";
	mes "嗯!方法很簡單!";
	mes "所謂的婚姻也是契約的一種";
	mes "只要拜託話我的話可以很容易地幫你毀約的";
	mes "也不需要對方的同意";
	mes "可以使婚姻無效喔";
	next;
	mes "[小惡魔]";
	mes "總之，可自己過著自由的生活呢!";
	mes "願意的話，也許會墜入新戀情唷";
	mes "不必要對離去的對方念念不忘";
	mes "喀喀喀...";
	mes "要離婚嗎?";
	next;
	if(select("不，我現在很幸福","對，我想自己過生活")==1) {
		mes "[小惡魔]";
		mes "哼 哼 哼!";
		mes "情侶伴侶們...決定了!!??";
		mes "即使之後再向我哭泣要毀約";
		mes "我也裝做什麼不知道!";
		close2;
		warp "niflheim.gat",169,162;
		end;
	}
	mes "[小惡魔]";
	mes "好吧";
	mes "只要有你的心意及2,500,000Zeny";
	mes "就夠了";
	mes "那麼回到結婚前的生活?";
	mes "馬上解除婚約嗎?";
	next;
	if(select("...我再考慮一下","是，離婚")==1) {
		mes "[小惡魔]";
		mes "不必再考慮了!人類到最後都是一個人的!";
		mes "當你做好心理的調適及資金的籌款";
		mes "兩者都具備好了的時候";
		mes "再來找我吧...";
		mes "喀喀喀...";
		close;
	}
	mes "[小惡魔]";
	if (!isloggedin(getpartnerid())) {
		mes "喀喀喀...人們總是那麼笨";
		mes "你的伴侶不在線上...喀喀喀...";
		emotion e_heh;
		close;
	}
	if (Zeny < 2500000){
		mes "我說過2,500,000Zeny了吧!";
		mes "這是為了拜託主人而表示的誠意。連那些錢也沒有的話";
		mes "就一輩子過著被約束的生活吧";
		mes "喀喀喀...";
		close;
	}
	set Zeny, Zeny - 2500000;
	mes "在你改戀心意之前馬上幫你解除婚約!";
	mes "好的好的! 請稍待一會兒";
	next;
	specialeffect 244;	//EF_MAGICROD
	specialeffect2 259;	//EF_DEVIL (now use a custom id)
	divorce;
	percentheal -100,-100;
	mes "[小惡魔]";
	mes "喀喀喀...你現在自由啦";
	mes "感覺很放鬆吧!";
	mes "這一切都應該感謝小惡魔.....!";
	close;
}