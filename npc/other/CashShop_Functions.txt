//========================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -----------------------------------------
// - 商城道具函式 腳本 -
//========================
//= 版本：1.2 ( 外加TRO的鑰匙工匠及各個轉蛋 )
//========================

// 隨地倉庫使用券
//============================================================ 
// - 隨時隨地打開倉庫
// - 沒有參數
function	script	F_CashStore	{
	cutin "kafra_01",2;
	mes "[卡普拉 職員]";
	mes "您好, 我是卡普拉呼叫服務,";
	mes "按照你的邀請開啟倉庫了";
	close2;
	openstorage;
	cutin "",255;
	return;
}


// 巨大蒼蠅翅膀
//============================================================ 
// - 隨著地圖上的隊長一起瞬間移動
// - 沒有參數
function	script	F_CashPartyCall	{
	warp "Random",0,0;
	if(getpartyleader(getcharid(1),2) == getcharid(0)) {
		getmapxy .@mapl$, .@xl, .@yl, 0;
		getpartymember getcharid(1);
		set .@partymembercount, $@partymembercount;
		copyarray .@partymembername$[0], $@partymembername$[0], .@partymembercount;
		for(set .@i, 0; .@i < .@partymembercount; set .@i, .@i + 1)
		if(!(getmapxy(.@mapm$, .@xm, .@ym, 0, .@partymembername$[.@i])) && (.@mapm$ == .@mapl$))
		warpchar .@mapl$, .@xl, .@yl, getcharid(0, .@partymembername$[.@i]);
	}
	return;
}

// 重置技能棒
//============================================================ 
// - 負重為0，且老鷹、手推車須放棄
// - 沒有參數
function	script	F_CashReset	{
	if (Class != Job_Novice) {
		if (Weight < 1) { 
			if (checkriding() || checkfalcon() || checkcart()) return;
			else {
				resetskill;
				sc_end SC_ALL;
			}
		}
	}
	return;
}

// 洞穴瞬間移動 捲軸
//============================================================ 
// - Warp player to random location of selected dungeon
//   due to lack of information.
// - 沒有參數
function	script	F_CashDungeon	{
	switch(select("諾可羅德:廢棄礦場:樹木裡的異界通路:艾音布羅克礦山洞穴:斐揚地下廢棄村落:玩具工廠倉庫:克雷斯特漢姆 地下監獄1樓:龍之城審良宮:崑崙神仙的圍棋臺:依斯魯得島海盜地洞密穴:烏龜島密穴:鐘塔 地下3樓:鐘塔 地上3樓:古城 地下水道2樓:史芬克斯 密穴4樓:金字塔 迷宮4樓:普隆德拉 地下水道3樓:天津町 榻榻米 迷宮1樓:生體試驗 研究所1樓:哎喲泰雅 古代神殿2樓"))
	{
	case 1: warp "mag_dun01",125,71; end;	// 諾可羅德
	case 2: warp "mjo_dun02",80,297; end;	// 廢棄礦場
	case 3: warp "um_dun02",125,122; end;	// 樹木裡的異界通路
	case 4: warp "ein_dun01",261,262; end;	// 艾音布羅克礦山洞穴
	case 5: warp "pay_dun03",155,150; end;	// 斐揚地下廢棄村落
	case 6: warp "xmas_dun01",133,130; end;	// 玩具工廠倉庫
	case 7: warp "gl_prison",140,15;end;		// 克雷斯特漢姆 地下監獄1樓
	case 8: warp "lou_dun03",165,38; end;	// 龍之城審良宮
	case 9: warp "gon_dun02",251,263; end;	// 崑崙神仙的圍棋臺
	case 10: warp "iz_dun02",350,335; end;	// 依斯魯得島海盜地洞密穴
	case 11: warp "tur_dun02",165,30; end;	// 烏龜島密穴
	case 12: warp "alde_dun03",275,180; end;	// 鐘塔 地下3樓
	case 13: warp "c_tower3",34,42; end;		// 鐘塔 地上3樓
	case 14: warp "gl_sew02",292,295; end;	// 古城 地下水道2樓
	case 15: warp "in_sphinx4",120,120; end;	// 史芬克斯 密穴4樓
	case 16: warp "moc_pryd04",195,4; end;	// 金字塔 迷宮4樓
	case 17: warp "prt_sewb3",20,175; end;	// 普隆德拉 地下水道3樓
	case 18: warp "ama_dun01",222,144; end;	// 天津町 榻榻米 迷宮1樓
	case 19: warp "lhz_dun01",19,153; end;	// 生體試驗 研究所1樓
	case 20: warp "ayo_dun02",70,240; end;	// 哎喲泰雅 古代神殿2樓
	}
	return;
}

// Coloured Butterly Wings
//============================================================ 
// - Warp player to differents Citys acording to the Scroll
// - Arg(0) = type of scroll.
function	script	F_CashCity	{
	switch(getarg(0))
	{
		// 黃
		case 1: switch(select("普隆德拉:吉芬:斐揚:夢羅克:艾爾帕蘭:艾爾貝塔")) {
			case 1: warp "prontera",0,0; end;
			case 2: warp "geffen",0,0; end;
			case 3: warp "payon",0,0; end;
			case 4: warp "morocc",0,0; end;
			case 5: warp "aldebaran",0,0; end;
			case 6: warp "alberta",0,0; end;
			}
		break;
		// 綠
		case 2: switch(select("朱諾:里希塔樂鎮:艾音布羅克:毀葛")) {
			case 1: warp "yuno",0,0; end;
			case 2: warp "lighthalzen",0,0; end;
			case 3: warp "einbroch",0,0; end;
			case 4: warp "hugel",0,0; end;
			}
		break;
		// 紅
		case 3: switch(select("拉赫:菲音斯")) {
			case 1: warp "rachel",0,0; end;
			case 2: warp "veins",0,0; end;
			}
		break;
		// 藍
		case 4: switch(select("哎喲泰雅:天津町:龍之城:崑崙")) {
			case 1: warp "ayothaya",0,0; end;
			case 2: warp "amatsu",0,0; end;
			case 3: warp "louyang",0,0; end;
			case 4: warp "gonryun",0,0; end;
			}
		break;
	}
	return;
}

// 空間移動捲軸
//============================================================ 
// - Warp player to save point or City acording to the Scroll
// - Arg(0) = type of scroll.
function	script	F_CashTele	{
	switch(getarg(0)) {
	case 1:
		switch(select("儲存點:普隆德拉:吉芬:艾爾帕蘭:依斯魯得島")) {
		case 1: warp "SavePoint",0,0; end;
		case 2: warp "prontera",167,68; end;
		case 3: warp "geffen",121,147; end;
		case 4: warp "aldebaran",168,112; end;
		case 5: warp "izlude",128,105; end;
		}
	case 2:
		switch(select("儲存點:斐揚:愛爾貝塔:夢羅克:克魔島")) {
		case 1: warp "SavePoint",0,0; end;
		case 2: warp "payon",172,101; end;
		case 3: warp "alberta",117,56; end;
		case 4: warp "morocc",165,255; end;
		case 5: warp "comodo",193,148; end;
		}
	case 3:
		switch(select("儲存點:龍之城:天津町:崑崙:哎喲泰雅")) {
		case 1: warp "SavePoint",0,0; end;
		case 2: warp "louyang",216,40; end;
		case 3: warp "amatsu",197,83; end;
		case 4: warp "gon_fild01",258,82; end;
		case 5:warp "ayothaya",149,71; end;
		}
	case 4:
		switch(select("儲存點:薑餅城:汶巴拉:尼芙菲姆")) {
		case 1: warp "SavePoint",0,0; end;
		case 2: warp "xmas_fild01",78,68; end;
		case 3: warp "umbala",100,154; end;
		case 4: warp "niflheim",49,146; end;
		}
	case 5:
		switch(select("儲存點:朱諾:艾音布羅克:里希塔樂鎮:毀葛")) {
		case 1: warp "SavePoint",0,0; end;
		case 2: warp "yuno",158,39; end;
		case 3: warp "einbroch",240,200; end;
		case 4: warp "lighthalzen",214,311; end;
		case 5: warp "hugel",95,43; end;
		}
	case 6:
		switch(select("儲存點:拉赫:菲音斯")) {
		case 1: warp "SavePoint",0,0; end;
		case 2: warp "rachel",115,125; end;
		case 3: warp "veins",205,102; end;
		}
	}
	return;
}

function	script	F_CashSeigeTele	{
	switch(select("Neuschwanstein (aldeg_cas01):Hohenschwangau (aldeg_cas02):Nuernberg (aldeg_cas03):Wuerzburg (aldeg_cas04):Rothenburg (aldeg_cas05):Repherion (gefg_cas01):Eeyorbriggar (gefg_cas02):Yesnelph (gefg_cas03):Bergel (gefg_cas04):Mersetzdeitz (gefg_cas05):Bright Arbor (payg_cas01):Sacred Altar (payg_cas02):Holy Shadow (payg_cas03):Scarlet Palace (payg_cas04):Bamboo Grove Hill (payg_cas05):Kriemhild (prtg_cas01):Swanhild (prtg_cas02):Fadhgridh (prtg_cas03):Skoegul (prtg_cas04):Gondul (prtg_cas05)")) {
	case 1: warp "alde_gld",48,91; end;
	case 2: warp "alde_gld",103,245; end;
	case 3: warp "alde_gld",142,87; end;
	case 4: warp "alde_gld",236,243; end;
	case 5: warp "alde_gld",269,90; end;
	case 6: warp "gef_fild13",217,75; end;
	case 7: warp "gef_fild13",307,237; end;
	case 8: warp "gef_fild13",77,297; end;
	case 9: warp "gef_fild13",190,276; end;
	case 10: warp "gef_fild13",312,91; end;
	case 11: warp "pay_gld",121,232; end;
	case 12: warp "pay_gld",297,116; end;
	case 13: warp "pay_gld",318,293; end;
	case 14: warp "pay_gld",140,164; end;
	case 15: warp "pay_gld",202,264; end;
	case 16: warp "prt_gld",141,64; end;
	case 17: warp "prt_gld",240,131; end;
	case 18: warp "prt_gld",153,133; end;
	case 19: warp "prt_gld",126,240; end;
	case 20: warp "prt_gld",195,240; end;
	}
	return;
}

// 鑰匙工匠 ( 又是黑錢的把戲 )
//============================================================ 
// - 持打怪得到的緊密金銀箱及商城購得的金銀箱鑰匙即可開箱
prontera,151,255,4	script	鑰匙工匠	824,{
	mes "[鑰匙工匠]";
	mes "歡迎你，";
	mes "你有帶來箱子和鑰匙嗎?";
	next;
	switch(select( "我帶來黃金箱子和黃金鑰匙:我帶來白銀箱子和白銀鑰匙:你說什麼箱子和鑰匙啊?")){
	case 1:
		setarray .@box[0],7777,7779,13726;
		break;
	case 2:
		setarray .@box[0],7778,7780,13727;
		break;
	case 3:
		mes "[鑰匙工匠]";
		mes "歡迎你，呵呵呵，";
		mes "我這老頭子正在負責開啟黃金箱子和黃金鑰匙及白銀箱子和白銀鑰匙的工作，";
		mes "這些箱子對於沒有特殊技術的人們而言是打不開的，";
		mes "你可以透過BOSS級魔物和公會寶物箱子來獲得黃金箱子和白銀箱子";
		next;
		mes "[鑰匙工匠]";
		mes "萬一你獲得了箱子和符合它的鑰匙時再帶來吧，";
		mes "那麼我會幫你打開，";
		mes "希望你能從那箱子中獲得稀有寶物囉~";
		mes "下次再見吧";
		close;
	}
	if(countitem(.@box[0]) && countitem(.@box[1])) {
		mes "[鑰匙工匠]";
		mes "乎噢~";
		mes "你真了不起，";
		mes "那應該不容易取得到吧，";
		mes "不管如何你把那箱子跟鑰匙拿給我看看，";
		mes "我會幫你打開";
		next;
		mes "　";
		mes " ========= 鏗鏗鏗鏗 =========";
		mes "　";
		mes " ========= 鏗鏗鏗鏗 =========";
		mes "　";
		mes " ========= 鏗鏗鏗鏗 =========";
		delitem .@box[0],1; //刪除 緊閉箱子
		delitem .@box[1],1; //刪除 鑰匙
		getitem .@box[2],1; //取得 箱子
		next;
		mes "[鑰匙工匠]";
		mes "好~打開了~";
		mes "祝你從那箱子中得到價值連城的寶物後發財吧~";
		close;
	}
	mes "[鑰匙工匠]";
	mes "你好奇怪喔，";
	mes "請再確認一下你現在手中是否都有"+ getitemname(.@box[0]) +"和"+ getitemname(.@box[1]) +"";
	mes "那麼等你確認完畢後再來吧";
	close;
}