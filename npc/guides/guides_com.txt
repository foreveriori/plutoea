// 克魔島導游腳本
// 
// 中文化作者:		Dino9021
// 
// 中文版本編號:	20041202.0.1
//
//============================================================

//===== eAthena Script ======================================= 
//= Comodo Guides
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena  0.5.2 +
//===== Description: ========================================= 
//=
//===== Additional Comments: ================================= 
//= Fully working
//= v1.1 Now using duplicate command.
//============================================================




// East -------------------------------------------------------------------------------------
comodo.gat,322,178,4	script	克魔島 導游#1::Comodo_Guide	724,{
	mes "[克魔島 導游]";
	mes "歡樂的夜晚，持續24小時的地方";
	mes "有夢，和幻想的村落!";
	next;
	mes "[克魔島 導游]";
	mes "歡迎來到克魔島!";
	mes "我會為你服務，幫你帶路.";
	mes "你想知道位置的話,隨時都可以問我.";
M_Menu:
	next;
	menu "開始由導游說明城市",M_1, "聽取注意事項",M_2, "取消",M_End;

	M_1:
		mes "[克魔島 導游]";
		mes "請問您需要要在微型地圖上留下記號嗎?";
		next;
		menu "不需要",sM_1a,"需要, 謝謝",sM_1b;
		
		sM_1a:
			set @COMPASS_CHECK,0;
			goto L_Cont;

		sM_1b:
			set @COMPASS_CHECK,1;

		L_Cont:
		mes "[克魔島 導游]";
		mes "請您選擇想知道的建筑物";
		next;
	sM_Menu:
		mes "[克魔島 導游]";
		menu	"^FF8000賭場^000000",sM_1, "^34B4A0呼啦圈舞臺^000000",sM_2, "裝備商店",sM_3, "道具商店",sM_4,
			"紀念品商店",sM_5, "^F1BE0E卡普拉 西部分社^000000",sM_6, "^FF5555村長之家^000000",sM_7, "酒店",sM_8,
			"^00B200露營村^000000",sM_9, "移除微型地圖上全部的記號",sM_10, "結束對話",M_End;

		sM_1:
			mes "克魔島夜生活的心臟!";
			mes "疲憊旅人的天堂!";
			mes "克魔島賭場 ~!";
			viewpoint 1,140,98,1,0xFF8000;
			goto sL_Wipe;
		sM_2:
			mes "呼啦 呼啦 - 呼啦 呼啦";
			next;
			mes "[克魔島 導游]";
			mes "若你已經習慣，舞臺上的氣氛的話,也可以考慮看看，轉職成舞者";
			mes "有興趣嗎? 呼啦圈舞 舞臺 - ^0000FF(舞者轉職地)^000000";
			viewpoint 1,188,168,2,0x34B4A0;
			goto sL_Wipe;
		sM_3:
			mes "克魔島里有克魔島的獨特的專案.";
			viewpoint 1,266,70,3,0xFF00FF;
			goto sL_Wipe;
		sM_4:
			mes "克魔島里有克魔島的獨特的專案";
			mes "'Comodo Tool Store'";
			viewpoint 1,86,128,4,0xFF00FF;
			goto sL_Wipe;
		sM_5:
			mes "克魔島里有克魔島的獨特的專案";
			mes "'Comodo Tourist Shop'";
			viewpoint 1,298,124,5,0x00FFFF;
			goto sL_Wipe;
		sM_6:
			mes "負責米德加茲大陸，西部";
			mes "世上最大的服務處!";
			mes "卡普拉在克魔島里的西部分社中心";
			viewpoint 1,136,202,6,0xF1BE0E;
			goto sL_Wipe;
		sM_7:
			mes "是克魔島村長，所在的地方";
			mes "你對克魔島有濃厚的興趣的話";
			mes "試著見一下村長.";
			viewpoint 1,114,294,7,0xFF5555;
			goto sL_Wipe;
		sM_8:
			mes "讓世界各地旅行者的情報，意見交換的地方";
			mes "克魔島，名俗酒店是更有特別氣氛的地方";
			viewpoint 1,166,298,8,0x6666FF;
			goto sL_Wipe;
		sM_9:
			mes "有時候和家人一起，有時候和戀人一起快樂的時光，讓你記憶猶新的地方";
			mes "烤肉 露營村 克魔島名俗料理中，有烤乳豬,這裡是唯一，可以品嘗的地方!";
			viewpoint 1,210,308,9,0x00B200;
			goto sL_Wipe;
		sM_10:
			set @COMPASS_CHECK, 2;

		sL_Wipe:
			if(@COMPASS_CHECK != 2) next;	// avoids a double 'next' when 'wipe indications' is chosen
			if(@COMPASS_CHECK == 1) goto sM_Menu;
			viewpoint 2,140,98,1,0xFF8000;
			viewpoint 2,188,168,2,0x34B4A0;
			viewpoint 2,266,70,3,0xFF00FF;
			viewpoint 2,86,128,4,0xFF00FF;
			viewpoint 2,298,124,5,0x00FFFF;
			viewpoint 2,136,202,6,0xF1BE0E;
			viewpoint 2,114,294,7,0xFF5555;
			viewpoint 2,166,298,8,0x6666FF;
			viewpoint 2,210,308,9,0x00B200;
			if (@COMPASS_CHECK == 0) goto sM_Menu;
			mes "所有的記號已經移除";
			close;

	M_2:
		mes "[克魔島 導游]";
		mes "我們采用了最現代化的數位指引方式, 無論您在哪裡都可以借由微型地圖知道您的位置.";
		mes "您們可以直接確認盧恩 米德加爾特王國魔法和科學技術組合后的廣網域測位系統的功能.";
		next;
		mes "[克魔島 導游]";
		mes "別忘了參考右上方的微型地圖";
		mes "如果看不見微型地圖的話 請按 ^0000ff'Ctrl+Tab'^000000 或是選取基本情報窗口內的 ^0000ff'Map'^000000 按鈕開啟地圖";
		mes "微型地圖出現之後 在其上的 ^ff0000+,-^000000 按鈕可讓您放大或縮小地圖顯示區域";
		goto M_Menu;

	M_End:
		mes "[克魔島 導游]";
		mes "克魔島位置在洞穴里.";
		mes "24小時照不到太陽";
		mes "一整天都是黑暗克魔島住民，利用這特性開發觀光商品旅游來維持生活. .";
		close;
}

// North ----------------------------------------------------------------------------------------
comodo.gat,176,350,4	duplicate(Comodo_Guide)	克魔島 導游#2	724
// West -----------------------------------------------------------------------------------------
comodo.gat,37,219,4	duplicate(Comodo_Guide)	克魔島 導游#3	724

