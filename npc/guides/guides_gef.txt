// 吉芬導游腳本
// 
// 中文化作者:		Dino9021
// 
// 中文版本編號:	20041202.0.1
//
//============================================================

//===== eAthena Script ======================================= 
//= Geffen Guides
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena  0.5.2 +
//===== Description: ========================================= 
//=
//===== Additional Comments: ================================= 
//= Fully working.  Added a guide at every exit.
//= v1.1 Now using duplicate command.
//============================================================







// North --------------------------------------------
geffen.gat,123,202,5	script	導游#1::Guide	705,{
	cutin "gef_soldier",2;
	mes "[吉芬 導游]";
	mes "歡迎光臨 魔幻之都 吉芬";
	next;
	mes "[吉芬 導游]";
	mes "我們為了來到吉的旅客安排了導游為您們介紹這個城市, 當您們有問題的時候可以隨時詢問他們.";
	mes "第一次來到這裡的旅客建議先聽取注意事項, 了解必須注意的事情.";
M_Menu:
	next;
	menu "開始由導游說明城市",M_1, "聽取注意事項",M_2, "取消",M_End;

	M_1:
		mes "[吉芬 導游]";
		mes "請問您需要要在微型地圖上留下記號嗎?";
		next;
		menu "不需要",sM_1a,"需要, 謝謝",sM_1b;
		
		sM_1a:
			set @COMPASS_CHECK,0;
			goto L_Cont;

		sM_1b:
			set @COMPASS_CHECK,1;

		L_Cont:
		mes "[吉芬 導游]";
		mes "請您選擇想知道的建筑物";
		next;
	sM_Menu:
		menu 	"^FF0000魔法學院^000000",M_1_1, "^00A500鐵匠工坊^000000",M_1_2, "^4444FF吉分塔^000000",M_1_3,
			"裝備商店",M_1_4, "道具商店",M_1_5, "酒店",M_1_6, "旅館",M_1_7, "移除微型地圖上全部的記號",M_1_8, "取消", M_End;

		M_1_1:
			mes "[吉芬 導游]";
			mes "'魔法學院' 是魔法理論的發源地, 也是轉職為魔法師的地方.";
			viewpoint 1,61,180,1,0xFF5555;
			goto sL_Wipe;
		M_1_2:
			mes "[吉芬 導游]";
			mes "'鐵匠工坊' 位在吉芬塔東南方. 是商人轉職為鐵匠的地方.";
			mes "您也可以在那兒強化您的武器及防具.";
			viewpoint 1,182,59,2,0x44FF44;
			goto sL_Wipe;
		M_1_3:
			mes "[吉芬 導游]";
			mes "'吉芬塔' 的底層有許多魔物. 人們常去那兒訓練自己.";
			mes "頂樓則是 ^0000FF'巫師公會'^000000. 魔法師可在哪裡轉職成巫師.";
			viewpoint 1,120,120,3,0x5555FF;
			goto sL_Wipe;
		M_1_4:
			mes "[吉芬 導游]";
			mes "在裝備商店可以買到武器和防具";
			viewpoint 1,99,140,4,0xFF00FF;
			goto sL_Wipe;
		M_1_5:
			mes "[吉芬 導游]";
			mes "'道具商店' 提供各種輔助道具, 如藥水或蒼蠅翅膀.";
			viewpoint 1,44,86,5,0xFF00FF;
			goto sL_Wipe;
		M_1_6:
			mes "[吉芬 導游]";
			mes "'酒店' 是一個認識新朋友與玩樂的絕佳地點.";
			viewpoint 1,138,138,6,0xF0C40F;
			goto sL_Wipe;
		M_1_7:
			mes "[吉芬 導游]";
			mes "在 '旅館' 您可以得到足夠的休息.";
			viewpoint 1,172,174,7,0xFF8000;
			goto sL_Wipe;
		M_1_8:
			set @COMPASS_CHECK, 2;

		sL_Wipe:
			if(@COMPASS_CHECK != 2) next;	// avoids a double 'next' when 'wipe indications' is chosen
			if(@COMPASS_CHECK == 1) goto sM_Menu;
			viewpoint 2,61,180,1,0xFF5555;
			viewpoint 2,182,59,2,0x44FF44;
			viewpoint 2,120,120,3,0x5555FF;
			viewpoint 2,99,140,4,0xFF00FF;
			viewpoint 2,44,86,5,0xFF00FF;
			viewpoint 2,138,138,6,0xF0C40F;
			viewpoint 2,172,174,7,0xFF8000;
			if (@COMPASS_CHECK == 0) goto sM_Menu;
			mes "[吉芬 導游]";
			mes "所有的記號已經移除";
			cutin "gef_soldier",255;
			close;

	M_2:
		mes "[吉芬 導游]";
		mes "我們采用了最現代化的數位指引方式, 無論您在哪裡都可以借由微型地圖知道您的位置.";
		mes "您們可以直接確認盧恩 米德加爾特王國魔法和科學技術組合后的廣網域測位系統的功能.";
		next;
		mes "[吉芬 導游]";
		mes "別忘了參考右上方的微型地圖";
		mes "如果看不見微型地圖的話 請按 ^0000ff'Ctrl+Tab'^000000 或是選取基本情報窗口內的 ^0000ff'Map'^000000 按鈕開啟地圖";
		mes "微型地圖出現之後 在其上的 ^ff0000+,-^000000 按鈕可讓您放大或縮小地圖顯示區域";
		goto M_Menu;

	M_End:
		mes "[吉芬 導游]";
		mes "祝您有個愉快的一天!";
		cutin "gef_soldier",255;
		close;
}

// South ------------------------------------------
geffen.gat,118,62,0	duplicate(Guide)	導游#2	705
// East -----------------------------------------------------
geffen.gat,203,116,2	duplicate(Guide)	導游#3	705
// West ----------------------------------------------
geffen.gat,37,123,5	duplicate(Guide)	導游#4	705
