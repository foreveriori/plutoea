// 艾爾貝塔導遊腳本
// 
// 中文化作者:		Dino9021
// 
// 中文版本編號:	20041202.0.1
//
//============================================================

//===== eAthena Script ======================================= 
//= Alberta Guides
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena  0.5.2 +
//===== Description: ========================================= 
//=
//===== Additional Comments: ================================= 
//= Fully working
//= v1.1 Now using duplicate command.
//============================================================ 




// North --------------------------------------------------------
alberta.gat,23,238,4	script	導遊#1::Alb_Guide	105,{
	cutin "prt_soldier",2;
	mes "[艾爾貝塔 導遊]";
	mes "歡迎光臨 港口之都 艾爾貝塔!";
	next;
	mes "[艾爾貝塔 導遊]";
	mes "我們為了來到艾爾貝塔的旅客安排了導遊為您們介紹這個城市, 當您們有問題的時候可以隨時詢問他們.";
	mes "第一次來到這裡的旅客建議先聽取注意事項, 瞭解必須注意的事情.";
M_Menu:
	next;
	menu "開始由導遊說明城市",M_1, "聽取注意事項",M_2, "取消",M_End;

	M_1:
		mes "[艾爾貝塔 導遊]";
		mes "請問您需要要在微型地圖上留下記號嗎?";
		next;
		menu "不需要",sM_1a,"需要, 謝謝",sM_1b;
		
		sM_1a:
			set @COMPASS_CHECK,0;
			goto L_Cont;
		sM_1b:
			set @COMPASS_CHECK,1;

		L_Cont:
		mes "[艾爾貝塔 導遊]";
		mes "請您選擇想知道的建築物";
		next;
	sM_Menu:
		menu "^FF0000商人公會^000000",sM_2a,"裝備商店",sM_2b,"道具商店",sM_2c,"旅館",sM_2d, "刪除微型地圖上全部的記號",sM_2e, "取消",M_End;

		sM_2a:
			mes "[艾爾貝塔 導遊]";
			mes "商人公會座落於城市的西南方, 專門辦理商人轉職業務.";
			viewpoint 1,33,41,1,0xFF0000;
			goto sL_Wipe;
		sM_2b:
			mes "[艾爾貝塔 導遊]";
			mes "裝備商店座落於城市的南方. 在那兒可以買到武器和防具.";
			viewpoint 1,148,57,2,0x6666FF;
			goto sL_Wipe;
		sM_2c:
			mes "[艾爾貝塔 導遊]";
			mes "道具商店座落於城市的中心";
			viewpoint 1,98,154,3,0xFF00FF;
			goto sL_Wipe;
		sM_2d:
			mes "[艾爾貝塔 導遊]";
			mes "旅館有兩間, 分別位於城市的北方與南方.";
			viewpoint 1,65,233,4,0xF5AD05;
			viewpoint 1,136,38,5,0xF5AD05;
			goto sL_Wipe;
		sM_2e:
			set @COMPASS_CHECK, 2;

		sL_Wipe:
			if(@COMPASS_CHECK != 2) next;
			if(@COMPASS_CHECK == 1) goto sM_Menu;
			viewpoint 2,33,41,1,0xFF0000;
			viewpoint 2,148,57,2,0x6666FF;
			viewpoint 2,98,154,3,0xFF00FF;
			viewpoint 2,65,233,4,0xF5AD05;
			viewpoint 2,136,38,5,0xF5AD05;
			if(@COMPASS_CHECK == 0) goto sM_Menu;
			mes "[艾爾貝塔 導遊]";
			mes "所有的記號已經刪除";
			cutin "prt_soldier",255;
			close;

	M_2:
		mes "[艾爾貝塔 導遊]";
		mes "我們採用了最現代化的數位指引方式, 無論您在哪裡都可以借由微型地圖知道您的位置.";
		mes "您們可以直接確認盧恩 米德加爾特王國魔法和科學技術組合後的廣網域測位系統的功能.";
		next;
		mes "[艾爾貝塔 導遊]";
		mes "別忘了參考右上方的微型地圖";
		mes "如果看不見微型地圖的話 請按 ^0000ff'Ctrl+Tab'^000000 或是選取基本情報窗口內的 ^0000ff'Map'^000000 按鈕開啟地圖";
		mes "微型地圖出現之後 在其上的 ^ff0000+,-^000000 按鈕可讓您放大或縮小地圖顯示區域";
		goto M_Menu;

	M_End:
		mes "[艾爾貝塔 導遊]";
		mes "祝您好運, 再見!";
		cutin "prt_soldier",255;
		close;
}

// South --------------------------------------------------------
alberta.gat,120,60,3	duplicate(Alb_Guide)	導遊#2	105
