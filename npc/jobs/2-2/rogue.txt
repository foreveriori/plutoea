// 流氓轉職腳本
// 
// 中文化作者:		Dino9021
//
// 版權宣告:		本文件由 eAthenaC 論壇提供，腳本結構屬原作者所有，翻譯及修正之版權屬 Dino9021 所有
//			轉載請勿移除任何說明敘述與說明文件，並需註明論壇連結 http://eathena.twbbs.org
// 
// 中文版本編號:	20041118.0.2
//			20050210.0.1	移除傳點設定 (於 npc\warps\other\jobquests.txt 中設定)
//			20050408.0.1	修正一些 精練 vs 精煉 的錯別字問題，統一為 精煉
//			20050409.0.1	修正一些 轉移到別處 vs 轉移別處去 的不相同問題，統一為 轉移到別處
//			20050418.0.1	與 eAthena 1.1 對應，修正沒有檢查技能點數點完了沒有的問題
//			20050420.0.1	修改職業判斷使用 eAthena 之變數，轉職命令改用 eAthena 之轉職函式
//
// 參考腳本:		本腳本參考 IRO 轉職程序編寫
//
// 注:			因腳本敘述有遺漏故與官方任務有一些不同
//
//============================================================

//===== eAthena Script ======================================= 
//= Rogue Job Quest
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0 +
//===== Description: ========================================= 
//= Rogue job quest based off of official iRO Rogue quest.
//= There are some differences from official quest due to scripting issues.
//= Missing some message text.  
//===== Additional Comments: ================================= 
//= 1.1 fixed 3 wrong questions, added missing lines [Lupus]
//= 1.3 Baby Class Support + 40/50 JobLevel Item fix [Lupus]
//============================================================ 


//=======================================================================================================//
//					Markie: Test 1 and Jobchange						//
//***********************************************************************************************************************************************************//
in_rogue.gat,363,123,3	script	瑪凱	747,{

	mes "[瑪凱]";
	if(BaseJob == Job_Thief) goto L_Thief;
	if(BaseJob == Job_Rogue) goto L_Rogue;

L_JobOther:
	mes "我不知道你是怎麼找到這個地方的，但我建議你最好趕快離開..........";
	close;

L_Rogue:
	mes "很高興又見到你了，當流氓很有趣吧! 呵呵呵....";
	close;

L_Thief:
	if(JobLevel >= 40) goto L_Start;
	mes "噢! 你好! 如果你想成為一個流氓的話我想你得再多加訓練才行.";
	mes "只有職業等級在 40 以上的盜賊才能轉職成流氓.";
	close;

HighRog:
	mes "[瑪凱]";
	mes "哎呀! 你已經是上位盜賊了呢!";
	mes "很抱歉! 我沒有辦法讓你轉職成流氓!";
	mes "請你去找追跡者轉職吧!";
	mes "那才是適合你的職業!";
	close;

L_Start:
	if(ROGUE_Q == 1) goto L_ReTest;
	if(ROGUE_Q == 2) goto L_Test2;
	if(ROGUE_Q == 3) goto L_Test3;
	if(ROGUE_Q == 4) goto L_Test4;
	if(ROGUE_Q == 5) goto L_Change;
	mes "啊? 什麼風把你吹來的? 噢! 這樣啊... 你想成為一個流氓是吧?";
	next;
	menu "是的",-,"不是",L_End;

	mes "[瑪凱]";
	if (((BaseJob == Job_Thief) && (Upper == 1)) || (ReBirth > 0)) goto HighRog;
	mes "很高興認識你，我的名字是瑪凱，你呢?";
	next;
	mes "[瑪凱]";
	mes strcharinfo(0) + "..... 呵呵，你的名字真好聽. 對了，你為什麼會想成為一名流氓呢?";
	next;
	mes "[瑪凱]";
	mes "不過，既然你這麼誠實地告訴我你的真實名字，我想那也不重要了.";
	mes "不過為了你的將來著想，流氓是絕對不會向任何人透露他的真實身份的.";
	next;
	mes "[瑪凱]";
	mes "這是流氓守則第一條，從今以後你最好牢牢記住這一點.";
	mes "嗯，那麼，這裡有一些表格需要你你填寫一些必備的資料.....";
	next;
	mes "(你填寫了一些表格並把他交給瑪凱)";
	set JBLVL, 40;			// used to determine what item to get at the end
	if(JobLevel == 50) set JBLVL, 50;	// used to determine what item to get at the end
	next;
	mes "[瑪凱]";
	mes "嗯.... 看起來沒什麼問題，那麼我就接受你的申請.";
	mes "為了保險起見，我要先對你做一些測試";
	next;
	mes "[瑪凱]";
	mes "噢! 你不用緊張... 我只是想知道你對流氓瞭解多少而已.";
	mes "我對每一個來申請的人都會作測試的，那麼，可以開始了嗎?";
	next;
	menu "開始吧",L_Test1, "等等...我還需要準備一下",-;

		mes "[瑪凱]";
		mes "好的，放心地去準備吧，等你準備好之後再回來找我.";
		close;


//=================================================================
L_Test1:
//=======
	mes "[瑪凱]";
	mes "現在開始我會問你一些問題，聽清楚題目然後把你認為最正確的答案告訴我，瞭解嗎?";
	next;
	savepoint "in_rogue.gat",366,114;
	set @score, 0;
	set ROGUE_Q, 1;
	set @temp, rand(1,3);
	if(@temp == 2) goto L_QSet2;
	if(@temp == 3) goto L_QSet3;

	L_QSet1:
		mes "[瑪凱]";
		mes "1) 當盜賊的殘影技能練到最高十時，所上升的迴避率是多少?";
		next;
		menu "30",-, "40",sM1_1, "160",sM1_1, "20",sM1_1;

			set @score, @score + 10;

		sM1_1:

		mes "[瑪凱]";
		mes "2) 下列哪一隻魔物可以看到，隱匿/偽裝的盜賊?";
		next;
		menu "艾斯蜘蛛",-, "森靈",sM1_2, "木乃伊",sM1_2, "邪骸士兵",sM1_2;

			set @score, @score + 10;

		sM1_2:

		mes "[瑪凱]";
		mes "3) 下列有流氓轉職所的地方是?";
		next;
		menu "克魔島",sM1_3, "剋剋魔海邊",sM1_3, "發樂斯 燈塔島",-, "夢羅克",sM1_3;

			set @score, @score + 10;

		sM1_3:

		mes "[瑪凱]";
		mes "4) 有盜賊轉職所的村落是?";
		next;
		menu "克魔島",sM1_4, "薑餅城",sM1_4, "愛爾貝塔",sM1_4, "夢羅克",-;

			set @score, @score + 10;

		sM1_4:

		mes "[瑪凱]";
		mes "5) 與靈巧(Dex)沒有關係的卡片是";
		next;
		menu "蝗蟲卡",sM1_5, "木乃伊卡",-, "傑洛米卡",sM1_5, "土波利卡",sM1_5;

			set @score, @score + 10;

		sM1_5:

		mes "[瑪凱]";
		mes "6) 請選擇流氓比其它職業有吸引人的地方?";
		next;
		menu "卓越的壞氣息",sM1_6, "卓越的狡猾",sM1_6, "卓越的厚顏無恥",sM1_6, "卓越的攻擊力",-;

			set @score, @score + 10;

		sM1_6:

		mes "[瑪凱]";
		mes "7) 盜賊的職業等級要達到幾級，才可以轉職成流氓?";
		next;
		menu "30",sM1_7, "35",sM1_7, "40",-, "50",-;

			set @score, @score + 10;

		sM1_7:

		mes "[瑪凱]";
		mes "8) 想要把頭髮染成藍色，請問帶著藍色顏料要到哪裡去?";
		next;
		menu 	"夢羅克，7點鐘方向建築裡",sM1_8, "普隆德拉，7點鐘方面建築裡",-,
			"夢羅克，5點鐘方向建築裡",sM1_8, "普隆德拉，1點鐘方向建築裡",sM1_8;

			set @score, @score + 10;

		sM1_8:

		mes "[瑪凱]";
		mes "9) 初心者要轉職為盜賊時，需要哪一種蘑菇?";
		next;
		menu "惡臭香菇",-, "紅毛黏呼呼香菇",sM1_9, "毒香菇",-, "紅香菇",sM1_9;

			set @score, @score + 10;

		sM1_9:

		mes "[瑪凱]";
		mes "10) 和流氓沒有關連的卡片是?";
		next;
		menu "白幽靈卡片",sM1_10, "長老樹精卡片",-, "傑洛米卡片",sM1_10, "黑狐卡片",sM1_10;

			set @score, @score + 10;

		sM1_10:
		goto L_Score;


	L_QSet2:
		mes "[瑪凱]";
		mes "1) 學習流氓的潛遁技能時，所需要的技能是?";
		next;
		menu "隱匿",-, "偷竊",sM2_1, "殘影",sM2_1, "狂擊",sM2_1;

			set @score, @score + 10;

		sM2_1:

		mes "[瑪凱]";
		mes "2) 流氓的技能， '強制減價'物品的折價率，比商人低價買進等級10的時候，多幾%?";
		next;
		menu "3%",sM2_2, "2%",sM2_2, "1%",-, "0%",sM2_2;

			set @score, @score + 10;

		sM2_2:

		mes "[瑪凱]";
		mes "3) '偷錢' 技能的作用是?";
		next;
		menu "從玩家身上偷物品",sM2_3, "從魔物身上偷物品",sM2_3, "從魔物身上偷金幣",-, "從玩家身上偷金幣",sM2_3;

			set @score, @score + 10;

		sM2_3:

		mes "[瑪凱]";
		mes "4) 使用 '流氓天國' 時所需要的人數是?";
		next;
		menu "1 流氓 + 2 刺客",sM2_4, "2 流氓 + 1 盜賊",sM2_4, "4 盜賊",sM2_4, "2 個或 2 個以上的流氓",-;

			set @score, @score + 10;

		sM2_4:

		mes "[瑪凱]";
		mes "5) 卸除頭盔技能Lv5時，所出現的技能是?";
		next;
		menu "施毒",sM2_5, "脫衣舞",sM2_5, "毒性感染",sM2_5, "卸除盾牌",-;

			set @score, @score + 10;

		sM2_5:

		mes "[瑪凱]";
		mes "6) 下列哪一種是可以在隱匿的狀態下移動?";
		next;
		menu "隱匿",sM2_6, "後退迴避",sM2_6, "潛遁",-, "噴砂",sM2_6;

			set @score, @score + 10;

		sM2_6:

		mes "[瑪凱]";
		mes "7) 下列哪一張卡片，可以發揮提升攻擊命中率的卡片是?";
		next;
		menu "白蟻卡片",sM2_7, "吸血蝙蝠卡片",sM2_7, "木乃伊卡片",-, "水母卡片",sM2_7;

			set @score, @score + 10;

		sM2_7:

		mes "[瑪凱]";
		mes "8) 使用螃蟹卡片時，對下列哪一種魔物的攻擊較有效率?";
		mes "(對火屬性魔物增加 20% 的傷害力)";
		next;
		menu 	"螃蟹",sM2_8, "小惡魔",sM2_8, "長老樹精",-, "巴風特",sM2_8;

			set @score, @score + 10;

		sM2_8:

		mes "[瑪凱]";
		mes "9) 用短劍練二刀連擊時，所消耗的SP量是?";
		next;
		menu "消耗 15 SP",sM2_9, "不需要消耗 SP",-, "消耗 10 SP",sM2_9, "消耗 54 SP",sM2_9;

			set @score, @score + 10;

		sM2_9:

		mes "[瑪凱]";
		mes "10) 在依斯魯得島密穴裡，有效的屬性劍是什麼?";
		next;
		menu "風屬性笨拙短劍",-, "水屬性笨拙短劍",sM2_10, "地屬性笨拙短劍",sM2_10, "火屬性笨拙短劍",sM2_10;

			set @score, @score + 10;

		sM2_10:
		goto L_Score;


	L_QSet3:
		mes "[瑪凱]";
		mes "1) 有洞的長柄短劍可以從哪一隻魔物身上獲得?";
		next;
		menu "小盜蟲",sM3_1, "大嘴鳥",sM3_1, "沙漠之狼",sM3_1, "犬妖",-;

			set @score, @score + 10;

		sM3_1:

		mes "[瑪凱]";
		mes "2) 有洞的笨拙短劍可以從哪一隻魔物身上獲得?";
		next;
		menu "蜂兵",-, "沙漠之狼",sM3_2, "傀儡娃娃",sM3_2, "麥斯特",sM3_2;

			set @score, @score + 10;

		sM3_2:

		mes "[瑪凱]";
		mes "3) 哪一個職業可以製作藥水?";
		next;
		menu "商人",sM3_3, "煉金術士",-, "鐵匠",sM3_3, "牧師",sM3_3;

			set @score, @score + 10;

		sM3_3:

		mes "[瑪凱]";
		mes "4) 下列哪一項是流氓不能使用的武器?";
		next;
		menu "角弓",sM3_4, "十字弓",sM3_4, "長柄短劍",sM3_4, "卡塔勒拳刃",-;

			set @score, @score + 10;

		sM3_4:

		mes "[瑪凱]";
		mes "5) 蚯蚓的屬性是?";
		next;
		menu "水屬性",sM3_5, "火屬性",sM3_5, "風屬性",sM3_5, "地屬性",-;

			set @score, @score + 10;

		sM3_5:

		mes "[瑪凱]";
		mes "6) 下列哪個魔物不能飼養成寵物?";
		next;
		menu "波波利",sM3_6, "克瑞米",-, "獸人戰士",sM3_6, "毒魔菇",sM3_6;

			set @score, @score + 10;

		sM3_6:

		mes "[瑪凱]";
		mes "7) 火屬性短劍對哪一種魔物最有效?";
		next;
		menu "刺刀哥布靈",sM3_7, "刺錘哥布靈",sM3_7, "鏈錘哥布靈",sM3_7, "鎯頭哥布靈",-;

			set @score, @score + 10;

		sM3_7:

		mes "[瑪凱]";
		mes "8) 下列哪一個村落沒有設置工會城堡?";
		next;
		menu 	"普隆德拉",sM3_8, "艾爾帕蘭",sM3_8, "艾爾貝塔",-, "斐揚",sM3_8;

			set @score, @score + 10;

		sM3_8:

		mes "[瑪凱]";
		mes "9) 下列哪一種草會給藍色藥草?";
		next;
		menu "綠草",sM3_9, "黃草",sM3_9, "藍草",-, "光芒草",-;

			set @score, @score + 10;

		sM3_9:

		mes "[瑪凱]";
		mes "10) 下列哪一種不是不死系魔物?";
		next;
		menu "腐屍",sM3_10, "邪骸食人魚",sM3_10, "吸血蝙蝠",-, "卡利斯格",sM3_10;

			set @score, @score + 10;

		sM3_10:
		goto L_Score;

	L_Score:
		mes "[瑪凱]";
		mes "結束! 這些就是我所要問的問題，不算太難吧?";
		mes "那麼，我要開始檢查你的答案了.";
		next;
		mes "[瑪凱]";
		mes "我喜歡為來申請的人打分數，這樣他們才知道他們的知識有多少.";
		mes "嗯...你的分數是: ^FF3355" + @score +"^000000/100............";
		next;
		mes "[瑪凱]";
		if(@score< 90) goto sL_Failed;
		if(@score<100) goto sL_Passed;

		sL_Perfect:
			mes "噢! 滿分呢! 你真有作流氓的資質.....";
			mes "但別自得意滿，因為後面還有一些其他的測驗等著你呢.....";
			next;
			mes "[瑪凱]";
			set ROGUE_Q, 2;
			goto L_Test2;
		sL_Passed:
			mes "很好，很好! 你做得不錯. 不過後面還有一些其他的測驗等著你呢.....";
			next;
			mes "[瑪凱]";
			set ROGUE_Q, 2;
			goto L_Test2;
		sL_Failed:
			mes "唉..... 這不是我可以接受的分數......";
			mes "我還能說什麼好呢? 這種程度的知識水準是絕對不能作一個流氓的..........";
			emotion 4;
			next;
			mes "[瑪凱]";
			mes "你還是再多去學習學習吧... 你很需要訓練吶..... 真是浪費我的時間.....";
			close;

L_ReTest:
	mes "你又來啦? 這次你準備好了嗎?...... 嗯，放輕鬆一點....";
	next;
	goto L_Test1;

L_Test2:
	mes "你的下一個測驗要去找 ^5533FF司馬斯拉托^000000.";
	mes "我要先警告你，司馬斯拉托是很個難取悅的人. 所以，注意你的言行舉止.";
	close;
L_Test3:
	mes "我知道那些傢伙實在很難找到. 但要有耐心地尋找. 同時確認一下你記得正確的密語..";
	close;
L_Test4:
	mes "你在這裡做什麼? 該不會是想要放棄最後的測驗吧?.........";
	emotion 1;
	next;
	mes "[瑪凱]";
	mes "你快要完成所有的測驗了，加油吧! 你一定可以辦到的!";
	close;


//=======================================================================
L_Change:
//===========
	mes "噢! 你活著回來了啊?看來你通過了所有的測驗了.";
	mes "你證明了你已經準備好要成為一名流氓!";
	emotion 5;
	next;
	if(SkillPoint != 0) goto L_SkillP;
	mes "[瑪凱]";
	mes "呵呵! 恭喜你成為一名流氓，你展現了你努力的精神，所以我要給你一個小禮物......";
	setoption 0;
	setlook 7,0;
	callfunc "Job_Change",Job_Rogue;
	emotion 46;
	next;
	mes "[瑪凱]";
	mes "你現在是一名流氓了，你可以隨心所欲到任何地方去，做任何你想做的事.";
	if(JBLVL == 40) getitem 1219,1;		// 2 slott gladius
	if(JBLVL == 50) getitem 1220,1;		// 3 slott gladius
	callfunc "F_ClearJobVar";
	next;
	mes "[瑪凱]";
	mes "但請記得，自由是要付出代價的! 待人要有同理心!";
	next;
L_End:
	mes "[瑪凱]";
	mes "祝你好運，再見!";
	close;

//=======================================================================
L_SkillP:
//===========
	mes "[瑪凱]";
	mes "噢! 不過有件事, 親愛的... 你還有些技能沒學完吧?!";
	mes "請你先把你該學習的課程學完後再回來吧! 好嗎?";
	close;
}



//=======================================================================================================//
//					司馬斯拉托: Tests 2 and 3						//
//***********************************************************************************************************************************************************//
in_rogue.gat,376,23,3	script	司馬斯拉托	57,{

	mes "[司馬斯拉托]";
	if(BaseJob == Job_Thief) goto L_Thief;
	if(BaseJob == Job_Rogue) goto L_Rogue;

L_OtherJob:
	mes "走開! 我很忙.....";
	close;
L_Rogue:
	mes "我的流氓朋友啊，你看起來氣色不錯!";
	close;
L_Thief:
	if(ROGUE_Q == 2) goto L_Test2;
	if(ROGUE_Q == 3) goto L_Test3;
	if(ROGUE_Q > 3) goto L_Done;
	mes "好 好 .... 1... 2.... 3.... 那是在... 哪裡.... 夢羅克... 吉分.... 克魔島??.......";
	next;
	mes "[司馬斯拉托]";
	mes "唔..... 頭好痛.......";
	next;
	mes "[司馬斯拉托]";
	mes "啊? 你是誰? 如果你想成為一名流氓的話你最好去找 ^5533FF瑪凱^000000....";
	next;
	mes "[司馬斯拉托]";
	mes "...... 可惡! 我忘了我數到哪裡了!!";
	emotion 16;
	close;

//----------------------------------------------------------------------------------------------------
L_Test2:
//--------------
	if(ROGUE_Q2 > 0) goto L_Check2;
	mes "你是來這裡參加第二階段測驗的是吧.... 嗯，很好.......";
	next;
	mes "[司馬斯拉托]";
	mes "在開始測驗之前，先告訴你一些有關測試費用的事情";
	mes "我們流氓工會有許多事務需要執行，所以我們需要固定的基金.";
	next;
	mes "[司馬斯拉托]";
	mes "這就是為什麼我們需要向每位來申請流氓轉職考試的人收取 ^5533FF10,000 zeny^000000 的費用.";
	mes "這些費用會在你完成第二階段測驗的時候才向你收取，所以假使你現在沒有錢，也可以放心參加測驗";
	next;
	mes "[司馬斯拉托]";
	mes "好了，至於你的測驗嘛..... 請你先去搜集下列物品:";
	next;
	mes "[司馬斯拉托]";
	set ROGUE_Q2, rand(1,3);
	if(ROGUE_Q2 == 1) callsub sF2_R1;
	if(ROGUE_Q2 == 2) callsub sF2_R2;
	if(ROGUE_Q2 == 3) callsub sF2_R3;
	next;
	mes "[司馬斯拉托]";
	mes "等你搜集完了請記得連同 10,000 zeny 一起帶過來給我.";
	close;

	sF2_R1:
		mes "^5533FF藍色藥草 6 片";
		mes "死人遺骨 10 個";
		mes "亡者指甲 10 個";
		mes "亡者牙齒 10 個^000000.";
		return;
	sF2_R2:
		mes "^5533FF綠色藥草 10 片";
		mes "蛇鱗 10 個";
		mes "加勒結晶 10 個";
		mes "螃蟹外殼 10 個^000000";
		return;
	sF2_R3:
		mes "^5533FF黃色藥草 10 片";
		mes "熊掌 10 個";
		mes "堅硬外殼 10 個";
		mes "蝗蟲後腿 10 個^000000.";
		return;

L_Check2:
	set @count, 0;
	if(ROGUE_Q2 == 2) goto L_2;
	if(ROGUE_Q2 == 3) goto L_3;

	L_1:
		set @R, 1;
		if(countitem(510)<6 || countitem(932)<10 || countitem(957)<10 || countitem(958)<10 || Zeny < 10000) goto L_NotEnuf;
		delitem 510,6;
		delitem 932,10;
		delitem 957,10;
		delitem 958,10;
		goto L_Done2;

	L_2:
		set @R, 2;
		if(countitem(511)<10 || countitem(910)<10 || countitem(926)<10 || countitem(964)<10 || Zeny < 10000) goto L_NotEnuf;
		delitem 511,10;
		delitem 910,10;
		delitem 926,10;
		delitem 964,10;
		goto L_Done2;

	L_3:
		set @R, 3;
		if(countitem(508)<10 || countitem(948)<10 || countitem(935)<10 || countitem(940)<10 || Zeny < 10000) goto L_NotEnuf;
		delitem 508,10;
		delitem 948,10;
		delitem 935,10;
		delitem 940,10;

	L_Done2:
		mes "噢! 讓我瞧瞧.... 申請費用.... 物品...... 嗯，都沒問題，你通過了第二階段測驗啦.";
		next;
		mes "[司馬斯拉托]";
		mes "等你準備好迎接下一階段測驗的時候再來找我.";
		set Zeny, Zeny - 10000;
		set ROGUE_Q, 3;
		set ROGUE_Q2, 0;
		close;

	L_NotEnuf:
		mes ".... 啥!?!? 東西沒帶齊全你回來幹嘛? 忘記要搜集哪些物品了嗎?.....";
		emotion 1;
		next;
		mes "[司馬斯拉托]";
		mes "拿紙筆寫下來吧! 總共需要 ^5533FF10,000^000000 zeny 以及下列物品:";
		if(ROGUE_Q2 == 1) callsub sF2_R1;
		if(ROGUE_Q2 == 2) callsub sF2_R2;
		if(ROGUE_Q2 == 3) callsub sF2_R3;
		next;
		mes "[司馬斯拉托]";
		mes "寫好了沒? 東西沒帶齊全不要回來找我......";
		emotion 32;
		close;


//----------------------------------------------------------------------------------------------
L_Test3:
//---------------
	if(ROGUE_Q2 > 0) goto L_Check3;
	mes "讓我瞧瞧..... 叫你去搜集什麼好........ 嗯.........";
	emotion 20;
	next;
	mes "[司馬斯拉托]";
	mes "噢! 我知道了! 就這麼辦，之前的測驗我叫你去搜集了一些物品，這次我要你去找個特殊的人.";
	emotion 5;
	next;
	mes "[司馬斯拉托]";
	set ROGUE_Q2, rand(1,3);
	if(ROGUE_Q2 == 1) callsub sF3_R1;
	if(ROGUE_Q2 == 2) callsub sF3_R2;
	if(ROGUE_Q2 == 3) callsub sF3_R3;
	next;
	mes "[司馬斯拉托]";
	mes "祝你好運. 盡快完成這階段的測試. 短時間內完成任務也是流氓的特點喔!";
	close;

	sF3_R1:
		mes "你去找 ^5533FF阿拉甘^000000 吧.";
		next;
		mes "[司馬斯拉托]";
		mes "^5533FF東邊地圖的西南方^000000 那片沙漠上";
		mes "有一間小房子，他就住在那兒";
		next;
		mes "[司馬斯拉托]";
		mes "他是一個不錯的傢伙，他很努力地在做他的討債工作";
		mes "不過有一些人卻因為他父親以前做過的一些事情而想殺他";
		next;
		mes "[司馬斯拉托]";
		mes "當他加入流氓工會之後，他決定隱居在他現在所住的地方";
		mes "除了流氓工會的人以外沒有人知道他住在哪兒.";
		next;
		mes "[司馬斯拉托]";
		mes "這裡有一段密語，牢牢記住，當你見到他的時候把密語告訴他.";
		mes "密語: '^5533FF阿拉甘 並沒有把 精煉物品 轉移到別處^000000'";
		return;
	sF3_R2:
		mes "你去找 ^5533FF豪格藍^000000 吧.";
		next;
		mes "[司馬斯拉托]";
		mes "^5533FF東邊地圖的東南方^000000 那片沙漠上";
		mes "有一間小房子，他就住在那兒";
		next;
		mes "[司馬斯拉托]";
		mes "除了流氓工會的人以外沒有人知道他住在哪兒.";
		next;
		mes "[司馬斯拉托]";
		mes "這裡有一段密語，牢牢記住，當你見到他的時候把密語告訴他.";
		mes "密語: '^5533FF我爸爸 並沒有把 精煉物品 轉移到別處^000000'";
		return;
	sF3_R3:
		mes "你去找 ^5533FF安童尼奧^000000 吧.";
		next;
		mes "[司馬斯拉托]";
		mes "^5533FF西北邊地圖的東方^000000 克魔島的沙灘上";
		mes "有一間小屋子，他就住在那兒";
		next;
		mes "[司馬斯拉托]";
		mes "除了流氓工會的人以外沒有人知道他住在哪兒.";
		next;
		mes "[司馬斯拉托]";
		mes "這裡有一段密語，牢牢記住，當你見到他的時候把密語告訴他.";
		mes "密語: '^5533FF安童尼奧 並不喜歡 打碎 精煉物品^000000'";
		return;


L_Check3:
	mes "什麼!?! 你忘記你要去哪裡?........";
	emotion 1;
	next;
	mes "[司馬斯拉托]";
	mes "拜託!!! 你不會拿紙筆記下來喔? 笨死了......";
	emotion 6;
	next;
	mes "[司馬斯拉托]";
	if(ROGUE_Q2 == 1) callsub sF3_R1;
	if(ROGUE_Q2 == 2) callsub sF3_R2;
	if(ROGUE_Q2 == 3) callsub sF3_R3;
	close;

//-------------------------------------------------------------------------------------------------
L_Done:
//----------------
	mes "這裡沒你的事了.........";
	close;
}


//===========================================================
// Guildsman that need to be found
//===========================================================

//------------------------------------------------------------------------------
in_rogue.gat,272,136,3	script	海勒曼梭	85,{

	mes "[海勒曼梭]";
	mes "啊? 什.... 你是誰?! 你不是從流氓工會來的!! 給我滾開!";
	emotion 1;
	next;
	mes "[海勒曼梭]";
	mes "你是..... 原來如此! 你是他們的人!!! 你是來這裡殺我的對吧??!! 不... 不要!! 我還沒準備好要死!";
	emotion 0;
	next;
	mes "[海勒曼梭]";
	mes "啊啊啊啊啊啊!! 走開! 滾蛋! 不然我就殺了你!!!";
	emotion 16;
	close;
}

// --------------------------------------------------------------------------
cmd_fild09.gat,106,195,0	script	Warp#1	45,1,1,{
	callfunc "F_RogueTest3", 1, "阿拉甘", "並沒有把", "精煉物品", "轉移到別處", 244, 24;
}

// -----------------------------------------------------------------------------
cmd_fild09.gat,335,143,0	script	Warp#2	45,1,1,{
	callfunc "F_RogueTest3", 2, "我爸爸", "並沒有把", "精煉物品", "轉移到別處", 168, 34;
}

// ------------------------------------------------------------------------------
cmd_fild04.gat,304,179,0	script	Warp#3	45,1,1,{
	callfunc "F_RogueTest3", 3, "安童尼奧", "並不喜歡", "打碎", "精煉物品", 165, 104;
}

// Function for Guildsman ===================================
function	script	F_RogueTest3	{

	mes "[???]";
	mes "誰在哪裡?!!!";
	mes "好大的膽子，竟敢擅自進入我的地盤?";
	if((BaseJob != Job_Thief) || (ROGUE_Q < 3) || (ROGUE_Q2 == 0)) close;
L_Start:
	deletearray @choice$[1], 4;
	next;
	menu "我爸爸",M_1a, "阿拉甘",M_1b, "安童尼奧",M_1c, "勒苟拉斯",M_1d;

	M_1a:
		set @choice$[1], "我爸爸";
		goto M_Menu2;
	M_1b:
		set @choice$[1], "阿拉甘";
		goto M_Menu2;
	M_1c:
		set @choice$[1], "安童尼奧";
		goto M_Menu2;
	M_1d:
		set @choice$[1], "勒苟拉斯";

	M_Menu2:
	callsub sF_Password;
	menu "並沒有將",M_2a, "並不喜歡",M_2b, "並沒有把",M_2c, "從來沒把",M_2d;

	M_2a:
		set @choice$[2], "並沒有將";
		goto M_Menu3;
	M_2b:
		set @choice$[2], "並不喜歡";
		goto M_Menu3;
	M_2c:
		set @choice$[2], "並沒有把";
		goto M_Menu3;
	M_2d:
		set @choice$[2], "從來沒把";

	M_Menu3:
	callsub sF_Password;
	menu "精煉東西",M_3a, "精煉物品",M_3b, "藏起來",M_3c, "拿走",M_3d, "打碎",M_3e;

	M_3a:
		set @choice$[3], "精煉東西";
		goto M_Menu4;
	M_3b:
		set @choice$[3], "精煉物品";
		goto M_Menu4;
	M_3c:
		set @choice$[3], "藏起來";
		goto M_Menu4;
	M_3d:
		set @choice$[3], "拿走";
		goto M_Menu4;
	M_3e:
		set @choice$[3], "打碎";

	M_Menu4:
	callsub sF_Password;
	menu "轉移到別處",M_4a, "偽造物品",M_4b, "精煉物品",M_4c, "精煉裝備",M_4d;

	M_4a:
		set @choice$[4], "轉移到別處";
		goto L_Check;
	M_4b:
		set @choice$[4], "偽造物品";
		goto L_Check;
	M_4c:
		set @choice$[4], "精煉物品";
		goto L_Check;
	M_4d:
		set @choice$[4], "精煉裝備";

L_Check:
	callsub sF_Password;
	if(ROGUE_Q2 != getarg(0)) goto L_Wrong;
	if((@choice$[1] != getarg(1)) || (@choice$[2] != getarg(2)) || (@choice$[3] != getarg(3)) || (@choice$[4] != getarg(4))) goto L_Wrong;
	mes "~ 咯吱咯吱 ~";
	mes "你聽到門打開的聲音........";
	next;
	deletearray @choice$[1], 4;
	initnpctimer "rogue04";
	warp "in_rogue.gat", getarg(5), getarg(6);
	end;

	L_Wrong:
		mes "[???]";
		mes "滾開!!";
		close;
sF_Password:
	mes "[^5533FF" + strcharinfo(0) + "^000000]";
	mes @choice$[1];
	mes @choice$[2];
	mes @choice$[3];
	mes @choice$[4];
	next;
	return;
}


//=======================================================================================================//
//						Test 4: Last Test						//
//***********************************************************************************************************************************************************//
// Other path
// warp "in_rogue.gat", 11, 388;
// 390,389 end warp
// 9,9 -> 9,21
//===============================================
// Guildsman
//===============================================
//---------------------------------------------------------------------------
in_rogue.gat,244,39,4	script	阿拉甘#rg	99,{
	callfunc "F_RogueTest4", "阿拉甘", 245, 27;
}
//---------------------------------------------------------------------------
in_rogue.gat,152,27,6	script	豪格藍#rg	85,{
	callfunc "F_RogueTest4", "豪格藍", 160, 32;
}
//---------------------------------------------------------------------------
in_rogue.gat,183,105,4	script	安童尼奧#rg	88,{
	callfunc "F_RogueTest4", "安童尼奧", 175, 107;
}

// Function for Guildsman =============================
function	script	F_RogueTest4	{

	if(ROGUE_Q == 4) goto L_Restart;
	mes "[" + getarg(0) + "]";
	mes "嘿! 小子... 你是從流氓工會來的吧!  我是 " + getarg(0) + "...... 我是沙漠的流氓..............";
	next;
	mes "[" + getarg(0) + "]";
	mes "呵呵...我只是開玩笑...... 別說這些胡言亂語了，你來這裡是希望能成為流氓的嗎?";
	emotion 4;
	next;
	mes "[" + getarg(0) + "]";
	mes "流氓的格言: ^5533FF'避免硬碰硬! 找到弱點畢其功於一役!'^000000";
	mes "這是與魔物對抗時最簡單的法則.";
	next;
	mes "[" + getarg(0) + "]";
	mes "在你成為流氓前最後一件要你做的事就是從地下密道走回去流氓工會.";
	next;
	mes "[" + getarg(0) + "]";
	mes "聽起來很簡單吧? 很抱歉! 並沒有想像中那麼簡單! 地下密道中充滿了魔物! 不過不用擔心....";
	mes "如果你牢記流氓的格言並善用隱匿技能，打不過的時候就死命狂奔的話，你會通過測驗的.";

	M_Menu:
	next;
	mes "[" + getarg(0) + "]";
	mes "嗯，你準備好了嗎?";
	next;
	menu "我從來沒有這麼認真過",-, "老實說，我有點害怕",M_End;

	if ($@rogue04test > 0) mes "[" + getarg(0) + "]";
	if ($@rogue04test > 0) mes "地下密道中目前有人正在拚命中，你請在此處稍候一下吧，等裡面沒人了我會通知你!";
	if ($@rogue04test > 0) close;

		mes "[" + getarg(0) + "]";
		mes "那麼，祝你好運!";
		next;
		set ROGUE_Q, 4;
		killmonsterall "in_rogue.gat";
		savepoint "in_rogue.gat", getarg(1), getarg(2);
		set $@rogue04test,1;
		initnpctimer "rogue04";
		warp "in_rogue.gat", 15, 105;
		end;

	M_End:
		mes "[" + getarg(0) + "]";
		mes ".....................";
		next;
		mes "[" + getarg(0) + "]";
		mes "那麼，你就慢慢磨蹭吧......";
		close;

L_Restart:
	mes "[" + getarg(0) + "]";
	mes "....... 看來你失敗了.......";
	emotion 4;
	next;
	mes "[" + getarg(0) + "]";
	mes "如果你還想繼續的話，我就再送你進去. 失敗為成功之母.... 呵呵 嘻嘻 哈哈....";
	percentheal 100,100;
	goto M_Menu;
}

//================================================
// Monster Spawns
//================================================
in_rogue.gat,15,185,0	script	rogue_mob1	139,8,0,{
	monster "in_rogue.gat",14,188,"腐屍",1015,1;
	monster "in_rogue.gat",15,188,"腐屍",1015,1;
	monster "in_rogue.gat",16,188,"腐屍",1015,1;
	monster "in_rogue.gat",14,189,"腐屍",1015,1;
	monster "in_rogue.gat",15,189,"腐屍",1015,1;
	monster "in_rogue.gat",16,189,"腐屍",1015,1;
	end;
}
in_rogue.gat,15,245,0	script	rogue_mob2	139,8,0,{
	monster "in_rogue.gat",14,247,"木乃伊",1041,1;
	monster "in_rogue.gat",15,247,"木乃伊",1041,1;
	monster "in_rogue.gat",16,247,"木乃伊",1041,1;
	monster "in_rogue.gat",14,248,"木乃伊",1041,1;
	monster "in_rogue.gat",15,248,"木乃伊",1041,1;
	monster "in_rogue.gat",16,248,"木乃伊",1041,1;
	end;
}
in_rogue.gat,15,328,0	script	rogue_mob3	139,8,0,{
	monster "in_rogue.gat",14,331,"腐屍",1015,1;
	monster "in_rogue.gat",15,331,"腐屍",1015,1;
	monster "in_rogue.gat",16,331,"腐屍",1015,1;
	monster "in_rogue.gat",14,332,"腐屍",1015,1;
	monster "in_rogue.gat",15,332,"腐屍",1015,1;
	monster "in_rogue.gat",16,332,"腐屍",1015,1;
	end;
}
in_rogue.gat,35,342,0	script	rogue_mob4	139,0,8,{
	monster "in_rogue.gat",38,340,"木乃伊",1041,1;
	monster "in_rogue.gat",37,342,"深淵騎士",1219,1;
	monster "in_rogue.gat",39,342,"綠腐屍",1036,1;
	monster "in_rogue.gat",38,344,"木乃伊",1041,1;
	end;
}
in_rogue.gat,57,306,0	script	rogue_mob5	139,4,0,{
	monster "in_rogue.gat",59,301,"卡利斯格",1132,1;
	end;
}
in_rogue.gat,96,333,0	script	rogue_mob6	139,0,4,{
	monster "in_rogue.gat",92,325,"卡利斯格",1132,1;
	end;
}
in_rogue.gat,139,313,0	script	rogue_mob7	139,4,0,{
	monster "in_rogue.gat",139,309,"深淵騎士",1219,1;
	end;
}
in_rogue.gat,135,246,0	script	rogue_mob8	139,0,4,{
	monster "in_rogue.gat",139,245,"腐屍",1015,2;
	monster "in_rogue.gat",139,246,"腐屍",1015,2;
	monster "in_rogue.gat",139,247,"腐屍",1015,2;
	end;
}
in_rogue.gat,62,302,0	script	rogue_mob9	139,0,4,{
	monster "in_rogue.gat",57,305,"卡利斯格",1132,1;
	end;
}
in_rogue.gat,199,218,0	script	rogue_mob10	139,0,8,{
	monster "in_rogue.gat",157,218,"腐屍",1015,10;
	monster "in_rogue.gat",157,218,"深淵騎士",1219,1;
	monster "in_rogue.gat",157,218,"綠腐屍",1036,1;
	monster "in_rogue.gat",157,218,"邪骸弓箭手",1016,1;
	end;
}
in_rogue.gat,86,187,0	script	rogue_mob11	139,0,8,{
	monster "in_rogue.gat",88,186,"木乃伊",1041,1;
	monster "in_rogue.gat",88,187,"木乃伊",1041,1;
	monster "in_rogue.gat",88,188,"木乃伊",1041,1;
	end;
}
in_rogue.gat,225,187,0	script	rogue_mob12	139,0,8,{
	monster "in_rogue.gat",232,191,"深淵騎士",1219,1;
	monster "in_rogue.gat",233,190,"深淵騎士",1219,1;
	monster "in_rogue.gat",234,189,"深淵騎士",1219,1;
	end;
}
in_rogue.gat,252,320,0	script	rogue_mob13	139,0,8,{
	monster "in_rogue.gat",252,319,"木乃伊",1041,1;
	monster "in_rogue.gat",252,321,"木乃伊",1041,1;
	monster "in_rogue.gat",252,319,"綠腐屍",1036,1;
	monster "in_rogue.gat",252,321,"綠腐屍",1036,1;
	monster "in_rogue.gat",257,320,"邪骸弓箭手",1016,1;
	monster "in_rogue.gat",248,320,"邪骸弓箭手",1016,1;
	end;
}

//================================================
// End Warp
//================================================
in_rogue.gat,370,320,0	script	rogue04	45,1,1,{
	set ROGUE_Q, 5;
	killmonsterall "in_rogue.gat";
	stopnpctimer "rogue04";
	set $@rogue04test,0;
	warp "in_rogue.gat",378,113;
	end;

OnTimer30000:
	set $job_rogue_map1, getareausers("in_rogue.gat",234,20,275,61);
	set $job_rogue_map2, getareausers("in_rogue.gat",148,22,173,59);
	set $job_rogue_map3, getareausers("in_rogue.gat",158,102,185,119);
	set $job_rogue_map4, getareausers("in_rogue.gat",1,1,391,391);
	set $job_rogue_testarea,$job_rogue_map4-$job_rogue_map1-$job_rogue_map2-$job_rogue_map3;
	if ($job_rogue_testarea == 0 && $job_rogue_map4 > 0) goto endtest;
	if ($job_rogue_map4 == 0) goto endrogue;
	initnpctimer "rogue04";
	end;

endtest:
	killmonsterall "in_rogue.gat";
	stopnpctimer "rogue04";
	set $@rogue04test,0;
	areaannounce "in_rogue.gat",234,20,275,61,"考場目前已經空出來了!下一位申請者請準備!",8;
	areaannounce "in_rogue.gat",148,22,173,59,"考場目前已經空出來了!下一位申請者請準備!",8;
	areaannounce "in_rogue.gat",158,102,185,119,"考場目前已經空出來了!下一位申請者請準備!",8;
	end;

endrogue:
	killmonsterall "in_rogue.gat";
	stopnpctimer "rogue04";
	set $@rogue04test,0;
	end;

}

//================================================
// Timer: Keeps monsters from overspawning
//================================================
//~- script RogueTest3 -1,
//~{
//~	end;
//~
//~OnMinute10:
//~OnMinute20:
//~OnMinute30:
//~OnMinute40:
//~OnMinute50:
//~OnMinute60:
//~	killmonsterall "in_rogue.gat";
//~	end;
//~}