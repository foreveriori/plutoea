// 煉金術士轉職腳本
// 
// 中文化作者:		Dino9021
//
// 版權宣告:		本檔案由 eAthenaC 論壇提供，腳本結構屬原作者所有，翻譯及修正之版權屬 Dino9021 所有
//			轉載請勿移除任何說明敘述與說明檔案，并需注明論壇連結 http://eathena.twbbs.org
// 
// 中文版本編號:	20041118.0.2	轉職時會依照職業等級發給禮物
//			20050210.0.1	煉金術士職業相關物品販賣員重新從煉金術士轉職腳本中分離出來并以 eAthena 腳本重新翻譯
//					移除傳點設定 (于 npc\warps\other\jobquests.txt 中設定)
//			20050420.0.1	修改職業判斷使用 eAthena 之變數，轉職指令改用 eAthena 之轉職函式
//
// 參考腳本:		本腳本參考 IRO 轉職程式編寫
//			Athena Conversion by RoVeRT
//			Kingbo 2004/2/15 AM 04:20
//
// 注:			本腳本同時需要 npc/quests/counteragent_mixture.txt 腳本
//			本腳本同時需要 npc/quests/quests_yuno.txt 腳本
//
//============================================================

//===== eAthena Script ======================================= 
//= Alchemist Job Quest
//===== By: ==================================================
//= nestor_zulueta (Fusion)
//= converted by Darkchild
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0 +
//===== Description: ========================================= 
//= Alchemist job quest based off of official iRO quest.
//===== Additional Comments: ================================= 
//= v1.0 Working.
//= npc/quests/counteragent_mixture.txt Also Needed
//= npc/quests/quests_yuno.txt Also Needed [Darkchild]
//= v1.1 Fixed some minor bugs.  Optimized some lines. Re-organized the script a bit. 
//= Giving Parmry NPC, Hammer and Old Book now waves the 50000 fee. (based of mRO website)
//= Having a joblvl of 50 allows you to skip Rasputin's test. (based of mRO website) [kobra_k88]
//= 1.2 fixed a few typos (have to hunt more) [Lupus]
//= 1.3 Added Baby Class Support [Lupus]
//============================================================ 


//==================================================================================================//
//					Registration and First Test
//==================================================================================================//
alde_alche.gat,27,185,4	script	煉金術師 組合員	744,{
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	if(BaseJob == Job_Merchant) goto L_Merc;
	if(BaseJob == Job_Alchem) goto L_Alch;
L_Other:	
	mes "歡迎光臨煉金術士公會! 我現在很忙，請你自己參觀吧!";
	close;
L_Alch:
	mes "會友們，讓我們一起為煉金事業的發展努力吧~~~！";
	close;
L_Merc:	
	if(ALCH_Q == 1) goto L_Check;
	if(ALCH_Q == 2) goto L_Test2;
	if(ALCH_Q == 3) goto L_Test3;
	if(ALCH_Q == 4) goto L_Test4;
	if(ALCH_Q == 5) goto L_Test5;
	if(ALCH_Q == 6) goto L_Test6;
	if(ALCH_Q == 7) goto L_GoChange;
	mes "歡迎光臨煉金術士公會!";
	mes "有什么需要我效勞的地方嗎?";
	next;
	menu "請告訴我有關煉金術士的事",M_Learn, "我想成為一名煉金術士",L_Start, "取消",M_End;

	M_Learn:
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "煉金術士是從目前已知的各種物質中研究并提煉創造出新物質的人.";
		mes "他們研究化學元素中最微小的物質，并嘗試改變這些物質的特性.";
		next;
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "大部分的人都以為煉金術士的最終目的是為了要提煉黃金，但那并非完全是事實.";
		mes "他們從簡單的化學藥品中提煉創造初各種新的物質.";
		next;
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "有時候，有些研究的目的在于創造出新的生命....但那是屬于上帝的領域....";
		mes "那個領域太復雜難懂了，所以到目前為止仍是處于研究階段.";
		next;
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "如果你有興趣成為一名煉金術士，我建議你先培養一些作商人的經驗.";
		mes "作一名商人能讓你有絕佳的機會在交易各種物品時了解它們的特性.";
		next;
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "只有你能為你自己作決定.";
		mes "通往成為一名煉金術士的道路是充滿了挑戰的.";
		mes "你需要把心思專注在對于物品特性的了解與研究，而不只是單純的交易買賣上.";
		close;
	M_End:
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "噢...那么...有任何需要請盡管開口.";
		close;
L_Start:
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	if(JobLevel < 40) goto L_LowLvl;
	mes "這樣嗎? 我是 煉金術師 組合員 '帕樂米 基阿尼諾'";
	mes "很高興認識你.";
	next;
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	mes "如果你加入我們的公會并通過我們的測驗，你就可以得到公會的認可，";
	mes "成為一名煉金術士并可以與我們一同作研究.";
	next;
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	mes "不過我們并不是來者不拒統統接受的.";
	mes "你必須非常努力、有堅強的韌性、并且有決心把自己奉獻給煉金研究.";
	next;
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	mes "在加入公會前有一些東西需要你去準備....";
	mes "不過那等你申請加入之后我們再來討論吧!";
	next;
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	mes "說了那么多，你想要加入煉金術士公會嗎？";
	next;
	menu "我要申請加入",M_Apply,"讓我再考慮考慮吧",-;

		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "如果你是一個很有才干的商人，";
		mes "我們永遠都歡迎你來加入我們.";
		mes "請時常來與我們交流.";
		close;

	HighAlc:
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "哎呀! 你已經是上位商人了呢!";
		mes "很抱歉! 我沒有辦法讓你轉職成煉金術士!";
		mes "請你去找制造家轉職吧!";
		mes "那才是適合你的職業!";
		close;

	M_Apply:
		if (((BaseJob == Job_Merchant) && (Upper == 1)) || (ReBirth > 0)) goto HighAlc;
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "好的，請先填妥這些表格.....";
		next;
		mes "(你填寫了一些表格并把他交給帕樂米 基阿尼諾)";
		next;
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "好極了! 首先，要加入煉金術士公會必須先繳交 ^5533FF50,000^000000 zeny 的會費.";
		mes "另外還需要帶一些東西來給我們.";
		next;
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "不過，如果你能帶一本 ^5533FF古魔法書^000000 及一個 ^5533FF高級鐵鉆^000000 來的話...";
		mes "我們就跳過會費的部分吧.";
		next;
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "讓我瞧瞧.... 我們需要你帶....^5533FF100 個攜帶用熔礦爐^000000 過來.";
		mes "等你準備好了就回來這里找我.";
		next;
		mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
		mes "到時候我會一并向你收取 ^5533FF50,000^000000 zeny 的會費，所以屆時可別忘了把錢帶來.";
		mes "希望很快能再見到你...";
		set ALCH_Q,1;
		close;
L_LowLvl:
	mes "你還沒夠資格轉職，回去多練幾年，等你的職業技能 40 級的時候再來找我.";
	close;

L_Check:
	mes "噢! 你回來啦... 讓我瞧瞧....";
	next;
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	if(countitem(612) < 100) goto L_NoItems;
	if(countitem(1005) > 0 && countitem(1006) > 0) goto L_Skip;
	if(zeny < 50000) goto L_NoZeny;
 	set zeny,zeny - 50000;
	delitem 612, 100;
	mes "嗯... 50,000 zeny 的會費...... 100 個攜帶用熔礦爐.... 好極了!";
	mes "接下來你得知道一些作煉金術士應有的基礎.";
	next;

	L_Cont:
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	mes "不過在那之前... ^5533FF拉斯普琴^000000 想要先見見你.....";
	mes "嗯..... 我不太清楚那是為了什么....";
	next;
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	mes "你現在先去找他吧! 從我右手邊的樓梯下樓，他的房間在東南方的角落.";
	set ALCH_Q,2;
	close;

	L_NoZeny:
		mes "你還需要支付 ^FF553350,000^000000 zeny 的會費.";
		mes "等你湊到足夠的錢再來找我吧.";
		close;
	L_NoItems:
		mes "我之前不是跟你說過了嗎? 你得帶 ^FF3355100 個攜帶用熔礦爐^000000 來給我.";
		mes "請準備好之后再回來找我...";
		close;
L_Skip:
	mes "噢! 你有 ^5533FF古魔法書^000000 和 ^5533FF高級鐵鉆^000000 啊........";
	mes "真是不簡單! 那我們就跳過會費的部分吧.";
	next;
	mes "[煉金術師 組合員 帕樂米 基阿尼諾]";
	mes "太完美了! 那么，我就收下你的古魔法書、高級鐵鉆和攜帶用熔礦爐了，接下來你得知道一些作煉金術士應有的基礎..";
	next;
	delitem 1005,1;
	delitem 1006,1;
 	delitem 612, 100;
	goto L_Cont;

L_Test2:	
	mes "快去見 ^5533FF拉斯普琴^000000 吧!";
	close;
L_Test3:	
	mes "快去見 ^5533FF塔羅西亞^000000 吧!";
	close;
L_Test4:	
	mes "快去見 ^5533FF班 黑文特^000000 吧!";
	close;
L_Test5:	
	mes "快去見 ^5533FF尼克拉斯 波拉媒^000000 吧!";
	close;
L_Test6:	
	mes "回去找 ^5533FF尼克拉斯 波拉媒^000000 吧!";
	mes "他對你還有一些測驗未完成.";
	close;
L_GoChange:	
	mes "哇! 你通過了所有的測驗嗎? 太厲害了，現在請你去見我們的公會長 ^5533FF賓珊特 卡樂士阿羅^000000 吧.";
	mes "他是這里唯一能幫你轉職的人.";
	close;
}


//==================================================================================================//
//					2nd Test: Math test :(
//==================================================================================================//
alde_alche.gat,175,107,4	script	怪癖的 煉金術士	749,{
	mes "[拉斯普琴 克勒咕哩]";
	if(class == 5 || Class == 4006) goto L_Merc;
L_Other:
	mes "這是什么? 嘻嘻嘻嘻...你很好奇我在作什么?";
	mes "我只告訴你一個人...你看我...呵呵呵呵";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "是一個研究者! 等這個藥劑完成了... 你就有能力征服一個國家!!";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "嘻嘻嘻嘻呵呵呵呵... 普隆德拉的人都還不知道這個秘密呢.....";
	close;
L_Merc:
	if(ALCH_Q == 2) goto L_Start;
	if(ALCH_Q == 3) goto L_Done;
	mes "嘻嘻嘻嘻呵呵呵呵...";
	mes "這是什么? 你這個不學無術的笨蛋.";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "商人就該去開個店面賣東西...";
	mes "你來這里作什么?!?";
	mes "找東西搞破壞嗎?";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "嘻嘻嘻... 滾! 離開這里!";
	mes "別在這里閑晃 ... 呵呵呵...";
	close;
L_Start:
	if(ALCH_Q2 == 1) goto L_ReTest;
	if(JobLevel == 50) goto L_Skip;
	mes "呵呵... 有個笨蛋摔跤摔進來了...";
	mes "加入公會?";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "我不喜歡這樣... 我不喜歡...!";
	mes "世界各地的阿貓阿狗都來加入公會然后稱自己為 '煉金術士' 結果呢? 只知道怎么搗藥草!!!";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "嘻嘻嘻... 所以，想加入公會的人都需要通過面試才行!";
	mes "我決定要狠狠教訓那些愚蠢的家伙然后再把他們統統趕出去!!";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "你看起來真是有夠蠢的. 實在是太瘋狂了...";
	mes "如果你剛剛只是在想要換件衣服的話我可以告訴你，你大錯特錯了... 呵呵呵..";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "哼! 現在我要出幾個問題考你，看看你的腦袋轉不轉得過來.";
	mes "看看你有多聰明.";
	next;
	set @score,0;

	//set of question 1
	q1:
		mes "[拉斯普琴 克勒咕哩]";
		mes "12+23+34+45 =?";
		next;
		input @num;
		if(@num == 114) set @score,@score+10;
	q2:	
		mes "[拉斯普琴 克勒咕哩]";
		mes "1000-36-227-348 =?";
		next;
		input @num;
		if(@num == 389) set @score,@score+10;
	q3:
		mes "[拉斯普琴 克勒咕哩]";
		mes "9765/3/5/7 =?";
		next;
		input @num;
		if(@num == 93) set @score,@score+10; 
	q4:
		mes "[拉斯普琴 克勒咕哩]";
		mes "(2646/7) + (13*28) =?";
		next;
		input @num;
		if(@num == 742) set @score,@score+10;
	q5:
		mes "[拉斯普琴 克勒咕哩]";
		mes "當用 7.6 折低價買進時";
		mes "紅色藥水 12 瓶";
		mes "蒼蠅翅膀 5 個";
		mes "再加上 1 個蝴蝶翅膀總共多少錢?";
		next;
		input @num;
		if(@num == 909) set @score,@score+10;
	q6:
		mes "[拉斯普琴 克勒咕哩]";
		mes "彎刀 3 把";
		mes "金屬頭盔 2 頂";
		mes "再加上 1 件長大衣總共多重?";
		next;
		input @num;
		if(@num == 450) set @score,@score+10;
	q7:	
		mes "[拉斯普琴 克勒咕哩]";
		mes "圣職之帽、大衣、歌劇之魂假面";
		mes "紫蝶發帶、披肩、戰士長靴";
		mes "耳罩、安全帽.";
		mes "以上裝備防御值加起來總共多少?";
		next;
		input @num;
		if(@num == 22) set @score,@score+10;
	q8:	
		mes "[拉斯普琴 克勒咕哩]";
		mes "如果你以 7.6 折低價買進金屬頭盔";
		mes "然后再以原價的 8 折賣出去";
		mes "那么你可以賺多少錢?";
		next;
		input @num;
		if(@num == 1760) set @score,@score+10;
		goto L_Total;

L_ReTest:
	mes "什么?! 你想再考一次?";
	mes "我不是叫你滾了嗎?";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "好吧! 就讓你再考一次...";
	mes "我原諒你... 呵呵呵.,.";
	mes "這次好好表現吧...";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "好，現在開始回答我的問題.";
	mes "看看你有多聰明.";
	next;
	set @score,0;

	//set of question 2
	q11:
		mes "[拉斯普琴 克勒咕哩]";
		mes "13+25+37+48=?";
		next;
		input @num;
		if(@num == 123) set @score,@score+10;
	q12:
		mes "[拉斯普琴 克勒咕哩]";
		mes "1000-58-214-416 =?";
		next;
		input @num;
		if(@num == 312) set @score,@score+10;
	q13:
		mes "[拉斯普琴 克勒咕哩]";
		mes "12*24*3=?";
		next;
		input @num;
		if(@num == 864) set @score,@score+10;
	q14:
		mes "[拉斯普琴 克勒咕哩]";
		mes "10530/3/5/2=?";
		next;
		input @num;
		if(@num == 351) set @score,@score+10;
	q15:
		mes "[拉斯普琴 克勒咕哩]";
		mes "(35*19) - (1792/7) =?";
		next;
		input @num;
		if(@num == 409) set @score,@score+10;
	q16:
		mes "[拉斯普琴 克勒咕哩]";
		mes "(2368/8) + (24*17) = ?";
		next;
		input @num;
		if(@num == 704) set @score,@score+10;
	q17:
		mes "[拉斯普琴 克勒咕哩]";
		mes "(2646/7) + (13*28)=?";
		next;
		input @num;
		if(@num == 742) set @score,@score+10;
	q18:
		mes "[拉斯普琴 克勒咕哩]";
		mes "當用 7.6 折低價買進時";
		mes "綠色藥水 15 瓶";
		mes "放大鏡 6 個";
		mes "再加上 4 個設定用陷阱道具總共多少錢?";
		next;
		input @num;
		if(@num == 934) set @score,@score+10;
	q19:
		mes "[拉斯普琴 克勒咕哩]";
		mes "圓柄馬刀 3 把";
		mes "無邊帽 4 頂";
		mes "再加上 2 雙戰士長靴總共多重?";
		next;
		input @num;
		if(@num == 550) set @score,@score+10;
	q20:
		mes "[拉斯普琴 克勒咕哩]";
		mes "圓盾、長大衣、防毒面具";
		mes "紅蝶發帶、紫蝶發帶、草帽";
		mes "眼鏡.";
		mes "以上裝備防御值加起來總共多少?";
		next;
		input @num;
		if(@num == 16) set @score,@score+10;
	q21:
		mes "[拉斯普琴 克勒咕哩]";
		mes "如果你以 7.6 折低價買進緊身便衣";
		mes "然后再以原價的 8 折賣出去";
		mes "那么你可以賺多少錢?";
		next;
		input @num;
		if(@num == 2840) set @score,@score+10;
L_Total:
	mes "[拉斯普琴 克勒咕哩]";
	if(@score < 80) goto L_Failed;
	set ALCH_Q,3;
	set ALCH_Q2, 0;
	mes "嘻嘻嘻... 你快滾吧...";
	mes "要常常作研究并提供給拉斯普琴我... 嘻嘻嘻!";
	next;	
	mes "[拉斯普琴 克勒咕哩]";
	mes "還不快滾! 去找 ^5533FF塔羅西亞^000000 吧!";
	mes "他會教你怎么進行實驗.";
	mes "告訴他是我叫你去的.";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "嘻嘻嘻呵呵呵...";
	mes "別以為這樣就結束了!";
	close;

	L_Failed:
		set ALCH_Q2,1;
		mes "...呵呵呵... 你這個蠢蛋...";
		mes "我竟然笨到以為你能回答所有的問題!";
		mes "你簡直是錯得一塌糊涂!!!";
		next;
		mes "[拉斯普琴 克勒咕哩]";
		mes "連這么簡單的問題都不會還想作什么煉金術士?!";
		mes "什么? 你有沒有答對任何一題?";
		next;
		mes "[拉斯普琴 克勒咕哩]";
		mes "笨蛋! 在煉金術士的世界里只要錯一個小地方就全盤皆錯了!";
		mes "滾!! 滾遠一點!!";
		close;
L_Done:
	mes "你還在這里作什么? 快滾吧!";
	mes "去找 ^5533FF塔羅西亞^000000 吧!";
	mes "他會教你怎么進行實驗.";
	mes "告訴他是我叫你去的.";
	next;
	mes "[拉斯普琴 克勒咕哩]";
	mes "嘻嘻嘻呵呵呵...";
	mes "別以為這樣就結束了!";
	close;
L_Skip:
	mes "噢... 你的等級還滿高的嘛..... 我今天就放你一馬好了...";
	mes "接下來你需要去找 ^5533FF塔羅西亞^000000.";
	mes "他會教你怎么進行實驗.";
	mes "告訴他是我叫你去的.";
	set ALCH_Q,3;
	set ALCH_Q2, 0;
	close;
}


//==================================================================================================//
//					3rd Test: Mix some medicine
//==================================================================================================//
alde_alche.gat,13,15,8	script	研究者	750,{
	mes "[研究者 塔羅西亞]";
	if(BaseJob == Job_Merchant) goto L_Merc;
L_Other:
	mes "失去的總比得到的多...";
	mes "永遠都是這樣的...";
	next;
	mes "[研究者 塔羅西亞]";
	mes "一切都是幻覺啊!!";
	mes "嚇不倒我的!!";
	mes "哈哈哈哈哈...";
	next;
	mes "[研究者 塔羅西亞]";
	mes "天堂是不存在的... 曾經存在過嗎?";
	mes "我珍愛的哈蒙娜.... 啊....";
	close;

L_Merc:
	if(ALCH_Q == 3) goto L_Start;
	if(ALCH_Q == 4) goto L_Done;
	goto L_Other;

L_Start:
	if(ALCH_Q2 == 1) goto L_Check;
	mes "...........";
	mes ".........";
	mes "....誰...";
	next;
	mes "[研究者 塔羅西亞]";
	mes "狼? 還是人...";
	mes "你一定也在找尋某樣東西吧...";
	next;
	mes "[研究者 塔羅西亞]";
	mes "如果你有某樣你很珍愛的東西，小心...";
	mes "人很容易在追尋另一件東西的時候失去了原有的東西...";
	next;
	mes "[研究者 塔羅西亞]";
	mes "..........";
	next;
	mes "[研究者 塔羅西亞]";
	mes "... 呃...你為什么會來到這里?";
	mes "來到一個像這樣的地方...";
	next;
	menu "我想學習如何做實驗",M_Exp, "請告訴我更多有關哈蒙娜的事",M_Flow;

	M_Flow:	
		mes "[研究者 塔羅西亞]";
		mes ".......";
		mes "......";
		mes "...想一下你最珍愛的東西...";
		next;
		mes "[研究者 塔羅西亞]";
		mes ".... 我依稀記得曾經有一朵花....";
		next;
		mes "[研究者 塔羅西亞]";
		mes "我加了一些東西到我的研究里.....";
		mes "為了我的珍愛...";
		next;
		mes "[研究者 塔羅西亞]";
		mes "就說是狼與花之間的風流韻事吧...";
		mes "我不會告訴你細節的...";
		next;
		mes "[研究者 塔羅西亞]";
		mes "但，是的... 那是一朵花...";
		mes "在她閃耀的光芒下，聽說可以讓你看見天堂里如幻影一般的花...";
		next;
		mes "[研究者 塔羅西亞]";
		mes "我創造了一個生命.";
		mes "沒有人相信我從一朵花創造了一個類人生物!!!";
		mes "我的研究啊!";
		next;
		mes "[研究者 塔羅西亞]";
		mes "但現在，我身無長物...";
		mes "都結束了，全部都結束了...";
		mes "我的生命就停在那一剎那之間...";
		next;
		mes "[研究者 塔羅西亞]";
		mes "啊... 哈蒙娜...美麗原野中的花朵們...";
		mes "到哪里去了....";
		close;
	M_Exp:
		mes "[研究者 塔羅西亞]";
		mes "你想學煉金術...";
		mes "我所擁有的全部知識..";
		mes "為了讓我的夢想成真...";
		next;
		mes "[研究者 塔羅西亞]";
		mes "我會教你一些基本的東西...";
		mes "但你必須先決定你的夢想.";
		next;
		mes "[研究者 塔羅西亞]";
		mes "好吧，這些都是最基本的.";
		mes "要制造一些最簡單的藥劑...";
		mes "準備一些材料...";
		next;
		mes "[研究者 塔羅西亞]";
		mes "^5533FF藥缽 3 個";
		mes "空瓶 3 個";
		mes "紅色藥草 1 片";
		mes "黃色藥草 1 片";
		mes "白黃色藥草 1 片^000000";
		next;
		mes "[研究者 塔羅西亞]";
		mes "等你準備好了再回來找我吧...";
		set ALCH_Q2, 1;
		close;

L_Check:
	mes "...........";
	mes ".........";
	mes "...誰....";
	next;
	mes "[研究者 塔羅西亞]";
	mes "呃...你是那個想學煉金術的人啊...";
	mes "你準備好需要的東西了嗎?";
	next;
	if(countitem(7134) < 3 || countitem(713) < 3 || countitem(507) < 1 || countitem(508) < 1 || countitem(509) < 1) goto L_NotEnuf;
	delitem 7134,3;
	delitem 713,3;
	delitem 507,1;
	delitem 508,1;
	delitem 509,1;
	mes "[研究者 塔羅西亞]";
	mes "嗯，看來該有的東西都有了.";
	mes "就如同我先前答應你的，教你制造一些最簡單的藥劑";
	next;
	mes "[研究者 塔羅西亞]";
	mes "首先，準備好藥缽，把一部份少量的藥草像這樣放進藥缽里.";
	mes "然后慢慢地研磨.";
	next;
	mes "[研究者 塔羅西亞]";
	mes "倒進一些干凈的清水然后攪拌直到變成濃稠狀";
	mes "接著繼續加入其他剩下的藥草";
	next;
	mes "[研究者 塔羅西亞]";
	mes "就這樣慢慢做，等到你覺得份量夠用了";
	mes "就慢慢地把它裝進空瓶里";
	next;
	mes "[研究者 塔羅西亞]";
	mes "吶! 你看，完成了.";
	mes "現在，照我剛剛的方法作一遍.";
	mes "這應該很簡單吧!";
	next;
	set @score,0;
	menu "準備藥缽",-, "把藥缽戴在頭上",M_0, "把藥缽一腳踢飛",M_0;

		set @score,@score+10;
	M_0:

	menu "在藥缽中放入沙子",M_1, "在藥缽中放入藥草",-, "在藥缽中放入跳蚤",M_1;

		set @score,@score+10;
	M_1:

	menu "打碎藥草",-, "打碎藥缽",M_2, "打碎塔羅西亞的腳",M_2;

		set @score,@score+10;
	M_2:

	menu "噴灑清水",M_3, "把水喝掉",M_3, "倒進清水",-;

		set @score,@score+10;
	M_3:

	menu "繼續研磨藥草",-, "繼續吃掉藥草",M_4, "繼續唱歌跳舞",M_4;

		set @score,@score+10;
	M_4:

	menu "放進面條用高溫油炸",M_5, "把成品倒入空瓶",-, "舉起藥缽一飲而盡",M_5;

		set @score,@score+10;
	M_5:

	mes "[研究者 塔羅西亞]";
	if(@score < 60) goto L_Failed;
	mes "... 做得不錯. 就第一次作的生手來說你的表現算不錯的...";
	mes "喏，就把他當成紀念品收起來吧.";
	getitem 501,1;
	getitem 503,1;
	getitem 504,1;
	set ALCH_Q,4;
	set ALCH_Q2,0;
	next;
	mes "[研究者 塔羅西亞]";
L_Done:
	mes "接下來去隔壁房間找 ^5533FF班 黑文特^000000 吧...";
	mes "他會指導你更多細節.";
	mes "去向他好好學習吧.";
	next;
	mes "[研究者 塔羅西亞]";
	mes "別忘了...";
	mes "要好好保護你真正珍愛的東西.....";
	close;

	L_Failed:
		mes "... 簡直是一塌糊涂，原料都被你搞爛了...";
		mes "... 再去準備一份材料來吧.";
		close;
L_NotEnuf:
	mes "[研究者 塔羅西亞]";
	mes "...忘了需要哪些材料了嗎?";
	mes "在跟你說一遍.";
	mes "別再忘了.";
	next;
	mes "[研究者 塔羅西亞]";
	mes "^5533FF藥缽 3 個";
	mes "空瓶 3 個";
	mes "紅色藥草 1 片";
	mes "黃色藥草 1 片";
	mes "白黃色藥草 1 片^000000";
	next;
	mes "[研究者 塔羅西亞]";
	mes "等你準備好了再回來找我吧...";
	close;	
}


//==================================================================================================//
//					4th Test: Learn from Morgenstein
//==================================================================================================//
alde_alche.gat,79,19,4	script	實驗專家	748,{
	mes "[班 黑文特]";
	if(BaseJob == Job_Merchant) goto L_Merc;
L_Other:
	mes "再加一點進去 ... 對，再加一點點進去....";
	mes "嗯...只要再加一點點進去應該就能成功了!";
	next;
	mes "[班 黑文特]";
	mes "為什么!! 為什么又失敗了!!!";
	mes "沒有錯啊!";
	mes "為什么! 為什么!";
	next;
	mes "[班 黑文特]";
	mes "這表示我得再去搜集更多材料嗎...";
	mes "總有一天我一定能成功....呼...";
	close;
L_Merc:
	if(ALCH_Q == 4) goto L_Start;
	if(ALCH_Q == 5) goto L_Done;
	goto L_Other;
L_Start:
	if(ALCH_Q2 == 1) goto L_Check;
	mes "啊啊啊... 為什么這個程式停住了...出了什么錯?";
	mes "理論上來說應該都正確啊...";
	mes "應該是哪一個步驟出了問題了吧...";
	mes "到底是哪一個步驟...";
	next;
	mes "[班 黑文特]";
	mes "就是這里! 到這里的時候它停住了... 而這個步驟是解決不流動的方法.";
	mes "我怎么會犯這么愚蠢的錯誤...我怎么會把它搞混了...";
	next;
	mes "[班 黑文特]";
	mes "對了對了，就是這樣，我得修正這個地方.";
	mes "應該不需要全部從頭來...";
	mes "我只要修正這個地方...";
	mes "....但是，等一下... 等等...";
	next;
	mes "[班 黑文特]";
	mes ".....................";
	next;
	mes "[班 黑文特]";
	mes "....你是誰?";
	next;
	menu "我想成為一名煉金術士",-,".....",M_Bye;

		mes "[班 黑文特]";
		mes "噢! 這樣啊....";
		mes "哈哈! 你想學像這樣的煉金術? 有趣的商人... 啊?";
		next;
		mes "[班 黑文特]";
		mes "嗯! 好吧! 但是我現在有個很迫切的實驗需要去做.";
		mes "所以...別擋住我的路!";
		next;
		menu "多少教我一些吧",-,"..........",M_Bye;

			mes "[班 黑文特]";
			mes "嚇... 我不是叫你別打擾我嗎?";
			mes "我說的話你聽不懂嗎?!";
			next;
			mes "[班 黑文特]";
			mes "好好好，我會出一些功課給你.";
			mes "自己去研究，有結果了再回來.";
			mes "我看看...什么比較好... 嗯...";
			next;
			mes "[班 黑文特]";
			mes "噢! 我想到了! 去向 ^5533FF莫基斯坦^000000 打聽吧. 向他學習如何制造 ^009500中和劑^000000 與 ^FF3355混和劑^000000.";
			mes "你可以在 ^5533FF吉分城^000000 找到他.";
			next;
			mes "[班 黑文特]";
			mes "你不需要把制作好的中和劑跟混和劑帶來. 但一定要專心看他是怎么制造的并把制造方法學起來.";
			mes "了解嗎?";
			next;
			mes "[班 黑文特]";
			mes "那么，祝你好運.";
			mes "你最好趕快出發，才不會繼續打擾我.";
			set ALCH_Q2,1;
			close;
	M_Bye:
		mes "[班 黑文特]";
		mes "如果你在這里沒有事要干就請你趕快離開好嗎!!!";
		close;
L_Check:
	if(al_morgen != 1) goto L_NotRdy;
	mes "嗯...剪一段觸須...";
	mes "混和一些粘稠液體...";
	mes "在試管里加入一些杰勒比結晶... 咦? 藥缽跑哪兒去了?...";
	next;
	mes "[班 黑文特]";
	mes "嗯...就這樣混和在一起... 就像這樣，嗝... 尼克拉斯不知道有沒有忘記什么...";
	mes "哼... 真是偉大的計劃啊...";
	mes "....啊! 等等... 等一下...";
	next;
	mes "[班 黑文特]";
	mes "呃，你是之前來的那個商人啊? 那么，你學到了什么了嗎?";
	mes "我可不是叫你去那邊玩的.";
	next;
	mes "[班 黑文特]";
	mes "好! 我出幾個問題問你，看看你學到了什么.";
	next;
	set @score,0;

	Q_1a:
		mes "[班 黑文特]";
		mes "哪一個是制作中和劑必需的材料?";
		next;
		menu "柔毛",-,"粘稠液體",-,"家畜血",-;

	Q_1b:
		mes "[班 黑文特]";
		mes "哪一個不是制作中和劑必需的材料?";
		next;
		menu "卡勒波迪藥水",-,"帝特立米藥水",Q_2a,"酒精",Q_2a;

			set @score,@score+10;

	Q_2a:
		mes "[班 黑文特]";
		mes "哪一個是制作混合劑必需的材料?";
		next;
		menu "魔物飼料",-,"人魚嘴巴",-,"腐爛繃帶",-;

	Q_2b:
		mes "[班 黑文特]";
		mes "哪一個不是制作混合劑必需的材料?";
		next;
		menu "卡勒波迪藥水",L_Result,"帝特立米藥水",-,"酒精",L_Result;

			set @score,@score+10;

L_Result:
	mes "[班 黑文特]";
	if(@score < 20) goto L_Failed;
	mes "嗯...你學的還不錯... 現在你認識了一些藥劑的材料了.";
	mes "啊?";
	mes "我知道，你有很多話要說，但是現在先讓我繼續我的實驗好嗎?";
	next;
	mes "[班 黑文特]";
	mes "請你去隔壁的房間  ^5533FF尼克拉斯^000000 或許比我有更多可以教你的.";
	set ALCH_Q,5;
	set ALCH_Q2,0;
	set al_morgen, 0;
	close;

	L_Failed:
		mes "...老實說，你不知道對吧?";
		mes "我不是叫你去找莫基斯坦學習嗎?...";
		next;
		mes "[班 黑文特]";
		mes "沒學到東西就不要想回來這里!";
		mes "不要再打擾我了! 快滾!";
		close;
L_NotRdy:
	mes "我不是叫你去吉芬找莫基斯坦學習嗎?...";
	mes "他是個聰明絕頂的人! 你應該多向他學習學習!";
	close;
L_Done:
	mes "你在這里做什么? 我不是叫你去隔壁的房間找  ^5533FF尼克拉斯^000000 嗎?";
	mes "他或許比我有更多可以教你的，我這里沒你的事了.";
	close;

}


//==================================================================================================//
//				Test 5 & 6: Word Scramble, Help the Baijin Bros.
//==================================================================================================//
alde_alche.gat,145,19,4	script	尼克拉斯 波拉媒	57,{
	mes "[首席研究員 尼克拉斯 波拉媒]";
	if(BaseJob == Job_Merchant) goto L_Merc;
L_Other:
	mes "加勒昆保欲豪原非物沾久審的擊字";
	mes "路宣敗不原狼給心木爬提肝祥兔怪";
	mes "翁波宣商人理安壁力大全怕理課感";
	mes "商率壽朋兜知商傷花全踢理有食差";
	mes "食草受商于善傷的久戰多怕九分當";
	mes "......念念....";
	close;
L_Merc:
	if(ALCH_Q == 5) goto L_Start;
	if(ALCH_Q == 6) goto L_Start2;
	if(ALCH_Q == 7) goto L_Done;
	goto L_Other;
L_Start:
	if(ALCH_Q2 == 1) goto L_ReTest;
	mes "噢... 你是想成為煉金術士的商人?";
	mes "但你知道的... 不是任何人都能成為煉金術士的.";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "煉金術士公會不喜歡沒有明確目標卻想成為煉金術士的人.";
	mes "所以... 我來測驗測驗你，看看你有沒有資格.";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "如你所見，煉金術士必須熟記許多化學反應方程式";	
	mes "除此之外還有大量的各種知識";
	mes "如果你沒有足夠的水準的話..";
	mes "我的測驗對你來說是很困難的.";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "這個測驗的目的是為了防止無用、沒前途的人加入煉金公會成為負擔!";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "好吧... 那我就開始出題了!";
	mes "這是要測驗你的集中精神夠不夠，避免你在看那些圖表的時候被自己搞混了.";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "找出這些字母里隱藏的單字. 這個隱藏的單字可能有用到這些字母的一部分或全部都用到";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "如果你找得出這些隱藏的單字，你就能通過我的測驗.";
	next;

	Q_1:
		set @score,0;
		mes "[首席研究員 尼克拉斯 波拉媒]";
		mes "s m i e x b w u n e n t a g l r";
		next;
		menu "tiger",-,"wolf",Q_2,"pumpkin",Q_2,"tripped",Q_2;

			set @score,@score+10; 

	Q_2:
		mes "[首席研究員 尼克拉斯 波拉媒]";
		mes "n i e g b o p d s o a u w r v";
		next;
		menu "bash",Q_3,"provoke",Q_3,"endure",-,"stun",Q_3,"abracadabra",Q_3;

			set @score,@score+10; 

	Q_3:
		mes "[首席研究員 尼克拉斯 波拉媒]";
		mes "l r m g r e x t a v i n e d e";
		next;
		menu "alberta",Q_4,"latifoliate",Q_4,"crimson",Q_4,"maple",Q_4,"evergreen",-;

			set @score,@score+10; 

	Q_4:
		mes "[首席研究員 尼克拉斯 波拉媒] ";
		mes "r o e h n r o m c a i n p t t";
		next;
		menu "forgemerchant",L_Score,"potionmerchant",-,"dcmerchant",L_Score,"vendingmerchant",L_Score;
	
			set @score,@score+10; 
L_Score:
	if(@score < 40) goto L_Failed;
	set ALCH_Q,6;
	set ALCH_Q2,0;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "看起來還不錯.";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "我檢檢視看.";
	mes "...干得好! 全部答對了!";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "你是怎么找到全部的隱藏單字的?";
	mes "就是要集中精神!! 集中精神就能很容易地看懂復雜的藥劑字典了.";
	mes "嗯... 下一個是...";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "噢! 隨時回到這里來吧! 記得先把你的思緒整理好...";
	close;

	L_Failed:
		mes "[首席研究員 尼克拉斯 波拉媒]";
		mes "嗯... 失敗!";
		mes "戴上你的老花眼鏡吧，也許那可以讓你看清楚一點.";
		set ALCH_Q2,1;
		close;
L_ReTest:
	mes "噢! 你又來啦?";
	mes "健全的心靈是可以從外表的改變上感覺得到的."; 
	next;
	goto Q_1;	

L_Start2:
	if(ALCH_Q2 > 0) goto L_Check2;
	mes "接下來，你得去一些地方.";
	mes "去 ^5533FF'遺忘的城市--朱諾'^000000 找向那里的賢者研究煉金術的 ^FF3355發仁 與 發鎮^000000 兩兄弟.";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "幫他們解決困難然后再回來這里，喏，帶著這些東西... 可以讓你更接近他們.";
	next;
	getitem 974,1;
	getitem 7068,5;
	getitem 7043,5;
	getitem 756,3;
	getitem 757,3;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "混合劑 1 瓶";
	mes "燃燒一半的木材 5 根";
	mes "黃晶沙 5 份";
	mes "神之金屬原石 3 顆";
	mes "鋁原石 3 顆";
	set ALCH_Q2,1;
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "祝你一路平安";
	mes "那里可是朱諾吶...可別死在半路上.";
	close;
L_Check2:
	if(ALCH_Q2 < 2) goto L_NotDone;
	set ALCH_Q, 7;
	set ALCH_Q2, 0;
	mes "噢! 你終于回來了啊.";
	mes "我收到他們兩兄弟寄來的信，他們說很高興見到你.";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
	mes "你應該是成功的幫他們解決了問題吧.";
	mes "既然你能夠解決他們的問題，那么我想你應該是有資格成為一名煉金術士的.";
	next;
	mes "[首席研究員 尼克拉斯 波拉媒]";
L_Done:
	mes "去二樓找公會會長吧!";
	mes "恭喜你! 你就快要可以轉職了!";
	close;

	L_NotDone:
		mes "發仁與發鎮兩兄弟在朱諾! 快去協助他們吧.";
		close;

}


//==================================================================================================//
//					Job Changer
//==================================================================================================//
alde_alche.gat,101,184,4	script	精通 煉金術士	122,{
	mes "[精通 煉金術士 賓珊特 卡樂士阿羅]";
	if(Class == 5 || Class == 4006) goto L_Merc;
	if(Class == 18 || Class == 4019) goto L_Alch;
L_Other:
	mes "啊? 平民百姓到這里來做什么??";
	next;
	mes "[精通 煉金術士 賓珊特 卡樂士阿羅]";
	mes "這里沒什么好玩的東西，快離開吧.";
	close;
L_Alch:
	mes "啊? 煉金術要注意什么? 永遠萬事小心，以安全為前題.";
	mes "我可不想你把自己給炸掉或炸掉任何東西.....";
	close;
L_Merc:
	if(ALCH_Q == 7) goto L_Change;
	mes "啊? 商人?";
	mes "你對煉金術有興趣嗎?";
	next;
	mes "[精通 煉金術士 賓珊特 卡樂士阿羅]";
	mes "這里是煉金術士公會";
	mes "我們研究各種不同的物質.";
	mes "我們的目的是以非魔法的手段創造新的物質.";
	next;
	mes "[精通 煉金術士 賓珊特 卡樂士阿羅]";
	mes "創造新的生活，研究長生不老與不死藥...";
	mes "我們也制造出以前所沒有的新原料...";
	mes "這只是一小部分我們做的事而已.";
	next;
	mes "[精通 煉金術士 賓珊特 卡樂士阿羅]";
	mes "作商人那么久了";
	mes "你應該對每一樣東西都很感興趣吧?";
	mes "如果你也對煉金術有興趣的話";
	mes "要不要加入我們煉金術士公會啊?";
	close;
L_Change:
	if(skillpoint != 0) goto L_Skpoint;
	mes "噢! 好的! 你已經加入煉金術士公會，而且也學了一些基本的東西，干得好!";
	next;
	setoption 0;
	callfunc "Job_Change",Job_Alchem;
	jobchange 18;
	emotion 46;
	mes "[精通 煉金術士 賓珊特 卡樂士阿羅]";
	mes "從現在開始，你就是煉金術士公會所承認的一名煉金術士了.";
	mes "我希望你能繼續不斷地學習...";
	next;
	mes "[精通 煉金術士 賓珊特 卡樂士阿羅]";
	mes "這本說明書送給你";
	mes "就從這里開始你的各項研究吧!";
	set @jobgift,rand(1,6);
	if(JobLevel > 49) set @jobgift,0;
	if(@jobgift==1) getitem 7127,1;
	if(@jobgift==2) getitem 7128,1;
	if(@jobgift==3) getitem 7129,1;
	if(@jobgift==4) getitem 7130,1;
	if(@jobgift==5) getitem 7131,1;
	if(@jobgift==6) getitem 7132,1;
	if(@jobgift==0) getitem 7133,1;
	next;
	mes "[精通 煉金術士 賓珊特 卡樂士阿羅]";
	mes "那么，有機會再見吧...";
	mes "要以身為一名煉金術士為榮!";
	close;

	L_Skpoint:
		mes "你還有一些必備技能還沒學會吧? 等你先學會那些技能之后，再來申請吧";
		close;
}
