//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 冷豔舞姬 轉職腳本 - 
//===================================
// 版本：1.3c
//===================================

valkyrie,53,56,3	script	冷豔舞姬#Valkyrie	101,{
	if (ADVJOB == 0 || Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[冷豔舞姬]";
			mes "恭喜你轉生了!";
			mes "這是戰士們的光榮!";
			close;
		}
		mes "[冷豔舞姬]";
		mes "左邊踩一下再換右邊~ : [→]";
		mes ".....";
		close;
	}
	else if (ADVJOB == Job_Gypsy && Class == Job_Archer_High && JobLevel > 39) {
		mes "[冷豔舞姬]";
		mes "恢復本來面貌、";
		mes "給世界帶來新的曙光的時候來臨了";
		mes "請以嶄新的姿態繼續你的旅程吧";
		next;
		mes "[冷豔舞姬]";
		mes "要轉職嗎?";
		next;
		if (select("不要:轉職成為冷豔舞姬") == 1) {
			mes "[冷豔舞姬]";
			mes "準備好了的話，";
			mes "請隨時找我吧";
			mes "祝你好運!";
			close;
		}
		if (SkillPoint) {
			mes "[冷豔舞姬]";
			mes "好像學習的還不夠多，";
			mes "學會所有技能之後再來吧";
			close;
		}
		jobchange Job_Gypsy;
		set ADVJOB,0;
		mes "[冷豔舞姬]";
		mes "恭喜你轉職成功";
		mes "請回到陸地上，";
		mes "用你的舞蹈把快樂帶給人們吧";
		close;
	}
	mes "[冷豔舞姬]";
	mes "歡迎來到榮譽的巴哈拉，";
	mes "好好的休息再回去吧，";
	mes "戲給戰士們光榮!";
	close;
}