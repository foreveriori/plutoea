// 劍士轉職腳本
// 
// 重整作者:		Dino9021
//
// 版權宣告:		本文件由 eAthenaC 論壇提供，腳本結構屬原作者所有，翻譯及修正之版權屬 Dino9021 所有
//			轉載請勿移除任何說明敘述與說明文件，並需註明論壇連結 http://eathena.twbbs.org
// 
// 中文版本編號:	20041118.0.2
//			20050420.0.1	修改職業判斷使用 eAthena 之變數，轉職命令改用 eAthena 之轉職函式
//
// 原始腳本:		不明
//
//============================================================


//=====================================================================================
// 劍士轉職任務 1.0
//=====================================================================================

izlude_in.gat,74,172,4	script	劍士公會人員#swd_1	119,{
		mes "[劍士公會服務人員]";
		mes "這裡是劍士公會！請問你有什麼事情嗎？";
		next;
		menu "轉職成為劍士。",L_APP,"詢問有關劍士。",L_INFO_S,"詢問有關轉職成為劍士的條件。",L_INFO_J,"放棄。",-;
		mes "[劍士公會服務人員]";
		mes "年輕就是本錢啊∼∼再多考慮看看吧。";
		close;
L_APP:
	if (callfunc("Is_Sword_Class")) goto OUT_1;
	if (BaseJob != Job_Novice) goto OUT_2;
	if ((BaseJob == Job_Novice) && (Upper == 1) && (ReBirth != Job_Knight) && (ReBirth != Job_Crusader)) goto OUT_2;		// 判斷轉生過但原職業非劍士系
	if (JobLevel < 10) goto OUT_3;
	if (SkillPoint != 0) goto OUT_4;
	if (JOB_SW_Q != 0) goto L_NEXT_1;
	mes "[劍士公會服務人員]";
	mes "你想要轉職成為劍士？好的，請先填寫劍士轉職申請書。";
	next;
	menu "申請轉職為劍士。",L_APP_IN,"放棄。",-;

	mes "[劍士公會服務人員]";
	mes "往後若是想到的話，再來申請好了。";
	close;

L_APP_IN:
		if ((BaseJob == Job_Novice) && (Upper == 1) && ((ReBirth == Job_Knight) || (ReBirth == Job_Crusader))) goto HighSwdM;			// 判斷轉生過且原職業為劍士系
		savepoint "izlude_in.gat",65,165;
		set JOB_SW_Q,1;
		mes "[劍士公會服務人員]";
		mes "很好，我馬上為你遞出申請書。";
		mes "啊，你已經準備好轉職成為劍士的條件的話，你可以立刻考口試。";
		mes "要不要？";
		next;
		menu "要。",L_NEXT_1,"不要。",-;

		mes "[劍士公會服務人員]";
		mes "如果對於轉職條件有任何疑問的話，歡迎向我詢問。";
		mes "接下來只要符合我們所開出來的條件就可以了。祝你好運！";
		close;

HighSwdM:
	mes "[劍士公會服務人員]";
	mes "嗯，你的基本技能等級足夠。轉生名單上也有你的名字，我就接受你的請求，讓你轉職成為上位劍士。";
	next;
	setoption 0;
	callfunc "Job_Change",Job_Swordman;
	skill 144,1,0;
	skill 145,1,0;
	skill 146,1,0;
	mes "[劍士公會服務人員]";
	mes "恭喜你再次成為劍士，希望你往後能踴躍參與劍士公會舉辦的各種活動。";
	close;

L_NEXT_1:
		mes "[劍士公會服務人員]";
		mes "要嗎？那麼，我看看你的力量吧。";
		next;
		mes "[劍士公會服務人員]";
	if (JOB_SW_Q == 4) goto SUCCESS;
		mes "初心者的技能等級已經夠了，實戰考試還沒有考過，是不是？";
		mes "如果連這種最基本的考試都過不了，你想要成為劍士的夢想，只能衝出大氣層飛向遙遠的宇宙，繼續賣力的飛行。";
		next;
		mes "[劍士公會服務人員]";
		mes "考試地點，在左邊的房間。";
		mes "雖然入口是封閉的，但是向劍士工會服務人員詢問的話，他們就會讓你進去的。";
		close;

SUCCESS:
		mes "恭喜你！你現在已經成為人人稱羨的劍士了！";
		mes "現在就馬上開始轉職！";
		next;
		setoption 0;
		callfunc "Job_Change",Job_Swordman;
		mes "[劍士公會服務人員]";
		mes "再次恭喜你成為劍士，希望你往後能踴躍參與劍士公會舉辦的各種活動。";
		close;
OUT_1:
		mes "[劍士公會服務人員]";
		mes "哈哈哈哈，你真幽默！你的職業已經是劍士啦！";
		close;
OUT_2:
		mes "[劍士公會服務人員]";
		mes "哈哈！真可笑！我代表劍士公會慎重地拒絕。";
		mes "你已經從事劍士以外的職業了，不是嗎？";
		close;
OUT_3:
		mes "[劍士公會服務人員]";
		mes "你基本技能等級還未到達要求的標準呢！";
		mes "問清楚轉職條件，等基本技能等級提升至要求的標準後再過來吧！";
		close;
OUT_4:
		mes "[劍士公會服務人員]";
		mes "嘿....既然你都已經確實的升級了，那為";
		mes "什麼不把技能學起來呢？";
		close;
L_INFO_S:
		mes "[劍士公會服務人員]";
		mes "想要對劍士這個職業瞭解更多嗎？好吧！那我就說給你聽吧。";
		next;
		mes "[劍士公會服務人員]";
		mes "劍士最大的優勢，就是在近距離戰時能發揮最強大的威力。";
		mes "近距離戰時，它比其它職業多三個優點！";
		next;
		mes "[劍士公會服務人員]";
		mes "１. 和其它職業相比ＨＰ高出許多。";
		mes "２. 除了弓箭類和權杖類之外，所有的武器都可以裝備，可以視情況需要充分發揮。";
		mes "３. 劍士的技能大部份都具有強大的攻擊力。";
		next;
		mes "[劍士公會服務人員]";
		mes "簡單說來就是這樣，我想你應該聽的懂。";
		mes "另外，我個人認為，劍士不管做什麼事，都是世界上最耀眼的職業！";
		close;
L_INFO_J:
		mes "[劍士公會服務人員]";
		mes "你想成為劍士？";
		if ((BaseJob == Job_Swordman) || ((BaseJob == Job_Novice) && (Upper == 1) && ((ReBirth == Job_Knight) || (ReBirth == Job_Crusader)))) mes "可是你的職業已經是劍士啦！";
		if ((BaseJob != Job_Swordman) && ((BaseJob == Job_Novice) && (Upper == 1) && (ReBirth != Job_Knight) && (ReBirth != Job_Crusader))) mes "可是，你已經有了其他的職業了，你是不是搞錯了？";
		mes "還是說你只想聽聽看條件是嗎？真的是這樣的話，說給你聽也無妨。";
		next;
		menu "是的，我想聽聽看。",L_GETINFO,"不了，我只是走錯路的。",-;

		mes "[劍士公會服務人員]";
		mes "我是不知道你為什麼在這裡閒晃啦。不過如果你想要出城的話，請往南邊去。";
		close;
L_GETINFO:
		mes "[劍士公會服務人員]";
		mes "想要成為劍士的話，基本技能等級要達到９級以上。若連這一點都辦不到的話，那還有什麼資格成為劍士呢？你說是不是啊？";
		next;
		mes "[劍士公會服務人員]";
		mes "第二 要通過劍士轉職考試的話，更詳細的內容，在考場等候區有考試服務人員會為你服務。";
		next;
		mes "[劍士公會服務人員]";
		mes "只要完全符合我們所要求的條件的話，我就會如你所願將你轉職成為劍士。";
		close;
}

izlude_in.gat,62,169,6	script	劍士公會人員#swd_2	85,{
		mes "[劍士公會服務人員]";
	if (callfunc("Is_Sword_Class")) goto OUT_1;
	if (BaseJob != Job_Novice) goto OUT_2;
	if (JobLevel < 10) goto OUT_3;
	if (JOB_SW_Q == 0) goto APP_FIRST;
	if (JOB_SW_Q == 4) goto SUCCESS;
		mes "加油吧，希望你早日達成目標。";
		next;
		savepoint "izlude_in.gat",65,165;
		warp "izlude_in",39,170;
		end;
APP_FIRST:
		mes "站住！想要參加轉職考試嗎？";
		mes "那麼首先，填好劍士轉職申請表格之後再來！";
		close;
SUCCESS:
		mes "你已經通過考試了，請到中央位置找轉職管理服務人員！";
		close;
OUT_1:
		mes "站住！這裡是初學者考試場！";
		mes "都已經轉職成劍士的傢伙，沒理由進到這裡面！回去吧！";
		close;
OUT_2:
		mes "是誰？！已經有其它職業的傢伙沒理由進到這裡面！回去吧！";
		close;
OUT_3:
		mes "站住！這裡是初心者技能等級不到９的傢伙不能進去的地方。";
		mes "若初心者技能等級到９的話，才能就讓你進去轉職考場。";
		close;
}

izlude_in.gat,30,175,4	script	劍士公會人員#swd_3	92,{
		mes "[劍士公會服務人員]";
		mes "有關實戰考試，簡單的向你說明一次！請你聽好不要到時候不認帳";
		next;
		mes "[劍士公會服務人員]";
		mes "這考試的目的是，我要確認參賽者，有沒有充沛的體力和堅強的意志力，作為身為一名劍士的後盾！";
		mes "若沒有天份的人，會在這場考試落榜。";
		next;
		mes "[劍士公會服務人員]";
		mes "考試 ^0083CF及格條件^000000非常簡單！";
		mes "考場裡面所有的行程都通過之後，到達最後檢查地點，向一位監考人員索取合格證就可以啦";
		next;
		mes "[劍士公會服務人員]";
		mes "相反的，參賽者放棄或者在有限的時間內，沒有抵達檢查點就算是失敗";
		mes "假如想要宣佈放棄的話，進入到每個場地入口處，或者在各檢查點向我們管理人員詢問";
		next;
		mes "[劍士公會服務人員]";
		mes "考場分為三個場地，有陷阱和迷路不小心失誤的話，可是會掉進無底洞的呦！";
		mes "祝你好運";
		close;
}

izlude_in.gat,30,163,0	script	考場負責人#swd_1	105,{
		mes "[考場負責人]";
	if (callfunc("Is_Sword_Class")) goto OUT_1;
	if (BaseJob != Job_Novice) goto OUT_2;
	if (JOB_SW_Q == 0) goto OUT_3;

	set $sword_map, getmapusers("job_sword1.gat");
	if($sword_map > 0) goto MSG_0;

	if (JOB_SW_Q == 1) goto MSG_1;
	if (JOB_SW_Q == 2) goto MSG_2;
		mes "不要放棄！再挑戰吧！";
		next;
		warp "job_sword1",10,245;
		end;
MSG_0:
		mes "[考場負責人]";
		mes "現在有正人在進行考試當中";
		mes "請在此等候！";
		close;

MSG_1:
		mes "想要成為劍士嗎？嗯！看起來很壯碩啊！";
		mes "和你以後的人生相比的話，這不算很難的行程，所以說不要太緊張好好的去再回來吧";
		next;
		set JOB_SW_Q,2;
		warp "job_sword1",10,245;
		end;
MSG_2:
		mes "重考嘛....不過，不放棄重新挑戰的精神，我很喜歡！吃下這個繼續加油啊！";
		next;
		getitem 512,5;
		set JOB_SW_Q,3;
		warp "job_sword1",10,245;
		end;
OUT_1:
	mes "你不是已經轉職了嗎？怎麼會進到這裡來？趕快出去吧！";
	next;
	warp "izlude_in",66,170;
	end;
OUT_2:
	mes "你是誰呀？！這裡是劍士考場，不是你進來的地方！趕快出去吧！";
	next;
	warp "izlude_in",66,170;
	end;
OUT_3:
	mes "如果你想要參加劍士轉職考試的話，請你先去報名。";
	next;
	warp "izlude_in",66,170;
	end;
}

// Examination Courses NPCs
//============================================================ 
// 1-1
job_sword1,230,245,2	script	考場服務人員#swd_1	105,{
	callfunc "F_JobSwdMedic","一";
}
job_sword1,230,242,2	script	考場服務人員#swd_2	105,{
	callfunc "F_JobSwdStaff",1;
}
job_sword1,230,207,2	script	考場服務人員#2swd_2	105,{
	callfunc "F_JobSwdMedic","二";
}
job_sword1,230,204,2	script	考場服務人員#2swd_3	105,{
	callfunc "F_JobSwdStaff",1;
}
job_sword1,223,167,2	script	馬修塔印#swd_1_success	92,{
	mapannounce "job_sword1","應試者 " + strcharinfo(0) + "。你成功通過考試",bc_map;
	set JOB_SW_Q,4;
	mes "[馬修塔印]";
	mes "恭喜你通過考試了！";
	mes "合格通知書，已經送達到管理部門了，從這邊走出去，到中央轉職管理部門詢問即可。";
	close2;
	warp "izlude_in",66,173;
	end;
}

// Examination Course Fucntions
//============================================================ 
function	script	F_JobSwdMedic	{
	percentheal 100,0;
	mes "[醫務小組]";
	mes "這裡是第 "+getarg(0)+" 個檢查點！ 我會幫你恢復體力，請你加油！";
	close;
}

function	script	F_JobSwdStaff	{
	mes "[考場服務人員]";
	mes "你要放棄考試嗎？";
	next;
	if (select("是:不是") == 1) {
		mapannounce "job_sword1","應試者 " + strcharinfo(0) + " 考生放棄考試了。",bc_map;
		warp "izlude_in",65,165;
		end;
	}
	mes "[考場服務人員]";
	mes "明智的決定！靠著年青人的毅力再奮戰一次！";
	close;
}

function	script	F_JobSwdTestStaff	{
OnTouch:
	mes "[考場服務人員]";
	mes "你要放棄考試嗎？";
	next;
	if (select("是:不是") == 1) {
		mapannounce "job_sword1","應試者 " + strcharinfo(0) + " 考生放棄考試了。",bc_map;
		warp "izlude_in",65,165;
		end;
	}
	warp "job_sword1",getarg(0),getarg(1);
	end;
}

function	script	F_JobSwdTestStaff2	{
OnTouch:
	mapannounce "job_sword1","恭禧應試者 " + strcharinfo(0) + ". 通過第 "+getarg(0)+" 關。",bc_map;
	warp "job_sword1",getarg(1),getarg(2);
	end;
}

// Examination Course 1
//============================================================ 
job_sword1,223,243,0	warp	checkp1-2#swd_1	1,1,job_sword1,11,206;
job_sword1,223,205,0	warp	checkp2-3#swd_1	1,1,job_sword1,11,168;

job_sword1,7,245,0	script	考場服務人員#swd_4	45,1,3,{
	callfunc "F_JobSwdTestStaff",10,245;
}

job_sword1,8,207,0	script	考場服務人員#swd_5	45,1,6,{
	callfunc "F_JobSwdTestStaff",11,207;
}

job_sword1,8,169,0	script	考場服務人員#swd_6	45,1,6,{
	callfunc "F_JobSwdTestStaff",11,169;
}

job_sword1,192,244,0	script	考場服務人員#swd_7	45,1,3,{
	callfunc "F_JobSwdTestStaff2","一",215,244;
}

job_sword1,193,207,0	script	考場服務人員#swd_8	45,1,3,{
	callfunc "F_JobSwdTestStaff2","二",215,205;
}

job_sword1,193,168,0	script	考場服務人員#swd_9	45,1,3,{
	callfunc "F_JobSwdTestStaff2","三",215,167;
}

//==============================================================================
// 傳點、陷阱設定
//==============================================================================
job_sword1.gat,65,117,0	warp	swd101	1,1,job_sword1.gat,10,245
job_sword1.gat,98,27,0	warp	swd102	1,1,job_sword1.gat,11,207
job_sword1.gat,161,27,0	warp	swd103	1,1,job_sword1.gat,11,207
job_sword1.gat,239,117,0	warp	swd106	1,1,job_sword1.gat,11,169
job_sword1.gat,192,244,0	warp	SwordWarp0	2,2,job_sword1.gat,215,244

job_sword1.gat,192,206,0	warp	SwordWarp2	2,2,job_sword1.gat,215,205
job_sword1.gat,192,168,0	warp	SwordWarp4	2,2,job_sword1.gat,215,167

// Pitfalls (Set 1)
//============================================================ 
job_sword1,16,251,0	script	1_blank_1_a::SwdTrap1	-1,0,1,{
OnTouch:
	switch(rand(1,5)) {
	case 1: warp "job_sword1",65,56; end;
	case 2: warp "job_sword1",29,26; end;
	case 3: warp "job_sword1",43,16; end;
	case 4: warp "job_sword1",23,112; end;
	case 5: warp "job_sword1",58,83; end;
	}
}
job_sword1,19,251,0	duplicate(SwdTrap1)	1_blank_1_b	-1,0,1
job_sword1,17,250,0	duplicate(SwdTrap1)	1_blank_1_c	-1,1,0
job_sword1,16,238,0	duplicate(SwdTrap1)	1_blank_2_a	-1,0,1
job_sword1,19,238,0	duplicate(SwdTrap1)	1_blank_2_b	-1,0,1
job_sword1,17,239,0	duplicate(SwdTrap1)	1_blank_2_c	-1,0,1
job_sword1,28,247,0	duplicate(SwdTrap1)	1_blank_3_a	-1,4,0
job_sword1,33,245,0	duplicate(SwdTrap1)	1_blank_3_b	-1,0,2
job_sword1,29,242,0	duplicate(SwdTrap1)	1_blank_3_c	-1,4,0
job_sword1,24,244,0	duplicate(SwdTrap1)	1_blank_3_d	-1,0,2
job_sword1,38,251,0	duplicate(SwdTrap1)	1_blank_4_a	-1,0,1
job_sword1,41,251,0	duplicate(SwdTrap1)	1_blank_4_b	-1,0,1
job_sword1,39,250,0	duplicate(SwdTrap1)	1_blank_4_c	-1,1,0
job_sword1,38,238,0	duplicate(SwdTrap1)	1_blank_5_a	-1,0,1
job_sword1,41,238,0	duplicate(SwdTrap1)	1_blank_5_b	-1,0,1
job_sword1,39,239,0	duplicate(SwdTrap1)	1_blank_5_c	-1,1,0
job_sword1,54,251,0	duplicate(SwdTrap1)	1_blank_6_a	-1,0,1
job_sword1,71,251,0	duplicate(SwdTrap1)	1_blank_6_b	-1,0,1
job_sword1,62,250,0	duplicate(SwdTrap1)	1_blank_6_c	-1,9,0
job_sword1,62,247,0	duplicate(SwdTrap1)	1_blank_7_a	-1,8,0
job_sword1,71,244,0	duplicate(SwdTrap1)	1_blank_7_b	-1,0,2
job_sword1,63,242,0	duplicate(SwdTrap1)	1_blank_7_c	-1,8,0
job_sword1,54,244,0	duplicate(SwdTrap1)	1_blank_7_d	-1,0,2
job_sword1,54,238,0	duplicate(SwdTrap1)	1_blank_8_a	-1,0,1
job_sword1,71,238,0	duplicate(SwdTrap1)	1_blank_8_b	-1,0,1
job_sword1,62,239,0	duplicate(SwdTrap1)	1_blank_8_c	-1,9,0
job_sword1,102,247,0	duplicate(SwdTrap1)	1_blank_9_a	-1,2,0
job_sword1,105,245,0	duplicate(SwdTrap1)	1_blank_9_b	-1,0,2
job_sword1,103,242,0	duplicate(SwdTrap1)	1_blank_9_c	-1,2,0
job_sword1,100,244,0	duplicate(SwdTrap1)	1_blank_9_d	-1,0,2
job_sword1,156,249,0	duplicate(SwdTrap1)	1_blank_10_a	-1,14,0
job_sword1,156,248,0	duplicate(SwdTrap1)	1_blank_10_b	-1,14,0
job_sword1,170,249,0	duplicate(SwdTrap1)	1_blank_10_c	-1,1,0
job_sword1,170,248,0	duplicate(SwdTrap1)	1_blank_10_d	-1,1,0
job_sword1,156,245,0	duplicate(SwdTrap1)	1_blank_11_a	-1,14,0
job_sword1,156,244,0	duplicate(SwdTrap1)	1_blank_11_b	-1,14,0
job_sword1,170,245,0	duplicate(SwdTrap1)	1_blank_11_c	-1,1,0
job_sword1,170,244,0	duplicate(SwdTrap1)	1_blank_11_d	-1,1,0
job_sword1,156,241,0	duplicate(SwdTrap1)	1_blank_12_a	-1,14,0
job_sword1,156,240,0	duplicate(SwdTrap1)	1_blank_12_b	-1,14,0
job_sword1,170,241,0	duplicate(SwdTrap1)	1_blank_12_c	-1,1,0
job_sword1,170,240,0	duplicate(SwdTrap1)	1_blank_12_d	-1,1,0
job_sword1,180,251,0	duplicate(SwdTrap1)	1_blank_13_a	-1,0,1
job_sword1,183,251,0	duplicate(SwdTrap1)	1_blank_13_b	-1,0,1
job_sword1,181,250,0	duplicate(SwdTrap1)	1_blank_13_c	-1,1,0
job_sword1,180,238,0	duplicate(SwdTrap1)	1_blank_14_a	-1,0,1
job_sword1,183,238,0	duplicate(SwdTrap1)	1_blank_14_b	-1,0,1
job_sword1,181,239,0	duplicate(SwdTrap1)	1_blank_14_c	-1,1,0

// Pitfalls (Set 2)
//============================================================ 
job_sword1,56,212,0	script	2_blank_1_a::SwdTrap2	-1,40,0,{
OnTouch:
	switch(rand(1,5)) {
	case 1: warp "job_sword1",162,120; end;
	case 2: warp "job_sword1",94,120; end;
	case 3: warp "job_sword1",94,85; end;
	case 4: warp "job_sword1",162,85; end;
	case 5: warp "job_sword1",130,47; end;
	}
}

job_sword1,95,212,0	duplicate(SwdTrap2)	2_blank_1_b	-1,2,0
job_sword1,56,210,0	duplicate(SwdTrap2)	2_blank_2_a	-1,40,0
job_sword1,95,210,0	duplicate(SwdTrap2)	2_blank_2_b	-1,2,0
job_sword1,16,206,0	duplicate(SwdTrap2)	2_blank_2_c	-1,0,3
job_sword1,97,206,0	duplicate(SwdTrap2)	2_blank_2_d	-1,0,3
job_sword1,56,203,0	duplicate(SwdTrap2)	2_blank_2_e	-1,40,0
job_sword1,95,203,0	duplicate(SwdTrap2)	2_blank_2_f	-1,2,0
job_sword1,56,201,0	duplicate(SwdTrap2)	2_blank_3_a	-1,40,0
job_sword1,95,201,0	duplicate(SwdTrap2)	2_blank_3_b	-1,2,0

job_sword1,113,212,0	duplicate(SwdTrap2)	2_blank_4_a	-1,14,0
job_sword1,125,212,0	duplicate(SwdTrap2)	2_blank_4_b	-1,2,0
job_sword1,113,210,0	duplicate(SwdTrap2)	2_blank_5_a	-1,14,0
job_sword1,125,210,0	duplicate(SwdTrap2)	2_blank_5_b	-1,2,0
job_sword1,100,206,0	duplicate(SwdTrap2)	2_blank_5_c	-1,0,3
job_sword1,127,206,0	duplicate(SwdTrap2)	2_blank_5_d	-1,0,3
job_sword1,113,203,0	duplicate(SwdTrap2)	2_blank_5_e	-1,14,0
job_sword1,125,210,0	duplicate(SwdTrap2)	2_blank_5_f	-1,2,0
job_sword1,113,201,0	duplicate(SwdTrap2)	2_blank_6_a	-1,14,0
job_sword1,113,201,0	duplicate(SwdTrap2)	2_blank_6_b	-1,2,0

job_sword1,155,212,0	duplicate(SwdTrap2)	2_blank_7_a	-1,21,0
job_sword1,181,212,0	duplicate(SwdTrap2)	2_blank_7_b	-1,2,0
job_sword1,155,210,0	duplicate(SwdTrap2)	2_blank_8_a	-1,21,0
job_sword1,181,210,0	duplicate(SwdTrap2)	2_blank_8_b	-1,2,0
job_sword1,130,206,0	duplicate(SwdTrap2)	2_blank_8_c	-1,0,3
job_sword1,183,206,0	duplicate(SwdTrap2)	2_blank_8_d	-1,0,3
job_sword1,155,203,0	duplicate(SwdTrap2)	2_blank_8_e	-1,21,0
job_sword1,181,203,0	duplicate(SwdTrap2)	2_blank_8_f	-1,2,0
job_sword1,155,201,0	duplicate(SwdTrap2)	2_blank_9_a	-1,40,0
job_sword1,181,201,0	duplicate(SwdTrap2)	2_blank_9_b	-1,2,0

// Pitfalls (Set 3)
//============================================================ 
job_sword1,17,174,0	script	3_blank_1_a::SwdTrap3	-1,2,0,{
OnTouch:
	switch(rand(1,5)) {
	case 1: warp "job_sword1",195,15; end;
	case 2: warp "job_sword1",195,38; end;
	case 3: warp "job_sword1",231,30; end;
	case 4: warp "job_sword1",198,65; end;
	case 5: warp "job_sword1",196,116; end;
	}
}

job_sword1,17,163,0	duplicate(SwdTrap3)	3_blank_2_a	-1,2,0
job_sword1,29,171,0	duplicate(SwdTrap3)	3_blank_3_a	-1,2,0
job_sword1,31,168,0	duplicate(SwdTrap3)	3_blank_3_b	-1,0,2
job_sword1,28,166,0	duplicate(SwdTrap3)	3_blank_3_c	-1,2,0
job_sword1,26,168,0	duplicate(SwdTrap3)	3_blank_3_d	-1,0,2
job_sword1,36,169,0	duplicate(SwdTrap3)	3_blank_4_a	-1,0,0
job_sword1,37,169,0	duplicate(SwdTrap3)	3_blank_4_b	-1,0,0
job_sword1,37,168,0	duplicate(SwdTrap3)	3_blank_4_c	-1,0,0
job_sword1,36,168,0	duplicate(SwdTrap3)	3_blank_4_c2	-1,0,0
job_sword1,40,175,0	duplicate(SwdTrap3)	3_blank_5_a	-1,0,1
job_sword1,41,175,0	duplicate(SwdTrap3)	3_blank_5_b	-1,0,1
job_sword1,41,171,0	duplicate(SwdTrap3)	3_blank_6_a	-1,1,0
job_sword1,41,170,0	duplicate(SwdTrap3)	3_blank_6_b	-1,1,0
job_sword1,41,167,0	duplicate(SwdTrap3)	3_blank_6_c	-1,1,0
job_sword1,41,166,0	duplicate(SwdTrap3)	3_blank_6_d	-1,1,0
job_sword1,42,169,0	duplicate(SwdTrap3)	3_blank_6_e	-1,0,1
job_sword1,43,170,0	duplicate(SwdTrap3)	3_blank_6_f	-1,0,1
job_sword1,43,167,0	duplicate(SwdTrap3)	3_blank_6_g	-1,0,1
job_sword1,40,162,0	duplicate(SwdTrap3)	3_blank_7_a	-1,0,1
job_sword1,41,162,0	duplicate(SwdTrap3)	3_blank_7_b	-1,0,1
job_sword1,46,175,0	duplicate(SwdTrap3)	3_blank_8_a	-1,0,1
job_sword1,51,175,0	duplicate(SwdTrap3)	3_blank_8_b	-1,0,1
job_sword1,47,174,0	duplicate(SwdTrap3)	3_blank_8_c	-1,1,0
job_sword1,50,174,0	duplicate(SwdTrap3)	3_blank_8_d	-1,1,0
job_sword1,48,173,0	duplicate(SwdTrap3)	3_blank_8_e	-1,0,1
job_sword1,49,173,0	duplicate(SwdTrap3)	3_blank_8_f	-1,0,1
job_sword1,46,162,0	duplicate(SwdTrap3)	3_blank_9_a	-1,0,1
job_sword1,51,162,0	duplicate(SwdTrap3)	3_blank_9_b	-1,0,1
job_sword1,47,163,0	duplicate(SwdTrap3)	3_blank_9_c	-1,1,0
job_sword1,50,163,0	duplicate(SwdTrap3)	3_blank_9_d	-1,1,0
job_sword1,48,164,0	duplicate(SwdTrap3)	3_blank_9_e	-1,0,1
job_sword1,49,164,0	duplicate(SwdTrap3)	3_blank_9_f	-1,0,1
job_sword1,54,170,0	duplicate(SwdTrap3)	3_blank_10_a	-1,0,1
job_sword1,55,170,0	duplicate(SwdTrap3)	3_blank_10_b	-1,0,1
job_sword1,54,167,0	duplicate(SwdTrap3)	3_blank_10_c	-1,0,1
job_sword1,55,167,0	duplicate(SwdTrap3)	3_blank_10_d	-1,0,1
job_sword1,53,169,0	duplicate(SwdTrap3)	3_blank_10_e	-1,1,0
job_sword1,53,168,0	duplicate(SwdTrap3)	3_blank_10_f	-1,1,0
job_sword1,56,169,0	duplicate(SwdTrap3)	3_blank_10_g	-1,1,0
job_sword1,56,168,0	duplicate(SwdTrap3)	3_blank_10_h	-1,1,0
job_sword1,58,175,0	duplicate(SwdTrap3)	3_blank_11_a	-1,0,1
job_sword1,59,174,0	duplicate(SwdTrap3)	3_blank_11_b	-1,1,0
job_sword1,60,173,0	duplicate(SwdTrap3)	3_blank_11_c	-1,0,1
job_sword1,61,172,0	duplicate(SwdTrap3)	3_blank_11_d	-1,1,0
job_sword1,58,162,0	duplicate(SwdTrap3)	3_blank_12_a	-1,0,1
job_sword1,59,163,0	duplicate(SwdTrap3)	3_blank_12_b	-1,1,0
job_sword1,60,164,0	duplicate(SwdTrap3)	3_blank_12_c	-1,0,1
job_sword1,61,165,0	duplicate(SwdTrap3)	3_blank_12_d	-1,1,0
job_sword1,76,172,0	duplicate(SwdTrap3)	3_blank_13_a	-1,1,0
job_sword1,77,173,0	duplicate(SwdTrap3)	3_blank_13_b	-1,0,1
job_sword1,78,174,0	duplicate(SwdTrap3)	3_blank_13_c	-1,1,0
job_sword1,79,175,0	duplicate(SwdTrap3)	3_blank_13_d	-1,0,1
job_sword1,76,165,0	duplicate(SwdTrap3)	3_blank_14_a	-1,1,0
job_sword1,77,164,0	duplicate(SwdTrap3)	3_blank_14_b	-1,0,1
job_sword1,78,163,0	duplicate(SwdTrap3)	3_blank_14_c	-1,1,0
job_sword1,79,162,0	duplicate(SwdTrap3)	3_blank_14_d	-1,0,1
job_sword1,94,175,0	duplicate(SwdTrap3)	3_blank_15_a	-1,0,1
job_sword1,95,174,0	duplicate(SwdTrap3)	3_blank_15_b	-1,1,0
job_sword1,98,174,0	duplicate(SwdTrap3)	3_blank_15_c	-1,1,0
job_sword1,99,175,0	duplicate(SwdTrap3)	3_blank_16_d	-1,0,1
job_sword1,96,169,0	duplicate(SwdTrap3)	3_blank_17_a	-1,0,0
job_sword1,97,169,0	duplicate(SwdTrap3)	3_blank_17_b	-1,0,0
job_sword1,97,168,0	duplicate(SwdTrap3)	3_blank_17_c	-1,0,0
job_sword1,96,168,0	duplicate(SwdTrap3)	3_blank_17_d	-1,0,0
job_sword1,94,162,0	duplicate(SwdTrap3)	3_blank_18_a	-1,0,1
job_sword1,95,163,0	duplicate(SwdTrap3)	3_blank_18_b	-1,1,0
job_sword1,98,163,0	duplicate(SwdTrap3)	3_blank_18_c	-1,1,0
job_sword1,99,162,0	duplicate(SwdTrap3)	3_blank_18_d	-1,0,1
job_sword1,114,175,0	duplicate(SwdTrap3)	3_blank_19_a	-1,0,1
job_sword1,115,175,0	duplicate(SwdTrap3)	3_blank_19_b	-1,0,1
job_sword1,114,162,0	duplicate(SwdTrap3)	3_blank_20_a	-1,0,1
job_sword1,115,162,0	duplicate(SwdTrap3)	3_blank_20_b	-1,0,1
job_sword1,126,175,0	duplicate(SwdTrap3)	3_blank_21_a	-1,0,1
job_sword1,127,175,0	duplicate(SwdTrap3)	3_blank_21_b	-1,0,1
job_sword1,126,162,0	duplicate(SwdTrap3)	3_blank_23_a	-1,0,1
job_sword1,127,162,0	duplicate(SwdTrap3)	3_blank_23_b	-1,0,1
job_sword1,160,174,0	duplicate(SwdTrap3)	3_blank_24_a	-1,0,2
job_sword1,161,174,0	duplicate(SwdTrap3)	3_blank_24_b	-1,0,2
job_sword1,160,163,0	duplicate(SwdTrap3)	3_blank_25_a	-1,0,2
job_sword1,161,163,0	duplicate(SwdTrap3)	3_blank_25_b	-1,0,2
job_sword1,168,175,0	duplicate(SwdTrap3)	3_blank_26_a	-1,0,2
job_sword1,169,175,0	duplicate(SwdTrap3)	3_blank_26_b	-1,0,2
job_sword1,168,162,0	duplicate(SwdTrap3)	3_blank_27_a	-1,0,2
job_sword1,169,162,0	duplicate(SwdTrap3)	3_blank_27_b	-1,0,2
job_sword1,176,174,0	duplicate(SwdTrap3)	3_blank_28_a	-1,0,2
job_sword1,177,174,0	duplicate(SwdTrap3)	3_blank_28_b	-1,0,2
job_sword1,178,173,0	duplicate(SwdTrap3)	3_blank_28_c	-1,1,0
job_sword1,178,172,0	duplicate(SwdTrap3)	3_blank_28_d	-1,1,0
job_sword1,181,174,0	duplicate(SwdTrap3)	3_blank_28_e	-1,2,0
job_sword1,179,169,0	duplicate(SwdTrap3)	3_blank_29_a	-1,3,0
job_sword1,179,168,0	duplicate(SwdTrap3)	3_blank_29_b	-1,3,0
job_sword1,182,169,0	duplicate(SwdTrap3)	3_blank_29_c	-1,0,2
job_sword1,183,169,0	duplicate(SwdTrap3)	3_blank_29_d	-1,0,2
job_sword1,181,167,0	duplicate(SwdTrap3)	3_blank_29_e	-1,1,0
job_sword1,181,166,0	duplicate(SwdTrap3)	3_blank_29_f	-1,1,0
job_sword1,183,167,0	duplicate(SwdTrap3)	3_blank_29_g	-1,0,1
job_sword1,176,163,0	duplicate(SwdTrap3)	3_blank_30_a	-1,0,2
job_sword1,177,163,0	duplicate(SwdTrap3)	3_blank_30_b	-1,0,2
job_sword1,181,163,0	duplicate(SwdTrap3)	3_blank_30_c	-1,2,0

// Monsters (all three courses)
//============================================================ 
job_sword1.gat,0,0,0,0		monster	蒼蠅	1183,5,0,0,0
job_sword1.gat,201,36,0,0	monster	蒼蠅	1183,5,0,0,0
job_sword1.gat,161,94,0,0	monster	蒼蠅	1183,5,0,0,0
job_sword1.gat,35,79,0,0	monster	蒼蠅	1183,5,0,0,0
job_sword1.gat,35,78,0,0	monster	綠棉蟲	1184,4,0,0,0
job_sword1.gat,50,108,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,61,28,0,0	monster	綠棉蟲	1184,4,0,0,0
job_sword1.gat,61,92,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,110,112,0,0	monster	綠棉蟲	1184,3,0,0,0
job_sword1.gat,161,94,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,130,76,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,103,58,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,130,24,0,0	monster	綠棉蟲	1184,3,0,0,0
job_sword1.gat,201,36,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,201,16,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,239,44,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,239,76,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,231,101,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,234,117,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,198,91,0,0	monster	綠棉蟲	1184,2,0,0,0
job_sword1.gat,200,63,0,0	monster	綠棉蟲	1184,2,0,0,0


//============================================================ 
// mapflag
//============================================================ 
job_sword1	mapflag	nomemo
job_sword1	mapflag	noteleport
job_sword1	mapflag	nosave	SavePoint
job_sword1	mapflag	nopenalty
job_sword1	mapflag	nobranch
