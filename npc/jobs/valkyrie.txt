//===========================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -------------------------------------------
// - 轉生 腳本 -
// ------------------------------------------

valkyrie.gat,48,86,4	script	巴奇利	811,{
	if(Upper != 0) {
		mes "[巴奇利]";
		mes "不要停歇腳步，繼續追求更高的境界吧!";
		next;
		mes "[巴奇利]";
		mes "戰鬥才剛剛開始...";
		close;
	}
	mes "[巴奇利]";
	mes "凱旋歸來的英雄，歡迎你來到女武神聖殿，";
	mes "從現在起我將賦予你一個嶄新的生命";
	next;
	if(Weight || Zeny || SkillPoint || checkcart() || checkfalcon() || checkriding()) {
		mes "[巴奇利]";
		mes "好..你必須要達成英雄的條件，";
		mes "首先，先將等級提升到最大，所謂的名譽就是";
		mes "由身心功力最大之後才會出來的";
		next;
		mes "[巴奇利]";
		mes "所攜帶的物品及金幣，全部都要淨空，";
		mes "之前招換的動物，或是手推車等...";
		mes "任何東西都不能帶走，";
		mes "那麼...準備好之後再來吧";
		close2;
		warp "yuno_in02.gat",93,205;
		end;
	}
	mes "[巴奇利]";
	mes "請空出一個完整的身體以及完美的心靈來迎接新的生命";
	next;
	mes "[巴奇利]";
	mes "好...現在，";
	mes "一，兀兒德會留下你過去的記憶，";
	mes "二，蓓兒丹娣只記憶著目前光榮的剎那";
	mes "三，在詩寇蒂會過著往未來的生活";
	next;
	mes "[巴奇利]";
	mes "我數1時，你會想起以往的事...";
	next;
	mes "[巴奇利]";
	mes "我數2時，你看到的是眼前...";
	next;
	mes "[巴奇利]";
	mes "我數3時，你看到的是未來...";
	set OLD_CLASS,Class;
	jobchange 0,1;
	set BaseLevel,1;
	resetstatus;
	resetskill;
	setoption 0x0000;
	skill 143,1,0;
	skill 142,1,0;
	set SkillPoint,0;
	set StatusPoint,100;
	set TRANSMIGRATE,0;
	next;
	getitem 1202,1;
	getitem 2302,1;
	mes "[巴奇利]";
	mes "好，恭喜你，所有過程都已經結束了";
	mes "這是，我送給即將走向新生活戰士的小禮物";
	next;
	mes "[巴奇利]";
	mes "不要停歇腳步，繼續追求更高的境界吧!";
	close2;
	switch(OLD_CLASS) {
		case 7:
		case 14:
			savepoint "izlude.gat",94,103;
			break;
		case 8:
		case 15:
			savepoint "prontera.gat",273,354;
			break;
		case 9:
		case 16:
			savepoint "geffen.gat",120,100;
			break;
		case 10:
		case 18:
			savepoint "alberta.gat",116,57;
			break;
		case 11:
		case 19:
		case 20:
			savepoint "payon.gat",160,58;
			break;
		case 12:
		case 17:
			savepoint "morocc.gat",160,94;
			break;
	}
	warp "SavePoint",0,0;
	end;
}


yuno_in02.gat,88,164,5	script	玫泰武司 西芙#圖書館	742,{
	if(TRANSMIGRATE == 0) {
		if(BaseLevel < 99 || JobLevel < 50 || Class < 7 || Class > 20 || Upper != 0) {
			mes "[玫泰武司 西芙]";
			mes "歡迎光臨，這裡是修巴宜且樂比 魔法學校";
			mes "的圖書館，慢慢逛吧，";
			mes "這裡有很多書籍";
			close;
		}
		mes "[玫泰武司 西芙]";
		mes "歡迎光臨，這裡是修巴宜且樂比";
		mes "魔法學校";
		mes "的圖書館，來到這裡的目的是";
		mes "為了要看'依美樂之書'吧?";
		next;
		mes "[玫泰武司 西芙]";
		mes "依美樂之書的閱覽雖然已";
		mes "開放給所有人，有時也會";
		mes "有想要親自閱讀的人，不過";
		mes "由於原本的狀態並不太好，";
		mes "所以對想要閱覽的人我們會特別的";
		mes "收下樂捐並給予許可";
		next;
		mes "[玫泰武司 西芙]";
		mes "樂捐是1,285,000 金幣，";
		mes "是為了書籍的安全保管及";
		mes "古書籍的研究，如果願意的話";
		mes "現在馬上繳款並進場就可以了，";
		mes "請問要樂捐嗎?";
		next;
		if(select("要:取消")==2) {
			mes "[玫泰武司 西芙]";
			mes "是嗎?";
			mes "您不願意啊?";
			mes "那真是太可惜了!";
			mes "既然如此我也就不勉強了";
			mes "再會!";
			close;
		}
		if(Zeny < 1285000) {
			mes "[玫泰武司 西芙]";
			mes "你的金幣有點不足啊，";
			mes "想要捐獻的話，先確認一下";
			mes "口袋裡的錢再過來吧";
			close;
		}
		set Zeny,Zeny-1285000;
		set TRANSMIGRATE,1;
	}
	mes "[玫泰武司 西芙]";
	mes "謝謝你，這些樂捐我們會好好的";
	mes "使用在大學研究方面，";
	mes "你現在可以進去閱覽了";
	close;
}

yuno_in02.gat,93,207,0	script	依美樂之書	111,{
	if(Upper == 1) {
		mes "[依美樂之書]";
		mes "...進入榮譽神殿的入口";
		mes "已為重生的所有人們而打開，";
		mes "那將有助於邁向英雄之路";
		mes "往世上任何地方的路都會為你開著";
		next;
		mes "[依美樂之書]";
		mes "還有在那地方已為他們";
		mes "準備好了所有事情，聽說";
		mes "在現實生活中無法實現的願望，";
		mes "在那神殿是可以實現的";
		next;
		if(select("停止閱讀:繼續閱讀")==1) {
			mes "[依美樂之書]";
			mes "....";
			close;
		}
		mes "[依美樂之書]";
		mes "離天空最近的地方，應該會有";
		mes "通往巴奇利的神殿之路....";
		mes "平凡的他們永遠不會知道的";
		close2;
		warp "valkyrie.gat",48,8;
		end;
	}
	if(TRANSMIGRATE == 0) {
		mes "[依美樂之書]";
		mes "......";
		close;
	}
	mes "[依美樂之書]";
	mes "...然而伴隨前輩的無數功績";
	mes "同時可疑的地方是，面對英雄們";
	mes "必然會遇到有關'死亡' ";
	mes "的問題，所有被封上英雄稱號的人";
	mes "也都會有最後一刻的時候";
	next;
	mes "[依美樂之書]";
	mes "可是有關任何英雄的歌或一些記錄，";
	mes "就算是以口頭流傳下來的故事，";
	mes "也沒有留下那最後的紀錄，";
	mes "關於這必須要注意一點的是，";
	mes "那就是記述在 '仙境傳說' 裡";
	mes "有關巴奇利的內容";
	next;
	mes "[依美樂之書]";
	mes "對於人在巔峰時最棒的那一刻擁有";
	mes "偉大的素質者的人，在最後的聖戰上";
	mes "以戰士打仗，他的魂會隨著";
	mes "巴奇利的領導而走向巴哈拉，";
	mes "他跟一般所有人的最後不同，";
	mes "而是有關英雄的輪迴，";
	next;
	mes "[依美樂之書]";
	mes "以真正的英雄重新復活";
	mes "成為照亮新時代的人，";
	mes "跌倒的身體會再爬起來，";
	mes "靈魂喚起新的能力，";
	mes "但是很不幸的依美樂的心臟，";
	mes "在戰亂之後從大陸上消失了";
	next;
	mes "[依美樂之書]";
	mes "一點點的發現並不能支持我們";
	mes "做研究，沒有驗證過的";
	mes "就無法嘗試任何事，";
	mes "所以我就留下這紀錄，";
	mes "等後人發現這紀錄";
	mes "和他的意志及希望，希望能夠";
	next;
	mes "[依美樂之書]";
	mes "重生為點亮時代最後一道光";
	mes "的英雄，再次苦苦的等待";
	mes "希望領導巴奇利神殿之";
	mes "英雄們的人出現，還有那以後...";
	mes "........";
	set TRANSMIGRATE,2;
	close;
}

yuno_in05.gat,49,43,0	script	依美樂的心臟	111,{
	if(TRANSMIGRATE == 2)
		warp "valkyrie.gat",48,8;
	end;
}

valkyrie.gat,44,33,5	script	泰樂柏特	124,{
	if(Upper != 1) {
		if(rand(3)) {
			mes "[泰樂柏特]";
			mes "恭喜你轉生成功，";
			mes "獻給戰士們光榮!";
		}
		else {
			mes "[泰樂柏特]";
			mes "請不要觸摸展示品";
			mes "........";
		}
		close;
	}
	mes "[泰樂柏特]";
	mes "記住光榮時刻的你";
	mes "請問要去哪裡呢?";
	next;
	switch (select("普隆德拉:夢羅克:斐楊:吉芬:艾爾貝塔:伊斯魯德:艾爾帕蘭:克魔島:朱諾")) {
		case 1: savepoint "prontera.gat",116,72; 	break;
		case 2: savepoint "morocc.gat",156,46; 		break;
		case 3: savepoint "payon.gat",160,58; 		break;
		case 4: savepoint "geffen.gat",120,39; 		break;
		case 5: savepoint "alberta.gat",117,56; 	break;
		case 6: savepoint "izlude.gat",91,105; 		break;
		case 7: savepoint "aldebaran.gat",160,109; 	break;
		case 8: savepoint "comodo.gat",209,143; 	break;
		case 9: savepoint "yuno.gat",328,101; 		break;
	}
	warp "SavePoint",0,0;
	end;
}

// Warps
//============================================================ 
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75


function	script	JobChanger	{
	if(JobLevel < 40 || Class != getarg(1) || OLD_CLASS != getarg(2))
		return;
	mes "["+getarg(0)+"]";
	mes "找回你榮譽的樣子";
	mes "照亮時代的時機又回來了";
	mes "以新的模樣走向那英雄的路吧!";
	next;
	mes "["+getarg(0)+"]";
	mes "要轉職為"+getarg(0)+"嗎?";
	next;
	if(select("不了:是的")==1) {
		mes "["+getarg(0)+"]";
		mes "請你準備之後再來吧！";
		mes "戲給戰士們光榮!!";
		close;
	}
	if(SkillPoint) {
		mes "["+getarg(0)+"]";
		mes "不能有剩餘的技能點數";
		close;
	}
	nude;
	jobchange getarg(2),1;
	set OLD_CLASS,0;
	if(Class==18 && SKILL_BIOETHICS==13) {
		skill 238,1,0;
		set SKILL_BIOETHICS,0;
	}
	mes "["+getarg(0)+"]";
	mes "恭喜你，回到世上之後，";
	mes "希望能成為用你的能力";
	mes "領導全部的人";
	close;
}