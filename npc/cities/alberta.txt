//===== eAthena Script ======================================= 
//= Alberta Town
//===== By: ================================================== 
//= DZeroX
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= Town-specific Alberta NPCs
//===== Additional Comments: ================================= 
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//============================================================

alberta.gat,97,51,0	script	商人	84,{
	mes "[商人]";
	mes "這世上流傳著許許多多的傳聞，其中有一個傳聞是這麼說的，據說有一種卡片可以用來封住怪物的力量。";
	mes "如果使用那張卡片的話，還能夠擁有強大的力量…呼！不過那可能也只是傳說而已。";
	close;
}

alberta.gat,53,39,0	script	商人	100,{
	mes "[商人]";
	mes "呃？";
	mes "呃？";
	mes "對不起，現在沒有時間，請你再來說好嗎？";
	mes "聽說魔法之都吉芬有在販賣一種可以抵抗魔法的神秘防具，如果可以大量買賣那種道具的話，那我一定會賺大錢的。";
	close;
}

alberta.gat,20,183,0	script	青年	49,{
	mes "[青年]";
	mes "傳說中有一種叫做剛尼爾長矛的武器，一旦鎖定目標，一定百發百中，彈無虛發…要是真的有這種武器的話…應該會很棒吧？？";
	close;
}

alberta.gat,189,151,5	script	船員	100,{
	mes "[船員]";
	mes "你要去哪個地方？";
	switch(select("沉船附近的島嶼 -> 250 zeny.","依斯魯德島 -> 500 zeny.","離開")) {
		case 1:
			next;
			if (Zeny < 250) {
				mes "[船員]";
				mes "對不起，你至少得有250Zeny，我才可以送你去。";
				close;
			}
			set Zeny,Zeny - 250;
			warp "alb2trea.gat",43,53;
			end;
		case 2:
			next;
			if (Zeny < 500) {
				mes "[船員]";
				mes "對不起，你至少得有500Zeny，我才可以送你去。";
				close;
			}
			set Zeny,Zeny - 500;
			warp "izlude.gat",176,182;
			end;
		case 3:
			next;
			mes "[Fisk]";
			mes "Alright...";
			mes "Landlubber.";
			close;
	}
}

alb2trea.gat,39,50,6	script	船員#a2t	100,{
	mes "[船員]";
	mes "你要回去了嗎？";
	switch(select("是的，回愛爾貝塔","取消")) {
	case 1:
		warp "alberta.gat",192,169;
	case 2:
		close;
	}
}

alberta.gat,131,139,2	script	Drunken Old Man	54,{
	mes "[笨伯力]";
	mes "...看...看什麼，給我走開！";
	switch(select("不理他","走開點好了")) {
		case 1:
			next;
			mes "[笨伯力]";
			mes "喔喔喔，有骨氣的傢伙，嘿...我本來是高英海盜團的船員。";
			switch(select("那是什麼？","哇！真帥！")) {
				case 1:
					next;
					mes "[笨伯力]";
					mes "啊！你不知道？你不知道鼎鼎大名的海盜團嗎";
					next;
					mes "[笨伯力]";
					mes "啊... 好懷念啊... 當時沒有碰到那颱風的話，我現在就還在海上稱霸。";
					next;
					mes "[笨伯力]";
					mes "我的船長...他手上拿的那把武器，竟然有著神秘的力量，很多人都喪命於那把武器。";
					next;
					mes "[笨伯力]";
					mes "啊... 好想念他們啊... 嗯... 真心碎了... 你走開啊！";
					close;
				case 2:
					next;
					mes "[笨伯力]";
					mes "我的船長...他手上拿的那把武器竟然有著神秘的力量，很多人都喪命於那把武器。";
					next;
					mes "[笨伯力]";
					mes "啊... 好想念他們啊... 嗯... 真心碎了... 你走開啊！";
					close;
			}
		case 2:
			next;
			mes "[笨伯力]";
			mes "算你聰明! 快走開啊！";
			close;
	}
}

alberta.gat,58,80,2	script	馬傑	99,{
	set .@random,rand(1,2);
	if (.@random == 1) {
		mes "[馬傑]";
		mes "我們商人，可以開店做生意，用殺價技巧從其它店買進便宜貨，然後借手推車，放在手推車上面增加載貨量。";
		next;
		mes "[馬傑]";
		mes "也不需要一個一個去交易，我們有著一個神秘的技能，可以自動販賣物品";
		close;
	} else {
		mes "[馬傑]";
		mes "我們商人，也有一個高價賣出的特殊技能，可以把賣的東西價格拉到更高再賣出";
		next;
		mes "[馬傑]";
		mes "我已經用高出24%的價錢賣給其它商店，不過這種技巧要不段的修煉！";
		close;
	}
}

alberta.gat,62,156,2	script	畢力都	102,{
	set .@random,rand(1,3);
	if (.@random == 1) {
		mes "[畢力都]";
		mes "通往艾爾貝塔，連接斐楊城的森林裡，有一群笨熊在那出現，身上圍繞著蒼蠅。";
		next;
		mes "[畢力都]";
		mes "我抱著調皮的心情把樹枝丟過去，它突然衝過來，害我嚇一跳。";
		mes "還好我逃的快，我想有的動物看起來很可愛，但是一生氣起來就會讓人喪命喔。";
		close;
	}
	if (.@random == 2) {
		mes "[畢力都]";
		mes "你知道嗎？狼其實是很團結的。";
		mes "同胞遇難時會衝過來幫忙的，你要小心哦！";
		close;
	}
	if (.@random == 3) {
		mes "[畢力都]";
   		mes "有一天走在森林裡，發現綠色直立的草，非常漂亮。因為太漂亮了，所以用手去摸， 那棵草卻突然輕輕的打我的手。";
		next;
		mes "[畢力都]";
   		mes "害我嚇一跳，往後退一步看，原來不是草是一個非常可愛的小動物。";
		close;
	}
}

alberta.gat,93,174,2	script	瑪利祖母	103,{
	mes "[瑪利祖母]";
	mes "不久之前，有一艘快解體的船，飄到艾爾貝塔魚獲區裡，村落裡的年輕人為了查看是否有生存者";
	next;
	mes "[瑪利祖母]";
	mes "結果嚇的跑出來，裡面沒有生存者";
	next;
	mes "[瑪利祖母]";
	mes "反而屍體爬起來走來走去";
	next;
	mes "[瑪利祖母]";
	mes "有人說要把船撈起來，再處理怪物，不曉得有沒有危險";
	close;
}

alberta.gat,195,151,2	script	保羅	86,{
	mes "[保羅]";
	mes "你好，你想去遇難船探險嗎?";
	next;
	mes "[保羅]";
	mes "噢! 我先警告你喔! 那裡可不是一般人能存活的地方.";
	next;
	mes "[保羅]";
	mes "你覺得如何? 只要給我 200 Zeny 我就帶你過去";
	switch(select("帶我去吧","不用了")) {
		case 1:
			next;
			if (Zeny < 200) {
				mes "[保羅]";
				mes "對不起，你至少得有 200 Zeny，我才可以送你去";
				close;
			} else {
				set Zeny,Zeny - 200;
				warp "alb2trea.gat",62,69;
				close;
			}
		case 2:
			next;
			mes "[保羅]";
			mes "如果你想去的話，隨時來找我!";
			close;
	}
}

alberta.gat,190,173,4	script	Phelix	85,{
	set .@weight,MaxWeight-Weight;
	if ((.@weight) < 10000) {
		mes "[Phelix]";
		mes "Wait a moment!!";
		mes "You have brought too many things!";
		mes "You cannot accept any more items!";
		mes "Please reduce the amount of items,";
		mes "then come see me again";
		close;
	}
	if (@event_zelopy == 0) {
		mes "[Phelix]";
		mes "The hell are you doing here?";
		mes "There is nothing you can get for";
		mes "free on this ship, if you want";
		mes "somethin', work for it!!";
		next;
		mes "[Phelix]";
		mes "Hmm, so why don't you bring me";
		mes "10 jellopies and I will give 1";
		mes "potion. How's that sound?";
		mes "Or if that's too hard for your";
		mes "pansy ass, 3 jellopies for 1";
		mes "Carrot.";
		next;
		mes "[Phelix]";
		mes "If you're interested in my offer,";
		mes "get me the stuff I mentioned.";
		set @event_zelopy,1;;
		close;
	} else {
		mes "[Phelix]";
		mes "Hmm... you want to exchange";
		mes "jellopies for Red Potions or some";
		mes "Carrots eh? Well... which one?";
		switch(select("Red Potions please.","Carrots please.")) {
			case 1:
				next;
				mes "[Phelix]";
				mes "Alright...";
				mes "Let's see";
				mes "what'cha got...";
				next;
				if (countitem(909) < 10) {
					mes "[Phelix]";
					mes "Hey! Weren't you listening? I said";
					mes "10 jellopies for 1 Red Potion.. are";
					mes "ya deaf?";
					close;
				} else {
					set .@max,countitem(909)/10;
					mes "[Phelix]";
					mes "Hmm, not bad...";
					mes "How many potions";
					mes "do you want to get?";
					switch(select("As many as I can, please.","I want this many.","Never mind, I like my jellopy.")) {
						case 1:
							next;
							delitem 909,.@max*10;
							getitem 501,.@max;
							mes "[Phelix]";
							mes "There you go! As I promised. Don't";
							mes "go suckin' them all down at once.";
							close;
						case 2:
							next;
							mes "[Phelix]";
							mes "I'm not giving you more than 100";
							mes "at a time so don't bother, OK? If";
							mes "you don't want any, just say '0'.";
							mes "Right now, the most you can get is";
							mes ""+.@max+" but remember, 100 at most,";
							mes "you want to break my back?";
							input .@amount;
								if (.@amount <= 0) {
									mes "[Phelix]";
									mes "Much obliged, come again anytime.";
									close;
								}
								if (.@amount > 100) {
									mes "[Phelix]";
									mes "Hey, what'd I say? 100 at a time at";
									mes "most, you're trying to kill me";
									mes "aren't you!";
									close;
								}
								if (countitem(909) < .@amount*10) {
									mes "[Phelix]";
									mes "Hmmm, it looks like you don't have";
									mes "enough. Go get more jellopies if";
									mes "you want anything else from me.";
									close;
								}
							delitem 909,.@amount*10;
							getitem 501,.@amount;
							next;
							mes "[Phelix]";
							mes "There you go! As I promised. Don't";
							mes "go suckin' them all down at once.";
							close;
						case 3:
							next;
							mes "[Phelix]";
							mes "No problem,";
							mes "see you next time.";
							close;
					}
				}
			case 2:
				next;
				mes "[Phelix]";
				mes "Alright, let's see what ya got...";
				next;
				if (countitem(909) < 3) {
					mes "[Phelix]";
					mes "Hmm, look pansy ass, I said 3";
					mes "jellopies for 1 Carrot.. got it?";
					close;
				} else {
					set .@max,countitem(909)/3;
					mes "[Phelix]";
					mes "Not too bad pansy...";
					mes "How many do you want?";
					switch(select("As many as I can get, please","I want this many.","Never mind, I like my jellopy.")) {
					case 1:
						next;
						delitem 909,.@max*3;
						getitem 515,.@max;
						mes "[Phelix]";
						mes "There you go~! As I promised. Try";
						mes "not to stuff yer face.";
						close;
					case 2:
						next;
						mes "[Phelix]";
						mes "Right I'm not giving you more than";
						mes "100 at a time so don't bother,";
						mes "okay? If you don't want any, just";
						mes "say '0'.";
						input .@amount;
						next;
							if (.@amount == 0) {
								mes "[Phelix]";
								mes "Alright then, see you next time.";
								close;
							}
							if (.@amount > 100) {
								mes "[Phelix]";
								mes "Hey pansy ass, I said 100 at most,";
								mes "no more than that! I'm not going to";
								mes "break my back for the likes of";
								mes "you!";
								close;
							}
							if (countitem(909) < .@amount*10) {
								mes "[Phelix]";
								mes "Seems you don't have enough. Go get";
								mes "some more if you want anything";
								mes "else.";
								close;
							}
						delitem 909,.@amount*3;
						getitem 515,.@amount;
						next;
						mes "[Phelix]";
						mes "There you go~! As I promised. Try";
						mes "not to stuff yer face.";
						close;
					case 3:
						next;
						mes "[Phelix]";
						mes "Catch'ya later.";
						close;
					}
				}
		}
	}
}

alberta.gat,43,49,5	script	Poor-looking Merchant	89,{
	mes "[Machen]";
	mes "Why...?";
	mes "How did my";
	mes "family's wealth";
	mes "and prestige just";
	mes "go down the drain?";
	next;
	mes "[Machen]";
	mes "Ever since the";
	mes "Alcheshs came to Alberta,";
	mes "they quickly took our place";
	mes "as the biggest business owners";
	mes "here. It's our biggest shame...";
	close;
}

alberta.gat,43,244,8	script	Tourist	99,{
	mes "[Tourist]";
	mes "Where am I...?";
	mes "...Who am I?";
	next;
	mes "[Tourist]";
	mes "..........";
	next;
	mes "[Tourist]";
	mes ".....";
	next;
	mes "[Tourist]";
	mes "........";
	next;
	mes "[Tourist]";
	mes "Mahahahahahahaha!!";
	next;
	mes "[Tourist]";
	mes "*Drools*...I love chocolate!";
	next;
	mes "[Tourist]";
	mes "Wait...";
	mes "I have a girlfriend...";
	mes "and...and...";
	mes "cookies...?";
	mes "I feel sooo lost...";
	next;
	mes "[Tourist]";
	mes "^3355FFWe don't talk to crazy people~^000000";
	close;
}

alberta.gat,165,83,4	script	Fastidious Old Man	755,{
	mes "[Frank]";
	mes "My back~";
	mes "It's so sore!";
	mes "And my eyes are";
	mes "hurting worse and worse.";
	mes "*Sigh* I'm getting old...";
	close;
}

// 不良食品青年 -----------------------
alberta.gat,90,71,4	script	不良食品青年	89,{
	mes "^3355FF- 滾滾滾滾 燙燙燙燙-^000000";
	next;
	mes "[蘇打人]";
	mes "啊啊 ~ 不要吵我！";
	mes "你在問我，在做什麼嗎？";
	mes "明明都知道還在問 ~";
	mes "來，在這小勺子上將砂糖與";
	mes "蘇打適當的混合 ..";
	mes "放在固體燃料上加熱";
	next;
	mes "[蘇打人]";
	mes "慢慢的慢慢的溶化";
	mes "到達一定程度的黏度及";
	mes "色澤時，嗚哇啊 !! 瞬間";
	mes "倒在鐵板上方 -";
	mes "用力壓來做膨糖(台語)";
	mes "或著直接吃也可以 -";
	next;
	mes "[蘇打人]";
	mes "知道我現在";
	mes "在說什麼嗎 ?";
	mes ". . . . . 說我很吵 ?";
	mes "嗚咳，那，那麼";
	mes "說說差點征服烏龜島的";
	mes "某位老先生的故事怎麼樣 ?";
	next;
	mes "[蘇打人]";
	mes "去艾爾貝塔的旅館";
	mes "找找看看，會遇到一位";
	mes "滿身酒臭味的老先生";
	mes "雖然是酒醉後說的話";
	mes "不過不太像慌慌張張";
	mes "亂講一通，直接去聽聽看吧";
	next;
	mes "[蘇打人]";
	mes "啊 對了還有";
	mes "仔細看那位老先生房間裡";
	mes "桌子上有";
	mes "有關烏龜島的";
	mes "可怕信件";
	mes "順便看看吧 ~";
	close;
}


// 菲歷斯 -----------------------
alberta.gat,190,173,4	script	菲歷斯	92,{
	mes "[菲歷斯]";
	mes "天下沒有不勞而獲的事，要獲得就得有付出！";
	next;
	mes "[菲歷斯]";
	mes "假如你能撿到10個傑勒比結晶，我就拿一瓶紅色藥水和你換，你覺得很難的話，撿到3個傑勒比結晶，也可以換到1個蕃薯。";
	next;
	mes "[菲歷斯]";
	mes "你要用傑勒比結晶換紅色藥水或蕃薯嗎？";
	next;
	menu "請給我紅藥水",Case1,"請給我蕃薯",Case2,"離開",Cancel;
	Case1:
		mes "[菲歷斯]";
		mes "讓我看看你拿來的傑勒比結晶..";
		next;
		mes "[菲歷斯]";
		mes "喔...好了，10個傑勒比結晶可以換1個紅色藥水，每次最少換1個，最多換100個，你要幾個呢？";
		next;
		input @number;
		if(@number < 1 || @number > 100)
			goto Failnum1;
		if(countitem(909) < @number * 10)
			goto Failno1;
		delitem 909,@number * 10;
		getitem 501,@number;
		mes "[菲歷斯]";
		mes "這是您要的紅藥水！希望它能在戰鬥中幫上您的忙！再見！";
		close;
		Failnum1:
			mes "[菲歷斯]";
			mes "噢！我剛才不是說了嗎？每次最少換1個，最多換100個。";
			close;
		Failno1:
			mes "[菲歷斯]";
			mes "對不起！你沒有那麼多的傑勒比結晶，換不到那麼多的紅色藥水哦！";
			close;
	Case2:
		mes "[菲歷斯]";
		mes "讓我看看你拿來的傑勒比結晶..";
		next;
		mes "[菲歷斯]";
		mes "喔...那好吧，3個傑勒比結晶可以換1個蕃薯，每次最少換1個，最多換100個，你要幾個呢？";
		next;
		input @number;
		if(@number < 1 || @number > 100)
			goto Failnum2;
		if(countitem(909) < @number * 3)
			goto Failno2;
		delitem 909,@number * 3;
		getitem 516,@number;
		mes "[菲歷斯]";
		mes "這是您要的蕃薯！希望它能在戰鬥中幫上您的忙！再見！";
		close;
		Failnum2:
			mes "[菲歷斯]";
			mes "噢！我剛才不是說了嗎？每次最少換1個，最多換100個。";
			close;
		Failno2:
			mes "[菲歷斯]";
			mes "噢！數量不夠喔！你要有三個傑勒比結晶才可以換蕃薯，你不知道嗎？";
			close;
	Cancel:
		mes "[菲歷斯]";
		mes "那好吧，等你有需要的時候再來找我吧！再見！";
		close;
}

//===================================================
tur_dun02.gat,132,251,0	script	烏龜樹根	111,{
	if (kame_quest == 3) goto LStart2;
	mes "^3355FF- 感覺到相當久歲月的 -^000000";
	mes "^3355FF- 粗樹根 -^000000";
	mes "^3355FF- 嚴重的彎曲著-^000000";
	mes "^3355FF- 埋在地裡 -^000000";
	close;
LStart2:
	mes "^3355FF- 在樹根下方 -^000000";
	mes "^3355FF- 有刻著樹根的 -^000000";
	mes "^3355FF- 鑰匙孔 -^000000";
	mes "";
	mes "^3355FF- 你將 '樹根鑰匙'  -^000000";
	mes "^3355FF- 放入鑰匙孔轉動 -^000000";
	next;
	mes "^3355FF- 撕卡-開門聲 -^000000";
	next;
	mes "^3355FF- 在樹根間 -^000000";
	mes "^3355FF- 門被打開 -^000000";
	mes "^3355FF- 有這樣的  -^000000";
	mes "^3355FF- 卷軸出現了 -^000000";
	next;
	mes "[卷軸]";
	mes "^FF3355為了完成^000000";
	mes "^FF3355終極之劍^000000";
	mes "^FF3355我來到了這裡^000000";
	mes "^FF3355過去在我的故鄉^000000";
	mes "^FF3355流傳下來的^000000";
	mes "^FF3355有關劍的，終極之故事^000000";
	next;
	mes "[卷軸]";
	mes "^FF3355可以砍下^000000";
	mes "^FF3355烏龜外殼的人，將可奪取^000000";
	mes "^FF3355全天下!^000000";
	mes "^FF3355為了實現這件事^000000";
	mes "^FF3355所以到達烏龜島^000000";
	mes "^FF3355的啊 ..^000000";
	next;
	mes "[卷軸]";
	mes "^FF3355- 被砍 -^000000";
	mes "^FF3355- 撕掉的是 -^000000";
	mes "^FF3355- 烏龜的背 !! -^000000";
	mes "^FF3355-  是` 緣 '做的 -^000000";
	next;
	mes "[卷軸]";
	mes "^FF3355= 走吧 .. =^000000";
	mes "^FF3355tur_dun02^000000";
	mes "^FF3355X : 46 . Y : 125^000000";
	mes "^FF3355往烏龜水晶堆裡^000000";
	next;
	mes "^3355FF- 卷軸內 -^000000";
	mes "^3355FF- 有畫著烏龜水晶的 -^000000";
	mes "^3355FF- 鑰匙，卷在裡面 -^000000";
	mes "";
	set kame_quest,4;
	close;
}
//===================================================
tur_dun02.gat,46,125,0	script	烏龜水晶堆	111,{
	if (kame_quest == 4) goto LStart2;
	mes "^3355FF- 只有在烏龜島裡 -^000000";
	mes "^3355FF- 看得到的 -^000000";
	mes "^3355FF- 烏龜水晶，大量的聚集在一起 -^000000";
	close;
LStart2:
	mes "^3355FF- 在烏龜水晶堆裡-^000000";
	mes "^3355FF- 有烏龜水晶樣子的 -^000000";
	mes "^3355FF- 鑰匙孔 -^000000";
	mes "";
	mes "^3355FF- 你將 '烏龜水晶鑰匙'  -^000000";
	mes "^3355FF- 插入鑰匙孔轉動了了 -^000000";
	next;
	mes "^3355FF-叮噹-撕卡-開門聲 -^000000";
	next;
	mes "^3355FF- 在水晶之間 -^000000";
	mes "^3355FF- 有一扇小門打開了 -^000000";
	mes "^3355FF-有這樣的 -^000000";
	mes "^3355FF- 水晶板出現了 -^000000";
	next;
	mes "[水晶板]";
	mes "^3355AA劍士 ' 緣 ' ^000000";
	mes "^3355AA將烏龜木砍成兩半^000000";
	mes "^3355AA程度修練之後^000000";
	mes "^3355AA再向烏龜水晶^000000";
	mes "^3355AA做挑戰^000000";
	next;
	mes "[水晶板]";
	mes "^3355AA烏龜水晶的表面^000000";
	mes "^3355AA非常的不規則^000000";
	mes "^3355AA假如不正確的^000000";
	mes "^3355AA砍下去的話^000000";
	mes "^3355AA連一個刮痕，都很難留在上方^000000";
	next;
	mes "[水晶板]";
	mes "^3355AA不過我^000000";
	mes "^3355AA一定要成功啊^000000";
	next;
	mes "[水晶板]";
	mes "^3355AA往東邊走五步^000000";
	mes "^3355AA往南邊走三十^000000";
	mes "^3355AA再多走五步左右 ..^000000";
	mes "^3355AA- 烏龜柱子 -^000000";
	mes "^3355AA那裡有終極的劍器^000000";
	mes "^3355AA深睡於此^000000";
	next;
	mes "^3355FF- 水晶板中間位置上 -^000000";
	mes "^3355FF- 貼著長長的鑰匙 -^000000";
	mes "";
	set kame_quest,5;
	close;
}
//===================================================
tur_dun02.gat,49,156,0	script	烏龜柱子	111,{
	if (kame_quest == 5) goto LStart2;
	mes "^3355FF- 這些柱子是支撐-^000000";
	mes "^3355FF-烏龜島內部的-^000000";
	mes "^3355FF- 很多烏龜柱子之中的一個-^000000";
	close;
LStart2:
	mes "^3355FF- 在烏龜柱子下方 -^000000";
	mes "^3355FF- 有烏龜圖樣的 -^000000";
	mes "^3355FF- 鑰匙孔 -^000000";
	mes "";
	mes "^3355FF- 你將 ' 烏龜柱子鑰匙 '  -^000000";
	mes "^3355FF- 插入鑰匙孔轉動了了 -^000000";
	next;
	mes "^3355FF- 卡咳-卡咳-開門聲 -^000000";
	next;
	mes "^3355FF- 柱子下方 -^000000";
	mes "^3355FF- 門被打開 -^000000";
	mes "^3355FF- 有這樣的石珠 -^000000";
	mes "^3355FF- 滾出來了 -^000000";
	next;
	mes "[烏龜石珠]";
	mes "^33AA55在烏龜島裡^000000";
	mes "^33AA55最堅固的是^000000";
	mes "^33AA55稱號^000000";
	mes "^33AA55烏龜柱子 ..^000000";
	mes "^33AA55那是用這世上的^000000";
	mes "^33AA55劍，用劍術^000000";
	next;
	mes "[烏龜石珠]";
	mes "^33AA55也沒辦法砍下去的^000000";
	mes "^33AA55假如將柱子砍下時^000000";
	mes "^33AA55烏龜島內^000000";
	mes "^33AA55將不再是^000000";
	mes "^33AA55烏龜島了..^000000";
	mes "^33AA55劍術是這樣的嗎^000000";
	next;
	mes "[烏龜石珠]";
	mes "^33AA55可以砍下^000000";
	mes "^33AA55但不砍^000000";
	mes "^33AA55但卻擺著砍下的^000000";
	mes "^33AA55姿勢 .. 這就是^000000";
	mes "^33AA55所謂終極的^000000";
	mes "^33AA55劍術嗎 ?^000000";
	next;
	mes "[烏龜石珠]";
	mes "^33AA55最後^000000";
	mes "^33AA55到現在?止讓我的^000000";
	mes "^33AA55劍術成長^000000";
	mes "^33AA55為了留下劍術，成長的記錄^000000";
	mes "^33AA55而犧牲的^000000";
	mes "^33AA55各種畜生的血^000000";
	next;
	mes "[烏龜石珠]";
	mes "^33AA55為了紀念他門^000000";
	mes "^33AA55留下這樣的物品^000000";
	mes "^33AA55-- 緣 --^000000";
	next;
	mes "^3355FF- 柱子內部 -^000000";
	mes "^3355FF- 看的到各種物品 -^000000";
	getitem 716,1;
	set kame_quest,6;
	mes "^3355FF- 你獲得了 -^000000";
	mes "^3355FF- 紅色魔力礦石-^000000";
	close;
}