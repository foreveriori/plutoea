// 旅館腳本
// 
// 中文化作者:		Dino9021
//
// 版權宣告:		本文件由 eAthenaC 論壇提供，腳本結構屬原作者所有，翻譯及修正之版權屬 Dino9021 所有
//			轉載請勿移除任何說明敘述與說明文件，並需註明論壇連結 http://eathena.twbbs.org
// 
// 中文版本編號:	20041130.0.1
//			20050404.0.1	修改一個旅館睡覺的座標、修改 Lv.90 以上要 5000 Zeny
//
//============================================================

//===== eAthena Script ======================================= 
//= Inn Npcs
//===== By: ================================================== 
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0+
//===== Description: ========================================= 
//= Inn Npcs, Save and Heal
//=====================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Additional Comments: ================================= 
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=	  and save points[erKURITA]
//============================================================ 


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in.gat,244,135,2	script	旅館 侍女::Sammy	53,{

	callfunc "F_InnMaid","[珊珊]","西方旅館","prt_in.gat",238,130;
	warp "prt_in.gat",237,102;
}
// East Side Inn ------------------
prt_in.gat,61,141,2	script	旅館 侍女::Ahlma#01	53,{

	callfunc "F_InnMaid","[艾瑪]","東方旅館","prt_in.gat",64,136;
	warp "prt_in.gat",49,173;
}

//======================== Alberta ====================================
// 北邊 --------------------------
alberta_in.gat,32,142,5	script	旅館 侍女::Moira	53,{

	callfunc "F_InnMaid","[莫拉]","北方旅館","alberta_in.gat",26,138;
	warp "alberta_in.gat",18,188;
}
// 南邊 -----------------------------
alberta_in.gat,55,142,8	script	旅館 侍女::Tina	53,{

	callfunc "F_InnMaid","[汀娜]","南方旅館","alberta_in.gat",60,140;
	warp "alberta_in.gat",68,188;
}

//====================== 吉芬 ======================================
geffen_in.gat,70,64,5	script	旅館 侍女::Betty	53,{

	callfunc "F_InnMaid","[貝蒂]","吉芬旅館","geffen_in.gat",74,60;
	warp "geffen_in.gat",106,106;
}

//======================= 斐揚 ======================================
payon_in01.gat,131,62,5	script	旅館 侍女::SunHee	53,{

	callfunc "F_InnMaid","[珊琪]","斐揚旅館","payon_in01.gat",132,56;
	warp "payon_in01.gat",140,15;
}

//======================== Morocc ====================================
// North East --------------------------
morocc_in.gat,147,138,5	script	旅館 侍女::Suzie	53,{

	callfunc "F_InnMaid","[蘇西]","東北方旅館","morocc_in.gat",142,140;
	warp "morocc_in.gat",174,144;
}
// South --------------------------------
morocc_in.gat,80,100,5	script	旅館 侍女::Shala	53,{

	callfunc "F_InnMaid","[夏拉]","南方旅館","morocc_in.gat",78,95;
	warp "morocc_in.gat",74,128;
}

//======================== Rachel ====================================
ra_in01.gat,376,69,4	script	旅館 侍女::Rachel	931,{

	callfunc "F_InnMaid","[安妮]","毀葛旅館","ra_in01.gat",375,58;
	warp "ra_in01.gat",384,128;
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	mes getarg(0);
	mes "歡迎光臨 " + getarg(1) + ". 有什麼能為您服務的嗎?";
	next;
	menu "儲存服務",Msave, "租房間 -> 5000 zeny",Mrent, "沒有",Mend;

	Msave:
		mes getarg(0);
		mes "已經儲存好了.";
		mes "唉...我真想換個工作...";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	Mrent:
		mes getarg(0);
		if(Zeny < 5000){
			mes "很抱歉! 您的金錢不足";
			close;
		}
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		mes "謝謝惠顧，祝您有個美夢!";
		close2;
		return;

	Mend:
		mes getarg(0);
		mes "唉...我真想換個工作...";
		close;
}
