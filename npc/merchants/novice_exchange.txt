//===== eAthena Script =======================================
//= Novice Goods Exchanger
//===== By: ==================================================
//= ???, eAthena Team
//===== Current Version: =====================================
//= 1.1a
//===== Compatible With: =====================================
//= eAthena 1.0+
//===== Description: =========================================
//= Exchanges your cheap loot to Red Potions.
//= Official NPC.
//===== Additional Comments: =================================
//= 1.1 Made it as a function. U can add your own Merchs [Lupus]
//= 1.1a Fixed item select [KarLaeda]
//= Translated By 45°向上傾斜
//============================================================

function	script	F_NMerch	{
	mes "["+getarg(0)+"]";
	if((Weight*100)/MaxWeight >= 90){
		mes "哎呀!";
		mes "既然力氣不大，";
		mes "為什麼要帶在身上拿著走?";
		mes "可以寄放在卡普拉的倉庫";
		close;
	}
	mes "你好!";
	mes "你有沒有收集一些物品像比如 "+getitemname(getarg(1))+" 或者是 "+getitemname(getarg(3))+"?";
	if(getarg(5))
		mes "...又或者是 "+getitemname(getarg(5))+"?..";
	mes "噢, 是的, 我需要那些.";
	next;
	mes "["+getarg(0)+"]";
	mes "當然我並不是免費要那些東西的!";
	mes "像比如... 我用 1 瓶 紅色藥水來";
	mes "換 "+getarg(2)+" 塊 "+getitemname(getarg(1))+" 或者是 "+getarg(4)+" 塊 "+getitemname(getarg(3))+"?";
	if(getarg(5))
		mes "又或者是 "+getarg(6)+" 塊 "+getitemname(getarg(5))+"?";
	next;
	switch (select("好吧!:好像我有點吃虧...:不要!")) {
	case 1:
		mes "["+getarg(0)+"]";
		mes "那麼你給我帶了什麼?";
		mes ""+getitemname(getarg(1))+"?  "+getitemname(getarg(3))+"?";
		if(getarg(5))
			mes "或者是 "+getitemname(getarg(5))+"?";
		next;
		if(getarg(5))
			set @i,select("-"+getitemname(getarg(1))+"","-"+getitemname(getarg(3))+"","-"+getitemname(getarg(5))+"","-取消");
		else
			set @i,select("-"+getitemname(getarg(1))+"","-"+getitemname(getarg(3))+"","-取消");
		if(@i==4 || (@i==3 && !getarg(5))) {
			mes "["+getarg(0)+"]";
			mes "好吧, 沒問題.";
			close;
		}
		set @item,getarg(@i*2-1);
		set @amount,getarg(@i*2);
		set @have,countitem(@item);
		if(@have < @amount) {
			mes "["+getarg(0)+"]";
			mes "嘿, 你答應給我的";
			mes getitemname(@item)+" 呢?";
			close;
		}
		mes "["+getarg(0)+"]";
		mes "好吧, 讓我看看你帶的東西.";
		next;
		set @max,@have / @amount;
		mes "["+getarg(0)+"]";
		mes "你總共有 "+@have+" 塊 "+getitemname(@item)+"...";
		mes "我需要給你";
		mes "一共 "+@max+" 瓶紅色藥水.";
		next;
		mes "["+getarg(0)+"]";
		mes "你說什麼? 交易麼?";
		next;
		if(select("要換:不想換")==2) {
			mes "["+getarg(0)+"]";
			mes "...沒有辦法囉!";
			mes "等你有需要的時候再來找我吧!";
			close;
		}
		mes "["+getarg(0)+"]";
		mes "你知道 "+getitemname(@item)+" 的數量";
		mes "來換1瓶紅色藥水, 不是嗎?";
		next;
		mes "["+getarg(0)+"]";
		mes "你可以得到紅色藥水並且從";
		mes "1 到 100 一次交換.";
		mes "如果你改變主意, 輸入 '0'";
		mes "來取消.";
		while(1) {
			next;
			input @val;
			if(@val<0 || @val>100) {
				mes "["+getarg(0)+"]";
				mes "啊哦, 你輸入的數字";
				mes "看起來不正確.";
				mes "你最好看看你能拿多";
				mes "少的紅色藥水.";
				continue;
			}
			break;
		}
		mes "["+getarg(0)+"]";
		if(@val == 0) {
			mes "什麼... 怎麼來和我";
			mes "說話來擾亂我?";
			mes "滾, 我不想和你這種";
			mes "善變的人.";
			close;
		}
		if(@val > @max) {
			mes "啊哦, 你輸入的數字";
			mes "看起來不正確.";
			mes "你最好看看你能拿多";
			mes "少的紅色藥水.";
			close;
		}
		delitem @item,@amount*@val;
		getitem 501,@val;
		mes "給你!";
		mes "謝謝, 完美的交易.";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "你說吃虧?!!";
		mes "呵呵..你這就說錯了，";
		mes "論價錢比起你，";
		mes "應該是我吃虧才對，";
		mes "不想換就算了..";
		close;
	case 3:
		mes "["+getarg(0)+"]";
		mes "哼..不想那就下次";
		mes "再來換吧..";
		mes "喫!!";
		close;
	}
}

prontera,123,102,5	script	商人#1	85,{
	callfunc "F_NMerch","商人",935,5,914,10,909,10;
}

geffen,173,88,5	script	商人#2	85,{
	callfunc "F_NMerch","商人",935,5,915,6,909,10;
}

morocc,180,259,3	script	商人#3	85,{
	callfunc "F_NMerch","商人",935,5,916,7,909,10;
}

aldebaran,152,63,5	script	商人#4	85,{
	callfunc "F_NMerch","商人",955,1,916,7,909,10;
}

payon,200,134,5	script	商人#5	85,{
	//我們去除了第3個物品, 借由加上了 0,0
	callfunc "F_NMerch","商人",902,6,909,10,0,0;
}
