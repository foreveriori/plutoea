//===== eAthena Script =======================================
//= Socket Enchant
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= item loses all element refinement and upgrades but
//= cards too?
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2	Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1	Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//============================================================

payon,140,151,5		script	賽亞布仁::Seiyablem	84,{

	mes "[賽亞布仁]";
	mes "我是在一般的武器或防具上";
	mes "打出洞的技術人類.";
	mes "有些人也會問這個職業不就是";
	mes "打出洞的簡單工作嗎?";
	mes "但是，說這些話的人是因為";
	mes "不知道這個技術才這樣說.";
	next;
	mes "[賽亞布仁]";
	mes "比如說,除了我還有誰有";
	mes "這樣的技術? 這就說明";
	mes "打洞的技術不是那麼";
	mes "簡單的.反正,你有什麼裝備";
	mes "需要打洞嗎?";
	next;
	switch( select( "武器","防具","聽更詳細的說明","放棄" ) )
	{
		case 1:
			mes "[賽亞布仁]";
			mes "武器啊...好的.武器從";
			mes "工作的難度看...可以大致分4個";
			mes "種類.C級是最容易做的";
			mes ".S級是難度最高的.首先";
			mes "選擇一下等級吧.";
			set @sockettype,1;
			next;
			switch( select( "C級","B級","A級","S級" ) )
			{
				case 1:
					mes "[賽亞布仁]";
					mes "C級啊. 那麼想做C級的哪個武器";
					mes "啊?";
					next;
					switch( select( "德瑞尹長戟 [2]","鞭子 [3]","小提琴 [3]" ) )
					{
						case 1: callfunc "Func_Socket",1,1460,1461,40,66,200,1010,10; break;
						case 2: callfunc "Func_Socket",1,1950,1951,40,66,200,1010,10; break;
						case 3: callfunc "Func_Socket",1,1901,1902,40,66,200,1010,10; break;
					}
				break;
				
				case 2:
					mes "[賽亞布仁]";
					mes "不然的B級啊. B級的哪個武器?";
					mes "啊?";
					next;
					switch( select( "鏈錘 [2]","長柄短劍 [2]","角弓 [1]","步兵長矛 [3]","海東劍 [1]","魯特琴 [2]","鋼鐵鞭子 [2]","虎爪拳套 [3]","亞伯雷弓 [1]" ) )
					{
						case 1: callfunc "Func_Socket",1,1519,1520,40,61,300,1010,10; break;
						case 2: callfunc "Func_Socket",2,1219,1220,40,61,300,984,1,999,5; break;
						case 3: callfunc "Func_Socket",2,1714,1716,40,61,300,984,2,999,5; break;
						case 4: callfunc "Func_Socket",1,1407,1408,40,61,300,1010,10; break;
						case 5: callfunc "Func_Socket",2,1123,1128,40,61,300,984,2,999,5; break;
						case 6: callfunc "Func_Socket",1,1905,1906,40,61,300,1011,10; break;
						case 7: callfunc "Func_Socket",1,1954,1955,40,61,300,1011,10; break;
						case 8: callfunc "Func_Socket",1,1801,1802,40,61,300,1010,10; break;
						case 9: callfunc "Func_Socket",2,1713,1715,40,61,300,984,2,999,5; break;
					}
				break;
				
				case 3:
					mes "[賽亞布仁]";
					mes "看來你需要難度大點的. A級? 好的.";
					mes "需要做哪個武器?";
					next;
					switch( select( "獵人之弓 [0]","高等生存的魔杖(INT) [0]","巨大雙手劍 [0]","水紋之劍 [0]","刺殺拳刃 [0]","貝利斯塔強弩 [0]","昏迷之錘 [0]","秘拳套 閃光 [0]","十字巨劍 [0]" ) )
					{
						case 1: callfunc "Func_Socket",2,1718,1726,40,61,500,984,2,999,10; break;
						case 2: callfunc "Func_Socket",2,1619,1620,40,61,500,984,2,999,10; break;
						case 3: callfunc "Func_Socket",2,1168,1171,40,61,800,984,5,999,10; break;
						case 4: callfunc "Func_Socket",2,1129,1149,40,61,500,984,2,999,10; break;
						case 5: callfunc "Func_Socket",2,1261,1266,40,61,700,984,5,999,10; break;
						case 6: callfunc "Func_Socket",2,1722,1727,40,61,500,984,5,999,10; break;
						case 7: callfunc "Func_Socket",2,1522,1532,40,61,700,984,2,999,10; break;
						case 8: callfunc "Func_Socket",2,1814,1816,40,61,700,984,5,999,10; break;
						case 9: callfunc "Func_Socket",2,1163,1172,40,61,700,984,5,999,10; break;
					}
				break;
				
				case 4:
					mes "[賽亞布仁]";
					mes "呵，S級! 發抖...";
					mes "...需要做S級的哪個武器?!";
					next;
					switch( select( "剛尼爾長矛 [0]","巨毒短劍 [0]","破甲短錐 [0]","砂塵短劍 [0]","卡卡普劍 [0]","狂擊之劍 [0]","死神之鐮 [0]","生存的魔杖(DEX) [0]" ) )
					{
						case 1: callfunc "Func_Socket",2,1413,1418,40,51,1000,984,5,999,10; break;
						case 2: callfunc "Func_Socket",2,1239,13016,40,51,1000,984,5,999,10; break;
						case 3: callfunc "Func_Socket",2,1230,13017,40,51,1000,984,5,999,10; break;
						case 4: callfunc "Func_Socket",2,1236,13018,40,51,1000,984,5,999,10; break;
						case 5: callfunc "Func_Socket",2,13002,13019,40,51,1000,984,5,999,10; break;
						case 6: callfunc "Func_Socket",2,1135,13400,40,51,1000,984,5,999,10; break;
						case 7: callfunc "Func_Socket",2,1466,1476,40,51,1000,984,5,999,10; break;
						case 8: callfunc "Func_Socket",2,1617,1618,40,51,2000,984,5,999,10; break;
					}
				break;
			}
		break;
		
		case 2:
			mes "[賽亞布仁]";
			mes "防具啊...好的.防具從";
			mes "工作的難度看...可以大致分4個";
			mes "種類.C級是最容易做的";
			mes ".S級是難度最高的.首先";
			mes "選擇一下等級吧.";
			set @sockettype,2;
			next;
			switch( select( "C級","B級","A級","S級" ) )
			{
				case 1:
					mes "[賽亞布仁]";
					mes "C級啊. 那麼想做C級的哪個防具";
					mes "啊?";
					next;
					switch( select( "大衣 [0]","長大衣 [0]","頭箍 [0]","聖職之帽 [0]" ) )
					{
						case 1: callfunc "Func_Socket",1,2307,2308,40,66,200,999,3; break;
						case 2: callfunc "Func_Socket",1,2309,2310,40,66,200,999,3; break;
						case 3: callfunc "Func_Socket",1,2232,2233,40,66,200,999,3; break;
						case 4: callfunc "Func_Socket",1,2216,2217,40,66,200,999,3; break;
					}
				break;
				
				case 2:
					mes "[賽亞布仁]";
					mes "不然的B級啊. B級的哪個防具?";
					mes "啊?";
					next;
					switch( select( "鏡盾 [0]","鋼鐵鎖子甲 [0]","聖職之衣 [0]","絲質外袍 [0]","戰士長靴 [0]","長靴 [0]","披肩 [0]","鐵盾 [0]","圓盾 [0]","鋼盾 [0]","妖道帽 [0]" ) )
					{
						case 1:  callfunc "Func_Socket",1,2107,2108,40,61,250,999,5; break;
						case 2:  callfunc "Func_Socket",1,2314,2315,40,61,250,999,5; break;
						case 3:  callfunc "Func_Socket",1,2325,2326,40,61,300,999,5; break;
						case 4:  callfunc "Func_Socket",1,2321,2322,40,61,300,999,5; break;
						case 5:  callfunc "Func_Socket",1,2405,2406,40,61,300,999,5; break;
						case 6:  callfunc "Func_Socket",1,2403,2404,40,61,300,999,5; break;
						case 7:  callfunc "Func_Socket",1,2503,2504,40,61,300,999,5; break;
						case 8:  callfunc "Func_Socket",1,2101,2102,40,61,300,999,5; break;
						case 9:  callfunc "Func_Socket",1,2103,2104,40,61,300,999,5; break;
						case 10: callfunc "Func_Socket",1,2105,2106,40,61,250,999,5; break;
						case 11: callfunc "Func_Socket",1,5046,5168,40,61,250,999,5; break;
					}
				break;
				
				case 3:
					mes "[賽亞布仁]";
					mes "看來你需要難度大點的. A級? 好的.";
					mes "需要做哪個防具?";
					next;
					switch( select( "寶石頭盔 [0]","軟呢帽 [0]","謊言紀錄本 [0]","緊身便衣 [0]","秘銀金屬鎧甲 [0]","鋼鐵鎧甲 [0]","盜賊之衣 [0]","重靴 [0]","修女的帽子 [0]","斗篷 [0]","金屬頭盔 [0]","忍服 血櫻 [0]","獸人頭盔 [0]","伯爵斗篷 [0]","神聖之帽 [0]","黃金帽 [0]","敏捷別針 [0]","殭屍帽 [0]" ) )
					{
						case 1:  callfunc "Func_Socket",1,2230,2231,40,61,400,985,1; break;
						case 2:  callfunc "Func_Socket",1,5114,5120,40,61,400,985,1; break;
						case 3:  callfunc "Func_Socket",1,2109,2121,40,61,400,985,1; break;
						case 4:  callfunc "Func_Socket",1,2330,2331,40,61,400,985,1; break;
						case 5:  callfunc "Func_Socket",1,2341,2342,40,61,400,985,1; break;
						case 6:  callfunc "Func_Socket",1,2316,2317,40,61,400,985,1; break;
						case 7:  callfunc "Func_Socket",1,2335,2336,40,61,400,985,1; break;
						case 8:  callfunc "Func_Socket",1,2411,2412,40,61,400,985,1; break;
						case 9:  callfunc "Func_Socket",1,5092,5093,40,61,400,985,1; break;
						case 10: callfunc "Func_Socket",1,2505,2506,40,61,400,985,1; break;
						case 11: callfunc "Func_Socket",1,2228,2229,40,61,400,985,1; break;
						case 12: callfunc "Func_Socket",1,2337,2359,40,61,400,985,1; break;
						case 13: callfunc "Func_Socket",1,2299,5157,40,61,400,985,1; break;
						case 14: callfunc "Func_Socket",1,2507,2525,40,61,400,985,1; break;
						case 15: callfunc "Func_Socket",1,2251,5158,40,61,400,985,1; break;
						case 16: callfunc "Func_Socket",1,2246,5159,40,61,400,985,1; break;
						case 17: callfunc "Func_Socket",1,2605,2625,40,61,400,985,1; break;
						case 18: callfunc "Func_Socket",1,2264,5167,40,61,400,985,1; break;
					}
				break;
				
				case 4:
					mes "[賽亞布仁]";
					mes "呵，S級! 發抖...";
					mes "...需要做S級的哪個防具?!";
					next;
					switch( select( "山羊頭盔 [0]","尖角髮圈 [0]","骨製頭盔 [0]","海盜船長帽 [0]","王冠 [0]","后冠 [0]","人面獅身帽 [0]","創作者外袍 [0]","智力耳環 [0]","力量戒指 [0]","神射手套 [0]" ) )
					{
						case 1:  callfunc "Func_Socket",1,2256,5160,40,51,2000,985,2; break;
						case 2:  callfunc "Func_Socket",1,2258,5161,40,51,2000,985,2; break;
						case 3:  callfunc "Func_Socket",1,5017,5162,40,51,2000,985,2; break;
						case 4:  callfunc "Func_Socket",1,5019,5163,40,51,2000,985,2; break;
						case 5:  callfunc "Func_Socket",1,2235,5165,40,51,2000,985,2; break;
						case 6:  callfunc "Func_Socket",1,2234,5164,40,51,2000,985,2; break;
						case 7:  callfunc "Func_Socket",1,5053,5166,40,51,1000,985,2; break;
						case 8:  callfunc "Func_Socket",1,2343,2360,40,51,1000,985,2; break;
						case 9:  callfunc "Func_Socket",1,2602,2622,40,51,1000,985,2; break;
						case 10: callfunc "Func_Socket",1,2601,2621,40,51,1000,985,2; break;
						case 11: callfunc "Func_Socket",1,2619,2671,40,51,1000,985,2; break;
					}
				break;
			}
		break;
		
		case 3:
			mes "[賽亞布仁]";
			mes "你還要聽什麼...給你個暗示";
			mes "就是，我其實是流浪";
			mes "打洞技師，到處去";
			mes "體驗和工作，但";
			mes "這個村子普隆德拉，在這裡精煉值低";
			mes "裝備的打洞成功率很高.";
			next;	
			mes "[賽亞布仁]";
			mes "是個很微妙的差異，越是低精煉的裝備";
			mes "感覺愈容易打洞..";
			mes "當然，如果失敗的話，裝備就";
			mes "一下子消失...但是只是以成功率的";
			mes "角度看是那樣的.";
			next;
			mes "[賽亞布仁]";
			mes "精練值高的裝備做得";
			mes "更好的成功率高的村子.";
			mes "但是那只是人家的故事...";
			next;
			mes "[賽亞布仁]";
			mes "還有，需要注意的一點";
			mes "是，恩...舉個例子.";
			mes "如果拿著+7斗篷和一般斗篷";
			mes "時，如果委託我在斗篷上";
			mes "打洞時，兩個之中";
			mes "你會覺得我會做那個裝備?";
			next;
			mes "[賽亞布仁]";
			mes "正確的答案是...。即，,";
			mes "如果有同一名稱的裝備時";
			mes "跟對方的意志沒有關係，根據";
			mes "我想做哪個，就在哪個裝備上";
			mes "打洞.";
			next;
			mes "[賽亞布仁]";
			mes "^FF0000如果真想再特定裝備上打洞";
			mes "的話，就拿來同一名稱的";
			mes "裝備一個";
			mes ".把不想打洞的";
			mes "裝備放在其他地方";
			mes "再過來.^000000";
			next;
			mes "[賽亞布仁]";
			mes "如果不那樣做的話，,";
			mes "即使在不願打洞的裝備上做了洞";
			mes "做的結果不如意，";
			mes "我也不會管的.你要好好檢查.";
			next;
			mes "[賽亞布仁]";
			mes "不用再說明了吧? 那麼,";
			mes "百聞不如一見! 去挑戰一下";
			mes "打洞如何? 我也得";
			mes "賺生活費啊.";
			close;
			
		case 4:
			mes "[賽亞布仁]";
			mes "啪.";
			close;
	}
}
				
function	script	Func_Socket	{

	mes "[賽亞布仁]";
	mes "" +getitemname(getarg(1))+ "啊.好的";
	if (getarg(0) == 1)
	{
		mes "手續費^FF0000" +getarg(5) / 10+ "萬Zeny^000000、";
		mes "材料至少需要" +getitemname(getarg(6))+ " " +getarg(7)+ "個^000000.";
	}
	else if (getarg(0) == 2)
	{
		mes "手續費^FF0000" +getarg(5) / 10+ "萬Zeny^000000、";
		mes "材料需要^FF0000" +getitemname(getarg(6))+ " " +getarg(7)+ "個^000000、";
		mes "^FF0000" +getitemname(getarg(8))+ " " +getarg(9)+ "個^000000.";
	}
	mes "才能將這個" +getitemname(getarg(1))+ "打洞啊.";
	next;

	if(@sockettype == 1)
	{
		mes "[賽亞布仁]";
		mes "材料都帶來了嗎? 還有,";
		mes "如果失敗的話，材料,裝備將會消失掉";
		mes "，";
		mes "你得事先知道這一點，還有需要記住，即";
		mes "使";
		mes "成功打出了洞，精煉值以及鑲上去的";
		mes "卡片全部會消失.";
		next;
	}
	else if(@sockettype == 2)
	{
		mes "[賽亞布仁]";
		mes "材料都帶來了嗎? 還有,";
		mes "如果失敗的話，材料,裝備將會消失掉";
		mes "，";
		mes "你得事先知道這一點，還有需要記住，即";
		mes "使";
		mes "成功打出了洞，精煉值以及鑲上去的";
		mes "卡片全部會消失.";
		next;
	}

	if ( select("委託打洞的工作","挑戰下一次機會") == 2 )
	{
		mes "[賽亞布仁]";
		mes "那麼下次機會再見.";
		close;
	}

	switch(getarg(0))
	{
		case 1:		
			if((Zeny < getarg(5)*1000) || (countitem(getarg(6)) < getarg(7)) || (!countitem(getarg(1))))
			{
				mes "[賽亞布仁]";
				mes "傻瓜!材料或錢不足啊!";
				mes "還不趕快準備好後再拿?!";
				close;
			}
			break;
	
		case 2:
			if((Zeny < getarg(5)*1000) || (countitem(getarg(6)) < getarg(7)) || (countitem(getarg(8)) < getarg(9)) || (!countitem(getarg(1))))
			{
				mes "[賽亞布仁]";
				mes "傻瓜!材料或錢不足啊!";
				mes "還不趕快準備好後再拿?!";
				close;
			}
			break;
	}
	
	mes "[賽亞布仁]";
	mes "Alright then, let the work begin!";
	mes "You'd better pray for a successful result.";
	next;
	set .@socketrand,rand(1,100);
	if((.@socketrand > getarg(3)) && (.@socketrand < getarg(4)))
	{
		if(getarg(4) == 51) specialeffect 90;
		else specialeffect 83;
		mes "[賽亞布仁]";
		mes "好啊，成功了.";
		mes "恭喜恭喜.";
		delitem getarg(1),1;
		delitem getarg(6),getarg(7);
		if(getarg(0) == 2) delitem getarg(8),getarg(9);
		set Zeny,Zeny - getarg(5)*1000;
		getitem getarg(2),1;
		next;
		mes "[賽亞布仁]";
		mes "那麼歡迎您下次再使用.";
		close;
	}
	else
	{
		specialeffect 108;
		mes "[賽亞布仁]";
		mes "啊……失敗了，";
		mes "沒有這個運氣，";
		mes "別感到沉悶，";
		mes "這次一定會成功的喲.";
		delitem getarg(1),1;
		delitem getarg(6),getarg(7);
		if(getarg(0) == 2) delitem getarg(8),getarg(9);
		set Zeny,Zeny - getarg(5)*1000;
		next;
		mes "[賽亞布仁]";
		mes "那麼歡迎您下次再使用.";
		close;
	}
}

lhz_in02,281,35,5	duplicate(Seiyablem)	賽亞布仁	84,0,0
prt_in,33,70,5		duplicate(Seiyablem)	賽亞布仁	84,0,0
morocc,51,41,5		duplicate(Seiyablem)	賽亞布仁	84,0,0

payon,143,143,7	script	Young Man#socket	97,{

	mes "[Young Man]";
	mes "I'm considering becoming a Slotting Engineer.";
	mes "It's a new field where you can add Slots to equipment, and it'd be cool if I can work in such a lucrative career.";
	next;
	mes "[Young Man]";
	mes "There's a huge element of luck involved in successfully adding Slots from what I can understand.";
	mes "You fail sometimes, but if you're super lucky, you can add two Slots.";
	mes "Crazy. Huh?";
	next;
	mes "[Young Man]";
	mes "But don't get too excited.";
	mes "Only certain equipment can handle the addition of two extra Slots.";
	mes "There's so much more I have to learn before I can even be an apprentice...";
	close;
}

lhz_in02,269,33,5	duplicate(Young Man#socket)	Young Man	97,0,0
prt_in,31,57,1		duplicate(Young Man#socket)	Young Man	97,0,0
morocc,60,42,3		duplicate(Young Man#socket)	Young Man	97,0,0