// 藥水製作腳本
// 
// 重整作者:		Dino9021
// 
// 中文版本編號:	20041119.0.2
//			20050210.0.1	以 eAthena 腳本文基礎重新翻譯
//
//============================================================

//===== eAthena Script =======================================
//= Grandpa Pharmacist
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= eAthena 7.15 +
//===== Description: =========================================
//= Trade in items for potions
//===============================
//= Breakdown of arguments used
//= arg(0): first item id#
//= arg(1): 2nd item id# if needed, or else use 0
//= arg(2): zeny cost
//= arg(3): potion id#
//= arg(4): potion name
//===== Additional Comments: =================================
//= Fully working
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Created a subfunc for the potion making part.  Added more
//= input/zeny exploit checks. Added Lupus's "loopless" technique.[kobra_k88]
//============================================================


alberta_in.gat,16,28,4	script	愛爾貝塔 藥劑師	61,{
	mes "[愛爾貝塔 藥劑師]";
	mes "嗯? 你想作什麼?";
	next;
	menu "製作藥水",M_Make, "交談",M_Talk, "請給我一些資訊",M_Info, "取消對話",M_End;
	
	M_Make:
		mes "[愛爾貝塔 藥劑師]";
		mes "你把需要的東西都準備好了嗎? 如果是的話...你想製作什麼藥水呢?";
		M_Menu:
		next;
		menu	"紅色藥水",M_0,
			"赤色藥水",M_1,
			"黃色藥水",M_2,
			"白色藥水",M_3,
			"藍色藥水",M_4,
			"綠色藥水",M_5,
			"不，我改變主意了",M_End;
		
		M_0:
			callsub sF_Make, 507, 0, 2, 501, "紅色";
			goto M_Menu;
		M_1:
			callsub sF_Make, 507, 508, 5, 502, "赤色";
			goto M_Menu;
		M_2:
			callsub sF_Make, 508, 0, 10, 503, "黃色";
			goto M_Menu;
		M_3:
			callsub sF_Make, 509, 0, 20, 504, "白色";
			goto M_Menu;
		M_4:
			callsub sF_Make, 510, 0, 30, 505, "藍色";
			goto M_Menu;
		M_5:
			callsub sF_Make, 511, 0, 3, 506, "綠色";
			goto M_Menu;

	M_Talk:
		mes "[愛爾貝塔 藥劑師]";
		mes "藥水可以用來治療、解除毒物狀態，但是你必須謹記的是，使用藥水是會上癮的。";
		next;
		mes "[愛爾貝塔 藥劑師]";
		mes "不要嫌棄我又老又?嗦，呵呵呵呵，我是這裡的藥劑師，如果你想要製作藥水的話，或許我可以給你提供一些幫助！";
		close;
	M_Info:
		mes "[愛爾貝塔 藥劑師]";
		mes "真是好學的年輕人，那好吧，我就告訴你一些藥水的秘方。藥水比起藥草來說，能夠恢復能量最多，而且恢復的速度也是非常快速的。";
		next;
		mes "[愛爾貝塔 藥劑師]";
		mes "我可以幫助你製作一些常用的藥水，當然收取一些報酬也是必須的，因為我必須生活，呵呵呵呵……，下面是製作藥水的一些秘方。";
		next;
		mes "[愛爾貝塔 藥劑師]";
		mes "^FF5533紅色藥水^000000 - 2個紅色藥草, 1個空瓶, 加工費是 2 Zeny";
		mes "^FF8000赤色藥水^000000 - 1個紅色藥草, 1個黃色藥草, 1個空瓶, 加工費是 5 Zeny";
		mes "^E8CF20黃色藥水^000000 - 2個黃色藥草, 1個空瓶, 加工費是 10 Zeny";
		next;
		mes "[愛爾貝塔 藥劑師]";
		mes "^999999白色藥水^000000 - 2個白色藥草, 1個空瓶, 加工費是 20 Zeny";
		mes "^3355FF藍色藥水^000000 - 2個藍色藥草, 1個空瓶, 加工費是 30 Zeny";
		mes "^00B000綠色藥水^000000 - 2個綠色藥草, 1個空瓶, 加工費是 3 Zeny";
		close;
	M_End:
		mes "[愛爾貝塔 藥劑師]";
		mes "你沒有任何話可說嗎?!";
		close;

// Subfunction for making potions
//================================
sF_Make:
	set @herbnum, 2;
	if(getarg(1) != 0) set @herbnum, 1;
	if(countitem(getarg(0)) < @herbnum) goto L_NdHerbs;
	if((countitem(getarg(1)) < @herbnum) && (getarg(1) != 0)) goto L_NdHerbs;
	if(countitem(713) < 1) goto L_NdBottle;
	if(Zeny < getarg(2)) goto L_NdZeny;

	mes "[愛爾貝塔 藥劑師]";
	mes "嗯，那麼你打算製作多少紅色藥水呢?";
	next;
	menu "能做多少就做多少",sM_0a, "讓我想想...",sM_0b, "取消",M_End;

	sM_0a:
		set @amount, 1000;
		if(zeny/getarg(2) < @amount) set @amount, zeny/getarg(2);
		if(countitem(getarg(0))/@herbnum < @amount) set @amount, countitem(getarg(0))/@herbnum;
		if((countitem(getarg(1))/@herbnum < @amount) && (countitem(getarg(1)) != 0)) set @amount, countitem(getarg(1))/@herbnum;
		if(countitem(713) < @amount) set @amount, countitem(713);
		if(@amount > 0) goto L_End;
		mes "[愛爾貝塔 藥劑師]";
		mes "唔...你沒有製作所需的正確物品...";
		close;
		
	sM_0b:
		input @amount;
		if(@amount<1 || @amount>1000) goto L_BadAmnt;
		if(countitem(getarg(0))/@herbnum < @amount) goto L_NdHerbs;
		if((countitem(getarg(1))/@herbnum < @amount) && (countitem(getarg(1)) != 0)) goto L_NdHerbs;
		if(countitem(713) < @amount) goto L_NdBottle;
		if(Zeny < (getarg(2)*@amount)) goto L_NdZeny;

	L_End:
		mes "[愛爾貝塔 藥劑師]";
		mes "那麼...這些是你要的 " +getarg(4)+ " 藥水,";
		mes "歡迎下次再來!";
		delitem getarg(0), (@amount*@herbnum);
		if(getarg(1) != 0) delitem getarg(1), (@amount*@herbnum);
		delitem 713, @amount;
		set Zeny, Zeny - (getarg(2)*@amount);
		getitem getarg(3), @amount;
		close;

	L_NdBottle:
		mes "[愛爾貝塔 藥劑師]";
		mes "你沒有足夠的空瓶來裝藥水! 笨蛋!!";
		return;

	L_NdHerbs:
		mes "[愛爾貝塔 藥劑師]";
		mes "你到底想要我幫你什麼忙? 你連製作所需的正確物品都搞不清楚了!";
		return;

	L_NdZeny:
		mes "[愛爾貝塔 藥劑師]";
		mes "你沒有足夠的錢來付我加工費啊...";
		return;

	L_BadAmnt:
		mes "[愛爾貝塔 藥劑師]";
		mes "什麼? 你說你要幾個?!";	
		return;
}
