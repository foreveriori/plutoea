// 大嘴鳥、嘟嘟鳥、獵鷹租用腳本
// 
// 中文化作者:		Dino9021
// 
// 中文版本編號:	20041216.0.1
//
// 修改內容:		20041216.0.1 修正 npc 影像與所在座標
//
//============================================================

//===== eAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= PecoPeco Breeder, Grand PecoPeco Breeder, Falcon tamer scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support for Lord_Knight(2),Paladin(2) and Sniper.
//============================================================ 

// 大嘴鳥管理士兵 (for Knights)-------------------------------------------
prontera.gat,55,350,5	script	大嘴鳥管理士兵	708,{
	mes "[大嘴鳥管理士兵]";
	if(Class == 7 || Class == 13 || Class == 4008 || Class == 4014) goto L_Start;
	mes "我是大嘴鳥管理士兵，如果你是 ^4444FF騎士^000000 就可以來找我租用大嘴鳥";
	close;

L_Start:
	mes "歡迎光臨，你要租大嘴鳥嗎?";
	next;
	mes "[大嘴鳥管理士兵]";
	mes "租用一只大嘴鳥需要 2500 Zeny, 同時, 你必須學過騎乘術才能駕馭它.";
	next;
	menu "租用",M_0,"好貴啊",M_1;

	M_0:
		if(getskilllv(63) == 0) goto sL_NeedSkill;
		if(Zeny < 2500) goto sL_Zeny;
		if(checkriding(0) != 0) goto sL_GotPeco;
		set Zeny, Zeny - 2500;
		setriding;
		mes "[大嘴鳥管理兵士]";
		mes "那么, 這只大嘴鳥就交給你了";
		mes "要好好照顧他喔！";
		emotion 15;
		close;

		sL_NeedSkill:
			mes "[大嘴鳥管理士兵]";
			mes "你沒學過騎乘術呢";
			mes "要學過騎乘術才能使用大嘴鳥喔";
			close;
		sL_Zeny:
			mes "[大嘴鳥管理士兵]";
			mes "您的金錢不足.";
			close;
		sL_GotPeco:
			mes "[大嘴鳥管理士兵]";
			mes "往你的屁股下面看看";
			mes "你已經騎在大嘴鳥上面了......";
			emotion 20;
			close;
	M_1:
		mes "[大嘴鳥管理士兵]";
		mes "歡迎下次再來...";
		close;

}

// 嘟嘟鳥管理士兵 (for Crusaders)-------------------------------------------
prontera.gat,232,318,3	script	嘟嘟鳥管理士兵	708,{
	mes "[嘟嘟鳥管理士兵]";
	if(Class == 14 || Class == 21 || Class == 4015 || Class == 4015 || Class == 4022 ) goto L_Start;

	mes "我是大嘴鳥管理士兵，如果你是 ^4444FF十字軍^000000 救可以來找我租用嘟嘟鳥";
	close;

L_Start:
	mes "歡迎光臨，你要租嘟嘟鳥嗎?";
	next;
	mes "[嘟嘟鳥管理士兵]";
	mes "租用一只嘟嘟鳥需要 2500 Zeny, 同時, 你必須學過騎乘術才能駕馭它.";
	next;
	menu "租用",M_0,"好貴啊",M_1;

	M_0:
		if(getskilllv(63) == 0) goto sL_NeedSkill;
		if(Zeny < 2500) goto sL_Zeny;
		if(checkriding(0) != 0) goto sL_GotPeco;
		set Zeny, Zeny - 2500;
		setriding;
		mes "[大嘴鳥管理兵士]";
		mes "那么, 這只嘟嘟鳥就交給你了";
		mes "要好好照顧他喔！";
		emotion 15;
		close;

		sL_NeedSkill:
			mes "[嘟嘟鳥管理士兵]";
			mes "你沒學過騎乘術呢";
			mes "要學過騎乘術才能使用嘟嘟鳥喔";
			close;
		sL_Zeny:
			mes "[嘟嘟鳥管理士兵]";
			mes "您的金錢不足.";
			close;
		sL_GotPeco:
			mes "[嘟嘟鳥管理士兵]";
			mes "往你的屁股下面看看";
			mes "你已經騎在嘟嘟鳥上面了......";
			emotion 20;
			close;
	M_1:
		mes "[嘟嘟鳥管理士兵]";
		mes "Alright.  Good day to you.";
		close;

}

// 獵鷹管理員--------------------------------------------------------------
in_hunter.gat,146,100,2	script	獵鷹管理員	708,{
	mes "[獵鷹管理員]";
	mes "哈?! 我是獵鷹管理員.";
	next;
	menu "與他交談",M_0, "禮貌地離開",M_End;

	M_0:
		mes "[獵鷹管理員]";
		mes "如果你是獵人且有學習 ^5555FF'馴鷹術'^000000 的話我可以租給你一只獵鷹.";
		next;
		menu "我要租獵鷹",sM_0, "不用了，謝謝",M_End;

		sM_0:
			if(Class != 11 && Class != 4012) goto sL_NotHunter;

			if(checkfalcon(0) != 0) goto sL_GotFalc;
			if(getskilllv(127)==0) goto sL_NoSkill;
			mes "[獵鷹管理員]";
			mes "那么，請好好照顧它吧!";
			setfalcon;
			close;
			
			sL_NotHunter:
				mes "[獵鷹管理員]";
				mes "抱歉, 獵鷹只能租給獵人.";
				close;
			sL_GotFalc:
				mes "[獵鷹管理員]";
				mes "往你的頭上看看";
				mes "你的獵鷹正在你頭上盤旋......";
				close;
			sL_NoSkill:
				mes "[獵鷹管理員]";
				mes "對不起，你必須至少學會一級的訓鷹術技能才能使用獵鷹。";
				close;
	M_End:
		mes "[獵鷹管理員]";
		mes "祝你有個美好的一天";
		close;
}

//Not Supposed To Be One In Payon!
//Coordinate IS Correct For New Payon Map Though IF You Enable It
//payon_in01.gat,206,175,4	duplicate(Falcon Master)	獵鷹管理員	59
