// 大嘴鳥、嘟嘟鳥、獵鷹租用腳本
// 
// 中文化作者:		Dino9021
// 
// 中文版本編號:	20041216.0.1
//
// 修改內容:		20041216.0.1 修正 npc 影像與所在座標
//
//============================================================

//===== eAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//============================================================ 

// 大嘴鳥管理士兵 (for Knights)-------------------------------------------
prontera.gat,55,350,5	script	大嘴鳥管理士兵	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[大嘴鳥管理士兵]";
	if (BaseJob == Job_Knight) {
		mes "歡迎光臨，你要租大嘴鳥嗎?";
		mes "租用一隻大嘴鳥需要 "+.@price+" Zeny, 同時, 你必須學過騎乘術才能駕馭它.";
		next;
		if (select("租用:好貴啊") == 1) {
			if (zeny < .@price) {
				mes "[大嘴鳥管理兵士]";
				mes "您的金錢不足.";
				close;
			}
			else if(getskilllv(63) == 0) {
				mes "[大嘴鳥管理兵士]";
				mes "你沒學過騎乘術呢";
				mes "要學過騎乘術才能使用大嘴鳥喔";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鳥管理兵士]";
				mes "往你的屁股下面看看";
				mes "你已經騎在大嘴鳥上面了......";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
		}

		mes "[大嘴鳥管理兵士]";
		mes "那麼, 這隻大嘴鳥就交給你了";
		mes "要好好照顧他喔！";
		close;
	}
	mes "我是大嘴鳥管理士兵，如果你是 ^4444FF 騎士 ^000000 就可以來找我租用大嘴鳥";
	close;
}

// 嘟嘟鳥管理士兵 (for Crusaders)-------------------------------------------
prontera.gat,232,318,3	script	嘟嘟鳥管理士兵	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[嘟嘟鳥管理士兵]";
	if (BaseJob == Job_Crusader) {
		if (Class == Job_Crusader && Upper == 0 || Upper == 1) mes "Welcome, Crusader.";
		else 
		mes "Welcome, Paladin.";
		mes "歡迎光臨，你要租嘟嘟鳥嗎?";
		mes "租用一隻嘟嘟鳥需要 "+.@price+" Zeny, 同時, 你必須學過騎乘術才能駕馭它.";
		next;
		if (select("租用:好貴啊") ==1) {
			if (zeny < .@price) {
				mes "[嘟嘟鳥管理士兵]";
				mes "您的金錢不足.";
				close;
			}

			else if(getskilllv(63) == 0) {
				mes "[嘟嘟鳥管理士兵]";
				mes "你沒學過騎乘術呢";
				mes "要學過騎乘術才能使用嘟嘟鳥喔";
				close;
			}
			else if(checkriding()) {
				mes "[嘟嘟鳥管理士兵]";
				mes "往你的屁股下面看看";
				mes "你已經騎在嘟嘟鳥上面了......";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
		}
		mes "[嘟嘟鳥管理士兵]";
		mes "那麼, 這只嘟嘟鳥就交給你了";
		mes "要好好照顧他喔！";
		close;
	}
	mes "我是大嘴鳥管理士兵，如果你是 ^4444FF 十字軍^000000 就可以來找我租用嘟嘟鳥.";
	close;
}

// Falcon Master--------------------------------------------------------------
hu_in01.gat,381,304,5	script	獵鷹管理員	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[獵鷹管理員]";
	if (BaseJob == Job_Hunter) {
		mes "哈囉! 我是獵鷹管理員.";
		mes "你可以花費 "+.@price+" z 租用獵鷹，不過你要先學會 ^5555FF'馴鷹術'^000000 ";
		next;
		if (select("我要租獵鷹:不用了，謝謝") == 1) {
			if (zeny < .@price) {
				mes "[獵鷹管理員]";
				mes "您的金錢不足.";
				close;
			}
			else if(getskilllv(127) == 0) {
				mes "[獵鷹管理員]";
				mes "對不起，你必須至少學會一級的訓鷹術技能才能使用獵鷹。";
				close;
			}
			else if(checkfalcon()) {
				mes "[獵鷹管理員]";
				mes "往你的頭上看看";
				mes "你的獵鷹正在你頭上盤旋......";
				close;
			}
			set zeny,zeny-.@price;
			setfalcon;
			close;
		}
		mes "[獵鷹管理員]";
		mes "祝你有個美好的一天";
		close;
	}
	mes "抱歉, 獵鷹只能租給獵人.";
	close;
}
