//===========================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -------------------------------------------
// - 染髮 腳本 -
// ------------------------------------------
//  改寫自JA
//============================================

prt_in.gat,243,168,4	script	佑波迪曲	91,{
	if(Sex==0) {
		if(rand(2)) {
			mes "[美容師 佑波迪曲]";
			mes "歡迎光臨";
			mes "啊呀! 這位漂亮的小姐...";
			mes "讓我來為你保養頭髮吧";
		}
		else {
			mes "[美容師 佑波迪曲]";
			mes "啊呀!可愛的妳..";
			mes "要不要染頭髮?";
			mes "會讓你變的漂亮哦";
		}
	}
	if(Sex==1) {
		mes "[美容師 佑波迪曲]";
		mes "歡迎光臨";
		mes "你是來做頭部保養的吧...";
		mes "快請坐";
	}
	next;
	switch (select("染髮","關於染髮","取消")) {
	case 1:
		mes "[美容師 佑波迪曲]";
		mes "下定決心了嗎";
		mes "來，看看喜歡那種顏色吧";
		setarray .@word$,"紅色","黃色","紫色","橘色","綠色","藍色","白色","黑色";
		setarray .@color,8,1,2,3,4,5,6,7;
		setarray .@itemid,975,976,981,980,979,978,982,983;
		while(1) {
			next;
			set .@j,select("紅色","黃色","紫色","橘色",
					"綠色","藍色","白色","黑色","改變主意不染了")-1;
			mes "[美容師 佑波迪曲]";
			mes "好了，我幫你把頭髮染成" +.@word$[.@j]+ "把";
			next;
			if(select("好!","不染了!")==2) {
				if(.@dyecount) {
					mes "[美容師 佑波迪曲]";
					mes "好吧";
					mes "現在的顏色也蠻適合你的";
				}
				else {
					mes "[美容師 佑波迪曲]";
					mes "喜歡現在的顏色吧";
					mes "很適合你哦";
				}
				close;
			}
			if(Zeny<5000) {
				mes "[美容師 佑波迪曲]";
				mes "金錢不足";
				close;
			}
			if(countitem(.@itemid[.@j])<1) {
				mes "[美容師 佑波迪曲]";
				mes "什麼，你要帶" +.@word$[.@j]+ "染料來啊";
				mes "「" +.@word$[.@j]+ "染料」是必須的";
				close;
			}
			if(getlook(6) == .@color[.@j]) {
				mes "[美容師 佑波迪曲]";
				mes "嗯?";
				mes "你的頭髮已經是這個顏色了";
				continue;
			}
			set .@dyecount,1;
			set Zeny,Zeny-5000;
			delitem .@itemid[.@j],1;
			setlook 6,.@color[.@j];
			mes "[美容師 佑波迪曲]";
			mes "好了";
			mes "想染其他顏色看看嗎?";
			next;
			if(select("好","不好")==2) {
				mes "[美容師 佑波迪曲]";
				mes "好";
				mes "歡迎下次再來!";
				close;
			}
			mes "[美容師 佑波迪曲]";
			mes "好的，知道了";
		}
	case 2:
		mes "[美容師 佑波迪曲]";
		mes "心情不好的時候、";
		mes "失戀的時候、";
		mes "想裝帥的時候、";
		mes "有特別事情的時候，";
		mes "在特別的場所、特別的時間，";
		mes "只有您有獨特髮型，您想要炫耀嗎?";
		next;
		mes "[美容師 佑波迪曲]";
		mes "如果您自己有染料，";
		mes "不管什麼顏色都可以使您又細又柔軟髮質變美麗";
		mes "不要對費用有壓力，";
		mes "得到美麗就是得到一切，呵呵∼!!";
		next;
		mes "[美容師 佑波迪曲]";
		mes "* 只需要 *";
		mes "^FF3080一個染料^000000";
		mes "^FF30805000 金幣^000000";
		close;
	case 3:
		mes "[美容師 佑波迪曲]";
		mes "不管男人女人都有愛打扮的權利和義務";
		close;
	}
}