//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 攻城戰 公會監護人 腳本 - 
//===================================
// 版本：2.0
//===================================

function	script	F_GldManager	{
	set .@GID, GetCastleData(getarg(1),1);

	// Define the types of guardians on a per castle basis
	// 1 - Soldier Guardian; 2 - Archer Guardian; 3 - Knight Guardian
	// Aldebaran (Luina)
	if (getarg(1) == "aldeg_cas01") setarray .@guardiantype[0],1,2,2,2,2,3,3,3;
	if (getarg(1) == "aldeg_cas02") setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
	if (getarg(1) == "aldeg_cas03") setarray .@guardiantype[0],3,3,1,1,1,2,2,2;
	if (getarg(1) == "aldeg_cas04") setarray .@guardiantype[0],2,2,2,1,1,1,3,3;
	if (getarg(1) == "aldeg_cas05") setarray .@guardiantype[0],2,2,1,1,3,3,3,3;
	// Geffen (Britoniah)
	if (getarg(1) == "gefg_cas01") setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	if (getarg(1) == "gefg_cas02") setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	if (getarg(1) == "gefg_cas03") setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
	if (getarg(1) == "gefg_cas04") setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
	if (getarg(1) == "gefg_cas05") setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
	// Payon (Baulder)
	if (getarg(1) == "payg_cas01") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas02") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas03") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas04") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas05") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	// Prontera (Valkyrie Realms)
	if (getarg(1) == "prtg_cas01") setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	if (getarg(1) == "prtg_cas02") setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
	if (getarg(1) == "prtg_cas03") setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
	if (getarg(1) == "prtg_cas04") setarray .@guardiantype[0],3,3,3,1,1,1,2,2;
	if (getarg(1) == "prtg_cas05") setarray .@guardiantype[0],3,3,3,1,1,1,2,2;

	if (.@GID == 0){
		mes "["+getarg(0)+"]";
		mes "我在此等待我的主人... 勇趕的冒險者啊! 追隨你的命運吧!";
		return 0;
	}
	if (getcharid(2) != .@GID || strcharinfo(0) != getguildmaster(.@GID)){
		mes "["+getarg(0)+"]";
		mes "不管你們怎麼威脅我";
		mes "，我只願意追隨我的主人 ^FF0000" + getguildmaster(.@GID) + "^000000 !";
		mes "監護人你們在哪裡呀!";
		mes "趕快把這些無理的傢伙們趕走吧!";
		return 0;
	}
	mes "["+getarg(0)+"]";
	mes "歡迎光臨 ，";
	mes "我的主人^FF0000" + getguildmaster(.@GID) + "^000000先生/小姐.";
	mes "..";
	mes "請儘管吩咐";
	mes "，我將忠誠地為你完成任務";
	next;

	// To allow abandoning of castles, uncomment the following switch,
	// comment out the switch below it, and uncomment case 7.
	//switch(select("Castle briefing:Invest in commercial growth:Invest in Castle Defenses:Summon Guardian:Employ / discharge a storehouse staff member:Go into Master's room:Abandon Castle")) {
	switch(select("查看秘密基地狀況:投資商業發展度:投資防禦度:設置監護人:雇用 / 解雇 倉庫職員:進入主人房間")) {
	case 1:
		mes "["+getarg(0)+"]";
		mes "現在向你報告秘密基地的狀況";
		mes " ";
		mes "　 ^0000ff現在商業發展度是" + GetCastleData(getarg(1),2) + "^000000";
		if (GetCastleData(getarg(1),4) > 0) mes "　 ^0000ff- 在過去一天之內，投資了 " + GetCastleData(getarg(1),4) + " 次^000000";
		mes "　 ^0000ff現在的防禦度是" + GetCastleData(getarg(1),3) + "^000000";
		if (GetCastleData(getarg(1),5) > 0) mes "　 ^0000ff- 在過去一天之內，投資了 " + GetCastleData(getarg(1),5) + " 次^000000";
		mes " ";
		mes "報告完畢";
		return 0;

	case 2:
		set .@Economy,GetCastleData(getarg(1),2);
		if(.@Economy < 8) set .@eco_invest,10000;
		if(.@Economy >= 8) set .@eco_invest,20000;
		if(.@Economy >= 16) set .@eco_invest,40000;
		if(.@Economy >= 25) set .@eco_invest,80000;
		if(.@Economy >= 34) set .@eco_invest,160000;
		if(.@Economy >= 44) set .@eco_invest,320000;
		if(.@Economy >= 54) set .@eco_invest,640000;
		if(.@Economy >= 65) set .@eco_invest,1280000;
		if(.@Economy >= 76) set .@eco_invest,2560000;
		if(.@Economy >= 88) set .@eco_invest,5120000;
		mes "["+getarg(0)+"]";
		mes "提高商業發展度的話";
		mes "，在工會裡生產的物品數量會增加，為了以後著想的話，不能不投資啊";
		mes " ";
		mes "照原先的方式，一天只能投資一次";
		mes "，假如多付一點金錢的話";
		mes "，一天可以投資兩次";
		mes " ";
		if (.@Economy == 100) {
			mes "^ff0000目前商業發展度以經是100了，你已經不需要再加入任何投資了^000000";
			return 0;
		}
		if (GetCastleData(getarg(1),4) == 2) {
			mes "^ff0000可是你已經在今天之內投資了兩次，再不能投資了^000000.讓發展度趕快上升，期待著富貴的日子囉";
			return 0;
		}
		else if (GetCastleData(getarg(1),4) == 0) {
			mes "現在需要的投資金額是 ^FF0000" + .@eco_invest + "^000000 金幣";
			mes "，請問你要投資嗎?";
		}
		else {
			set @def_invest,@def_invest*4;
			mes "現在你已經投資一次了...";
			mes "還要再投資嗎? 還需要追加^FF0000" + .@eco_invest + "^000000";
			mes "金幣噢";
		}
		next;
		if (select("對商業發展度投資:取消") == 1) {
			mes "["+getarg(0)+"]";
			if (Zeny < .@eco_invest) {
				mes "主人";
				mes "，你沒有足夠的資金可以投資商業發展度";
				return 0;
			}
			set zeny,zeny-.@eco_invest;
			SetCastleData getarg(1),4,GetCastleData(getarg(1),4)+1;
			SetCastleData getarg(1),2,.@Economy + 1 + (.@Economy<99 && rand(2) && getgdskilllv(.@GID,10014));
			mes "順利地完成投資了!";
			mes "期待著我們往後要面臨的未來";
			mes "，明天的話發展度會上升";
			return 0;
		}
		mes "["+getarg(0)+"]";
		mes "我將依照你的吩咐去執行 ，是我的主人";
		mes "... 不需要太著急 ，我們會做得很好";
		return 0;

	case 3:
		set .@Defence,GetCastleData(getarg(1),3);
		if(.@Defence < 8) set .@def_invest,20000;
		if(.@Defence >= 8) set .@def_invest,40000;
		if(.@Defence >= 16) set .@def_invest,80000;
		if(.@Defence >= 25) set .@def_invest,160000;
		if(.@Defence >= 34) set .@def_invest,320000;
		if(.@Defence >= 44) set .@def_invest,640000;
		if(.@Defence >= 54) set .@def_invest,1280000;
		if(.@Defence >= 65) set .@def_invest,2560000;
		if(.@Defence >= 76) set .@def_invest,5120000;
		if(.@Defence >= 88) set .@def_invest,10240000;
		mes "["+getarg(0)+"]";
		mes "提高防禦程度的話";
		mes "，監護人和華麗金屬的耐久度會增加";
		mes "，考慮到往後的戰爭 ，不得不投資啊";
		mes " ";
		mes "照原先的方式，一天只能投資一次";
		mes "，假如多付一點金錢的話";
		mes "，一天可以投資兩次";
		mes " ";
		if (.@Defence == 100) {
			mes " ";
			mes "^ff0000目前防禦度以經是100了，你已經不需要再加入任何投資了^000000";
			return 0;
		}
		if (GetCastleData(getarg(1),5) == 2) {
			mes "^ff0000可是你已經在今天之內投資了兩次，再不能投資了^000000.讓防禦度趕快上升，期待著富貴的日子囉";
			return 0;
		}
		else if (GetCastleData(getarg(1),5) == 0) {
			mes "現在需要的投資金額是 ^FF0000" + .@def_invest + "^000000 金幣";
			mes "，請問你要投資嗎?";
		}
		else {
			set @def_invest,@def_invest*4;
			mes "現在你已經投資過一次了...";
			mes "還要再投資嗎? 還需要追加^FF0000" + .@def_invest + "^000000";
			mes "金幣噢";
		}
		next;

		if (select("對防禦度 投資:取消") == 1) {
			mes "["+getarg(0)+"]";
			if (Zeny < .@def_invest) {
				mes "主人，";
				mes "你沒有足夠的資金可以投資防禦度";
				return 0;
			}
			set Zeny,Zeny-.@def_invest;
			SetCastleData getarg(1),5,GetCastleData(getarg(1),5)+1;
			SetCastleData getarg(1),3,.@Defence+1;
			mes "順利地完成投資了!";
			mes "期待著我們往後要面臨的未來";
			mes "，明天的話防禦度會上升";
			return 0;
		}
		mes "["+getarg(0)+"]";
		mes "我將依照你的吩咐去執行 ，是我的主人";
		mes "... 不需要太著急 ，我們會做得很好";
		return 0;

	case 4:
		mes "["+getarg(0)+"]";
		mes "請問你要設置監護人嗎?";
		mes "監護人即將成為，我們秘密基地的忠誠守護者";
		mes " 請選擇要設置的監護人";
		next;

		for( set .@i, 0; .@i <= 7 ; set .@i, .@i+1 )
		{
			if      (.@guardiantype[.@i] == 1) set .@type$,"士兵 監護人";
			else if (.@guardiantype[.@i] == 2) set .@type$,"弓箭手 監護人";
			else                               set .@type$,"騎士 監護人";

			if( guardianinfo(getarg(1),.@i,0) )
				setarray .@name$[.@i], .@type$ + " - 設置好了 (" + guardianinfo(getarg(1),.@i,2) + "/" + guardianinfo(getarg(1),.@i,1) + ")";
			else
				setarray .@name$[.@i], .@type$ + " - 沒設置";
		}
	
		set .@menu$,.@name$[0]+":"+.@name$[1]+":"+.@name$[2]+":"+.@name$[3]+":"+.@name$[4]+":"+.@name$[5]+":"+.@name$[6]+":"+.@name$[7];

		// this thing will propagate outside of this function
		switch(select(.@menu$)) {
		case 1: set @GDnum,10; break;
		case 2: set @GDnum,11; break;
		case 3: set @GDnum,12; break;
		case 4: set @GDnum,13; break;
		case 5: set @GDnum,14; break;
		case 6: set @GDnum,15; break;
		case 7: set @GDnum,16; break;
		case 8: set @GDnum,17; break;
		}

		mes "["+getarg(0)+"]";
		mes "確定要設置已選擇的監護人嗎?";
		mes "設置時需要 10000 金幣.";
		next;

		if (select("要設置:取消") == 1) {
			mes "["+getarg(0)+"]";
			if (getgdskilllv(.@GID,10002) == 0) {
				mes "主人";
				mes "，設置監護人已經被取消了";
				mes "，假如想要設置監護人的話";
				mes "，先學會工會技能中 ^FF0000研究監護人^000000";
				mes "的技能吧";
				return 0;
			}
			if (GetCastleData(getarg(1),@GDnum) == 1) {
				mes "主人...";
				mes "那監護人已經是主人設置的監護人了";
				mes "，不能再設置了";
				return 0;
			}
			if (Zeny < 10000) {
				mes "主人";
				mes "，你沒有足夠的資金可以設置監護人";
				return 0;
			}
			set zeny,zeny-10000;
			SetCastleData getarg(1),@GDnum,1; // mark as 'installed'
			// guardian will be spawned outside of this function
			mes "完成設置監護人了";
			mes "，現在開始已經有了，保護我們秘密基地";
			mes "的強壯我軍了";
			return 1;

		}
		mes "["+getarg(0)+"]";
		mes "我已經按照你的吩咐做了";
		mes "，假如有點資金上寬裕的話，一定要設置監護人，是較好的方案";
		return 0;

	case 5:
		if (GetCastleData(getarg(1),9) == 1) {
			mes "["+getarg(0)+"]";
			mes "請問您要解雇卡普拉服務人員嗎?";
			next;
			switch(select("要解雇:取消")) {
			case 1:
				cutin "kafra_01",2;
				mes "[卡普拉 職員]";
				mes "我做錯了什麼事嗎? 如果我有做錯事的話，能請您原諒我，再給我一次機會嗎?";
				next;
				switch(select("解雇:取消")) {
				case 1:
					mes "[卡普拉 職員]";
					mes "不能夠繼續為您服務真是我最大的遺憾...";
					next;
					cutin "kafra_01",255;
					break;
				case 2:
					mes "[卡普拉 職員]";
					mes "我會努力勤奮的為您服務的!";
					close2;					
					cutin "kafra_01",255;
					return 0;
				}
				break;
			case 2:
				mes "["+getarg(0)+"]";
				mes "主人，我想我們應該要留下現在這位卡普拉職員，因為她已經竭盡所能地為我們服務了!";
				return 0;
			}	
			disablenpc "卡普拉 職員::"+getarg(4);
			SetCastleData getarg(1),9,0;
			mes "["+getarg(0)+"]";
			mes "....";
			mes "卡普拉職員已經解雇了... 不過為了辛苦的會員著想，雇用卡普拉職員為上策啊";
			return 0;
		}
		else {
			mes "["+getarg(0)+"]";
			mes "和卡普拉總公司訂契約後";
			mes "，想要在秘密基地裡面";
			mes "，雇用卡普拉職員嗎 ?";
			mes " ^FF0000想要雇用的話，需要 10000金幣...^000000";
			next;
			if (select("要雇用:取消") == 1) {
				mes "["+getarg(0)+"]";
				if (getgdskilllv(.@GID,10001) == 0) {
					mes "主人";
					mes "，如今還沒跟卡普拉總公司訂契約狀況";
					mes "，所以還不能雇用卡普拉職員";
					mes "，假如想要跟卡普拉總公司簽訂契約的話";
					mes "，先學會工會技能中 ^FF0000和卡普拉訂契約^000000";
					mes "的技能吧";
					return 0;
				}
				if (Zeny < 10000) {
					mes "["+getarg(0)+"]";
					mes "主人";
					mes "，你沒有足夠的資金可以僱用卡普拉職員";
					return 0;
				}
				set Zeny,Zeny-10000;
				enablenpc "卡普拉 職員::"+getarg(4);
				SetCastleData getarg(1),9,1;
				mes "成功的和卡普拉總公司簽訂契約了，您僱用了一位卡普拉職員!";
				next;
				cutin "kafra_01",2;
				mes "[卡普拉 職員]";
				mes "您好嗎? 我是總公司派來的卡普拉職員! 我將會竭盡所能地為您服務!";
				next;
				cutin "kafra_01",255;
				mes "["+getarg(0)+"]";
				mes "雇用的卡普拉職員將有";
				mes "^FF00001 個月的時間^000000 在此服務";
				mes "，時間到了之後必須重新簽訂契約";
				return 0;
			}
			mes "["+getarg(0)+"]";
			mes "我已經按照你的吩咐做了";
			mes "，不過也要為了辛苦的會員著想，早日雇用卡普拉職員為上策啊";
			return 0;
		}

	case 6:
		mes "["+getarg(0)+"]";
		mes "要到我們生產品囤積的，真貴物品房間嗎?";
		mes "那個地方是為了我們的主人，的專屬於你的地方...";
		mes "除了你之外，其它人都不能其門而入";
		next;
		mes "["+getarg(0)+"]";
		mes "主人，對寶物箱沒有在適當的時間內開啟的話，當發生不可預知的情況時，";
		mes "會有物品消失的情形，敬請特別注意，";
		mes "所以為了我們公會發展，請挪出時間來收取物品";
		next;
		if (select("要進入主人的房間:取消") == 1) {
			mes "["+getarg(0)+"]";
			mes "我將會為你服務到秘密通道那裡，請跟我來，";
			mes "回來的時候按下秘密按鈕即可";
			close2;
			warp getarg(1),getarg(2),getarg(3);
			end;
		}
		mes "["+getarg(0)+"]";
		mes "生產品一天只生產一次 . . .";
		mes "假如沒有即時收走的話，下一次就不能進來了";
		mes "所以說為了我們工會的發展，盡量花點時間去收取啊";
		return 0;

	//case 7:
	//	mes "["+getarg(0)+"]";
	//	mes "Master!!";
	//	mes "Will you give up this Castle after all that we have all gone through?!";
	//	mes "If.. If this is your final decision, what does all the blood that has been shed mean to you!?";
	//	mes "Please reconsider! Master!!";
	//	next;
	//	if (select("Leave Agit.:Cancel") == 1) {
	//		mes "["+getarg(0)+"]";
	//		mes "Master!! Please think it over, Master!!";
	//		mes "The blood we shed so far...don't you care for all the sacrifices that were made?!";
	//		next;
	//		if (select("Cancel:Leave Castle.") == 1) {
	//			mes "["+getarg(0)+"]";
	//			mes "I knew you were kidding, Master!!";
	//			mes "Please, Master.. Although you were kidding, don't do that again. You really scared me for a moment.";
	//			return 0;
	//		}
	//		mes "["+getarg(0)+"]";
	//		mes "Master!!.....";
	//		mes "...... Ma..s...ter.....";
	//		callfunc "F_GuildBreak",getarg(1),getarg(4);
	//		return 0;
	//	}
	//	mes "["+getarg(0)+"]";
	//	mes "I knew you were kidding, Master!!";
	//	mes "Please, Master.. Although you were kidding, don't do that again. You really scared me for a moment.";
	//	return 0;
	}
}
