//===== eAthena Script =======================================
//= War of Emperium Guild Manager Function
//===== By: ==================================================
//= jAthena - kalen (1.0)
//= 1.1 by Akaru, ho|yAnge|X, and Valaris
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= eAthena 0.1+; RO Episode 4+
//===== Description: =========================================
//= The Guild Manager allows the Guildmaster to invest in comerce
//= and defense, hire guardians and kafras, go to the treasure room,
//= and surrender the guild castle.
//==============================================
//= Break down of arguments used in the function:
//=   arg(0): name of Castle Manager
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp to treasure room
//=   arg(3): y1 coordinate for warp to treasure room
//=   arg(4): guild script suffix for kafra, gaurdian scripts etc.
//===== Additional Comments: =================================
//= 1.31: Added support for Emsolute Develop [celest]
//= 1.2: All Guild manager scripts use this function. Optimized Comerce and Defense investment. [kobra_k88]
//= 1.2a Function now returns to script that called it. Added disablenpc line to surrender castle option to remove kafra upon surrender.[kobra_k88]
//= 1.2b U can't surrender the base during WOE [Lupus]
//= 1.2c Fixed issue of guardians hp not increasing upon defense investment.[kobra_k88]
//= 1.3 Now you can't install Guardians during WOE [Lupus]
//= 1.4 Remove surrender abbility (Was 100% custom as far as I can tell) [Kayla]
//= 1.41 Fixed possible Economy investment overflow with Emsolute Develop learnt [Lupus]
//= 1.5 Official Novice Castles Menu (u can't invest / hire guardians) [Lupus]
//==============================================
function	script	F_GldManager	{

	set @GID, GetCastleData(getarg(1)+".gat",1);
	if (strcharinfo(0) == getguildmaster(@GID)) goto L_Start;
	if (@GID == 0) goto L_NotOwn;
	if (getcharid(2) == @GID) goto L_Mem;

L_NotMem:
	mes "[ "+getarg(0)+" ]";
	mes "我追隨我的主人 ^5533FF" + getguildmaster(@GID) + "^000000 ! 嘿! 你不是這個公會的會員!!";
	mes "守衛呢? 守衛!? 快消滅這些入侵者!";
	return 0;

L_NotOwn:
	mes "[ "+getarg(0)+" ]";
	mes "我在此等待我的主人... 勇趕的冒險者啊! 追隨你的命運吧!";
	return 0;

L_Mem:
	mes "[ "+getarg(0)+" ]";
	mes "你不是 ^5533FF" + getguildmaster(@GID) + "^000000! 我只遵從 ^5533FF" + getguildmaster(@GID) + "^000000 主人的命令!";
	return 0;

L_Start:
	mes "[ "+getarg(0)+" ]";
	mes "你好! 我的主人 ^5533FF" + getguildmaster(@GID) + "^000000 ! 有什麼我能為您效勞的地方嗎?";
	next;

	//Novice Castles. (we don't need ELSE here. Menu has direct labels)
	if(getarg(1) == "nguild_prt" || getarg(1) == "nguild_alde" || getarg(1) == "nguild_gef" || getarg(1) == "nguild_pay" )
		menu "僱用/解雇 卡普拉服務人員",M_Kaf, "進入珍寶庫房",M_Treas, "取消",M_End;

	//Common WoE Castles
	menu 	"給我做個簡報吧",M_Base, "投資商業發展",M_Comrc, "投資城堡防禦",M_Def, "設置守衛",M_Gaurd, 
		"僱用/解雇 卡普拉服務人員",M_Kaf, "進入珍寶庫房",M_Treas, "取消",M_End;

	//========================
	M_Base:
	//=========
		mes "[ "+getarg(0)+" ]";
		mes "現在向您報告秘密基地的狀況";
		mes " ";
		mes "目前的商業發展程度: ^FF3322" + GetCastleData(getarg(1)+".gat",2) + "^000000";
		mes "^0000ff - 您在過去一天之內投資了 " + GetCastleData(getarg(1)+".gat",4) + " 次^000000";
		next;
		mes "[ "+getarg(0)+" ]";
		mes "目前的城堡防禦程度: ^FF3322" + GetCastleData(getarg(1)+".gat",3)  + "^000000";
		mes "^0000ff- 您在過去一天之內投資了 " +  GetCastleData(getarg(1)+".gat",5)  + " 次^000000";
		mes " ";
		mes "報告完畢!";
		return 0;

	//========================
	M_Comrc:
	//=========
		set @TriggerE,GetCastleData(getarg(1)+".gat",4);
		set @Economy,GetCastleData(getarg(1)+".gat",2);
		if(@Economy < 8) set @eco_invest,10000;
		if(@Economy >= 8) set @eco_invest,20000;
		if(@Economy >= 16) set @eco_invest,40000;
		if(@Economy >= 25) set @eco_invest,80000;
		if(@Economy >= 34) set @eco_invest,160000;
		if(@Economy >= 44) set @eco_invest,320000;
		if(@Economy >= 54) set @eco_invest,640000;
		if(@Economy >= 65) set @eco_invest,1280000;
		if(@Economy >= 76) set @eco_invest,2560000;
		if(@Economy >= 88) set @eco_invest,5120000;

		mes "[ "+getarg(0)+" ]";
		if(@TriggerE == 2) goto L_MaxTimesC;
		if(@Economy >= 100) goto L_MaxInvestC;
		mes "提高商業發展程度的話，公會生產的物品數量會增加!";
		mes "所以為了將來著想的話，這是一定要投資的啊!";
		next;
		mes "[ "+getarg(0)+" ]";
		if(@TriggerE == 0) mes "您一天能投資兩次，不過第二次投資需要的金額會比較多";
		if(@TriggerE == 0) mes "投資所需金額為 ^5533FF" + @eco_invest + "^000000 zeny.";
		if(@TriggerE == 1) set @eco_invest,@eco_invest*4;
		if(@TriggerE == 1) mes "您今天已經投資過一次了，如果想再投資一次則需要 ^5533FF" + @eco_invest + "^000000 Zeny.";
		next;
		mes "[ "+getarg(0)+" ]";
		mes "請問你要投資嗎?";
		next;
		menu "投資商業發展",-,"放棄",M_End;

			if(Zeny < @eco_invest) goto sL_NoZenyC;
			set Zeny,Zeny-@eco_invest;
			SetCastleData getarg(1)+".gat",4,@TriggerE+1;
			// if we learnt Emsolute Develop there's 50% chance to get +1 investment again
			SetCastleData getarg(1)+".gat",2,@Economy + 1 + (getgdskilllv(@GID,10014)>0 && rand(100)>50 && @Economy<99);
			mes "[ "+getarg(0)+" ]";
			mes "順利地完成投資了!";
			return 0;

			sL_NoZenyC:
				mes "[ "+getarg(0)+" ]";
				mes "主人! 您沒有足夠的金額可以投資商業發展!";
				return 0;
		L_MaxTimesC:
			mes "^ff0000主人! 您今日投資次數已達上限! ^000000 我真希望快點看到我們的公會成長茁壯，越來越強大!";
 			return 0;
		L_MaxInvestC:
			mes "[ "+getarg(0)+" ]";
			mes " ";
			mes "^ff0000目前此城堡的商業發展程度以達 100 % 了，您已經不需要再加入任何投資了^000000";
	 		return 0;

	//=========================
	M_Def:
	//========
		set @TriggerD,GetCastleData(getarg(1)+".gat",5);
		set @Defence,GetCastleData(getarg(1)+".gat",3);
		if(@Defence < 8) set @def_invest,20000;
		if(@Defence >= 8) set @def_invest,40000;
		if(@Defence >= 16) set @def_invest,80000;
		if(@Defence >= 25) set @def_invest,160000;
		if(@Defence >= 34) set @def_invest,320000;
		if(@Defence >= 44) set @def_invest,640000;
		if(@Defence >= 54) set @def_invest,1280000;
		if(@Defence >= 65) set @def_invest,2560000;
		if(@Defence >= 76) set @def_invest,5120000;
		if(@Defence >= 88) set @def_invest,10240000;

		mes "[ "+getarg(0)+" ]";
		if(@TriggerD == 2) goto L_MaxTimesD;
		if(@Defence >= 100) goto L_MaxInvestD;
		mes "提高城堡防禦程度的話，僱用的守衛以及城堡的華麗金屬生命值將會增加!";
		mes "所以為了城堡的安全著想的話，這是一定要投資的啊!";
		next;
		mes "[ "+getarg(0)+" ]";
		if(@TriggerD == 0) mes "您一天能投資兩次，不過第二次投資需要的金額會比較多";
		if(@TriggerD == 0) mes "投資所需金額為 ^5533FF" + @def_invest + "^000000 zeny.";	
		if(@TriggerD == 1) set @def_invest,@def_invest*4;
		if(@TriggerD == 1) mes "您今天已經投資過一次了，如果想再投資一次則需要 ^5533FF" + @def_invest + "^000000 Zeny.";
		next;
		mes "[ "+getarg(0)+" ]";
		mes "請問你要投資嗎?";
		next;
		menu "投資城堡防禦",-, "放棄",M_End;

			if(Zeny < @def_invest) goto sL_NoZenyD;
			set Zeny,Zeny-@def_invest;
			SetCastleData getarg(1)+".gat",5,@TriggerD+1;
			SetCastleData getarg(1)+".gat",3,@Defence+1;
			// set new hp values for guardians
			set @Defence, @Defence + 1;
			set @AGuardian, 28634 + (@Defence*2000);
			set @KGuardian, 30214 + (@Defence*2000);
			set @SGuardian, 15670 + (@Defence*2000);
			//set @AGuardian,strmobinfo(4,1285) + (@Defence*2000);
			//set @KGuardian,strmobinfo(4,1286) + (@Defence*2000);
			//set @SGuardian,strmobinfo(4,1287) + (@Defence*2000);
			if (GetCastleData(getarg(1)+".gat",10) == 1) SetCastleData getarg(1)+".gat",18,@SGuardian;
			if (GetCastleData(getarg(1)+".gat",11) == 1) SetCastleData getarg(1)+".gat",19,@SGuardian;
			if (GetCastleData(getarg(1)+".gat",12) == 1) SetCastleData getarg(1)+".gat",20,@SGuardian;
			if (GetCastleData(getarg(1)+".gat",13) == 1) SetCastleData getarg(1)+".gat",21,@AGuardian;
			if (GetCastleData(getarg(1)+".gat",14) == 1) SetCastleData getarg(1)+".gat",22,@AGuardian;
			if (GetCastleData(getarg(1)+".gat",15) == 1) SetCastleData getarg(1)+".gat",23,@KGuardian;
			if (GetCastleData(getarg(1)+".gat",16) == 1) SetCastleData getarg(1)+".gat",24,@KGuardian;
			if (GetCastleData(getarg(1)+".gat",17) == 1) SetCastleData getarg(1)+".gat",25,@KGuardian;

			mes "[ "+getarg(0)+" ]";
			mes "順利地完成投資了!";
			return 0;

			sL_NoZenyD:
				mes "[ "+getarg(0)+" ]";
				mes "主人! 您沒有足夠的金額可以投資城堡防禦!";
				return 0;
		L_MaxTimesD:
			mes "^ff0000主人! 您今日投資次數已達上限! ^000000 我真希望快點看到我們的公會成長茁壯，越來越強大!";
 			return 0;
		L_MaxInvestD:
			mes "^ff0000目前此城堡的城堡防禦程度以達 100 % 了，您已經不需要再加入任何投資了^000000";
	 		return 0;

	//=========================
	M_Gaurd:
	//=========
		if (getgdskilllv(@GID,10002) == 0) goto L_NoSkGuard;
		set @Defence,GetCastleData(getarg(1)+".gat",3);
		set @Guardian0,guardianinfo(0);
		set @Guardian1,guardianinfo(1);
		set @Guardian2,guardianinfo(2);
		set @Guardian3,guardianinfo(3);
		set @Guardian4,guardianinfo(4);
		set @Guardian5,guardianinfo(5);
		set @Guardian6,guardianinfo(6);
		set @Guardian7,guardianinfo(7);
		set @AGuardian, 28634 + (@Defence*2000);
		set @KGuardian, 30214 + (@Defence*2000);
		set @SGuardian, 15670 + (@Defence*2000);
		//set @AGuardian,strmobinfo(4,1285) + (@Defence*2000);
		//set @KGuardian,strmobinfo(4,1286) + (@Defence*2000);
		//set @SGuardian,strmobinfo(4,1287) + (@Defence*2000);

		mes "[ "+getarg(0)+" ]";
		if(agitcheck(0) != 0) goto L_CantGuard;
		mes "設置守衛的話，這些守衛可以協助抵禦外來的入侵者";
		mes "請選擇守衛種類";
		next;
	
		menu "士兵 守衛 (" + @Guardian0 + "/" + @SGuardian + ")",L4_1,
			"士兵 守衛 (" + @Guardian1 + "/" + @SGuardian + ")",L4_2,
			"士兵 守衛 (" + @Guardian2 + "/" + @SGuardian + ")",L4_3,
			"弓箭手 守衛 (" + @Guardian3 + "/" + @AGuardian + ")",L4_4,
			"弓箭手 守衛 (" + @Guardian4 + "/" + @AGuardian + ")",L4_5,
			"騎士 守衛 (" + @Guardian5 + "/" + @KGuardian + ")",L4_6,
			"騎士 守衛 (" + @Guardian6 + "/" + @KGuardian + ")",L4_7,
			"騎士 守衛 (" + @Guardian7 + "/" + @KGuardian + ")",L4_8;
	
		L4_1:
			if (GetCastleData(getarg(1)+".gat",10) == 1) goto L_GotGuard;
			set @GDnum,10;
			set @GDnum2,18;
			set @GuardianHP,@SGuardian;
			goto L4_9;
		L4_2:
			if (GetCastleData(getarg(1)+".gat",11) == 1) goto L_GotGuard;
			set @GDnum,11;
			set @GDnum2,19;
			set @GuardianHP,@SGuardian;
			goto L4_9;
		L4_3:
			if (GetCastleData(getarg(1)+".gat",12) == 1) goto L_GotGuard;
			set @GDnum,12;
			set @GDnum2,20;
			set @GuardianHP,@SGuardian;
			goto L4_9;
		L4_4:
			if (GetCastleData(getarg(1)+".gat",13) == 1) goto L_GotGuard;
			set @GDnum,13;
			set @GDnum2,21;
			set @GuardianHP,@AGuardian;
			goto L4_9;
		L4_5:
			if (GetCastleData(getarg(1)+".gat",14) == 1) goto L_GotGuard;
			set @GDnum,14;
			set @GDnum2,22;
			set @GuardianHP,@AGuardian;
			goto L4_9;
		L4_6:
			if (GetCastleData(getarg(1)+".gat",15) == 1) goto L_GotGuard;
			set @GDnum,15;
			set @GDnum2,23;
			set @GuardianHP,@KGuardian;
			goto L4_9;
		L4_7:
			if (GetCastleData(getarg(1)+".gat",16) == 1) goto L_GotGuard;
			set @GDnum,16;
			set @GDnum2,24;
			set @GuardianHP,@KGuardian;
			goto L4_9;
		L4_8:
			if (GetCastleData(getarg(1)+".gat",17) == 1) goto L_GotGuard;
			set @GDnum,17;
			set @GDnum2,25;
			set @GuardianHP,@KGuardian;
		L4_9:
			mes "[ "+getarg(0)+" ]";
			mes "請問您確定要設置守衛嗎? 每設置一個守衛需要 ^5533FF10,000 zeny^000000";
			next;
			menu "設置守衛",-, "放棄",M_End;

				if (Zeny < 10000) goto sL_NoZenyG;
				set Zeny,Zeny-10000;
				SetCastleData getarg(1)+".gat",@GDnum,1;
				SetCastleData getarg(1)+".gat",@GDnum2,@GuardianHP;
				return 1;

				sL_NoZenyG:
					mes "[ "+getarg(0)+" ]";
					mes "主人! 您沒有足夠的金額可以設置守衛!";
					return 0;
		L_NoSkGuard:
			mes "[ "+getarg(0)+" ]";
			mes "主人! 很抱歉您目前沒有辦法設置任何一個守衛! 您的公會必須具備 ^5533FF研究監護人^000000 技能才能設置守衛";
			mes "設置守衛已經被取消.";
			return 0;
		L_GotGuard:
			mes "[ "+getarg(0)+" ]";
			mes "抱歉! 主人! 那個位置的守衛已經設置過了.....";
			emotion 4;
			return 0;
		L_CantGuard:
			mes "主人,您不知道在公會戰的時候不能設置守衛嗎!!";
			emotion 4;
			return 0;

	//===========================
	M_Kaf:
	//======
		mes "[ "+getarg(0)+" ]";
		if (GetCastleData(getarg(1)+".gat",9) == 1) goto L_Dismiss;
		if (getgdskilllv(@GID,10001) == 0) goto L_NoSkKaf;

		L_Hire:
			mes "請問您想僱用一個公會專屬的卡普拉服務人員嗎? 這將需要 ^5533FF10,000 Zeny^000000 ";
			next;
			menu "僱用卡普拉服務人員",-,"放棄",sM_KafEnd;

				mes "[ "+getarg(0)+" ]";
				if (Zeny < 10000) goto sL_NoZenyK;
				set Zeny,Zeny-10000;
				enablenpc "卡普拉服務人員#"+getarg(4);
				SetCastleData getarg(1)+".gat",9,1;
				mes "成功的和卡普拉總公司簽訂契約了，您僱用了一位卡普拉服務人員!";
				next;
				cutin "kafra_01",2;
				mes "[卡普拉服務人員]";
				mes "您好嗎? 我是總公司派來的卡普拉服務人員! 我將會竭盡所能地為您服務!";
				next;
				cutin "kafra_01",255;
				mes "[ "+getarg(0)+" ]";
				//mes "僱用的卡普拉服務人員將有 ^5533FF1 個月的時間^000000 在此服務，時間到了之後必須重新簽訂契約!";
				mes "我想這位卡普拉服務人員一定能嘉惠公會會員們的!";
				return 0;

				sL_NoZenyK:
					mes "主人! 您沒有足夠的金額可以僱用卡普拉服務人員!";
					return 0;
			sM_KafEnd:
				mes "[ "+getarg(0)+" ]";
				mes "好吧，不過我建議您盡快僱用一位卡普拉服務人員!";
				return 0;

		L_Dismiss:
			mes "請問您要解雇卡普拉服務人員嗎?";
			next;
			menu "解雇卡普拉服務人員",-,"取消",sM_KafEnd2;

				cutin "kafra_01",2;
				mes "[卡普拉服務人員]";
				mes "我做錯了什麼事嗎? 如果我有做錯事的話，能請您原諒我，再給我一次機會嗎?";
				next;
				menu "解雇",-,"取消",ssM_KafEnd2;

					mes "[卡普拉服務人員]";
					mes "不能夠繼續為您服務真是我最大的遺憾...";
					next;
					disablenpc "卡普拉服務人員#"+getarg(4);
					SetCastleData getarg(1)+".gat",9,0;
					cutin "kafra_01",255;
					mes "[ "+getarg(0)+" ]";
					mes "卡普拉服務人員已經解雇了...不過...我們實在需要一位卡普拉服務人員來為我們服務啊!";
					return 0;
				ssM_KafEnd2:
					mes "[卡普拉服務人員]";
					mes "Thank you master, I'll do my best! ^^.";
					cutin "kafra_01",255;
					return 0;
			sM_KafEnd2:
				mes "[ "+getarg(0)+" ]";
				mes "主人，我想我們應該要留下現在這位卡普拉服務人員，因為她已經竭盡所能地為我們服務了!";
				return 0;

		L_NoSkKaf:
			mes "主人! 您並沒有與卡普拉總公司簽訂契約!";
			mes "如果要僱用卡普拉服務人員的話，您的公會必須具備 ^5533FF和卡普拉訂契約^000000 技能才能僱用卡普拉服務人員";
			return 0;

	//=========================
	M_Treas:
	//========
		mes "[ "+getarg(0)+" ]";
		mes "您要進入珍寶庫房嗎? 只有你，公會長，才能夠進入這個房間";
		next;
		menu "進入珍寶庫房",-,"取消",sM_TresEnd;

			mes "[ "+getarg(0)+" ]";
			mes "請您跟著我穿過這條秘密走道";
			mes "到了房間之後您必須拉下秘密機關才能再出來";
			next;
			warp getarg(1)+".gat",getarg(2),getarg(3);
			return 0;
		sM_TresEnd:
			mes "[ "+getarg(0)+" ]";
			mes "公會產品一天只會生產一次";
			mes "當您有空的時候請記得盡快前往取出產品，否則他們將會自動消失!";
			return 0;


	//==========================
	M_End:
	//=======
		mes "[ "+getarg(0)+" ]";
		mes "我將隨時聽後您的差遣! 主人!";
		return 0;
}
