//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 攻城戰 旗幟 腳本 - 
//===================================
// 版本：1.6
//===================================

function	script	F_Flags	{
	set .@GID, GetCastleData(getarg(1),1);
	if (getarg(5) == 0) return;
	if (.@GID == 0) {
		mes "　　 [ 普隆德拉王國 公告令 ]";
		mes " ";
		mes "1. 根據這裡的，普隆德拉王國令";
		mes "　　" + getarg(0) + "被認定為";
		mes "　　沒有正式所有權的秘密基地";
		mes " ";
		mes "2. 戰勝守在這裡，可怕的考驗後，";
		mes "　　給破壞華麗金屬的人，";
		mes " ";
		mes "國王會賜予經國王認證的，秘密基地所有權";
		return;
	}
	else {
		if (getcharid(2) == .@GID && getarg(4) == 1) {
			mes "[ 響亮的嗓音 ]";
			mes "勇敢的人們...";
			mes "要回到您的名譽之地嗎?";
			next;
			if (select("回到秘密基地.:放棄") == 1) {
				if (getcharid(2) == GetCastleData(getarg(1),1))
					warp getarg(1),getarg(2),getarg(3);
			}
			//return;
		}
		mes "　　 [ 普隆德拉王國 公告令 ]";
		mes " ";
		mes "1. 根據這裡的，普隆德拉王國令";
		mes "　　這裡被認定為";
		mes "^ff0000" + GetGuildName(.@GID) + "^000000工會的";
		mes "　　私有物";
		mes " ";
		mes "2. ^ff0000"+ GetGuildName(.@GID) + "^000000 工會的主人是";
		mes "　　^ff0000" + GetGuildMaster(.@GID) + "^000000 ， 無論是誰";
		mes "　　對這件事情有意見者";
		mes " ";
		mes "請你舉起您的劍，提高自己的名譽吧";
		return;
	}
}