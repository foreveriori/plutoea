//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 准它 企劃社 服務人員 -
//===================================

function	script	ZondaMain	{

	cutin "zonda_01",2;
	mes "[准它 企劃社 服務人員]";
	mes "歡迎光臨，准它";
	mes "企畫設永遠秉持著最好服務的，";
	mes "請問您需要哪一種服務呢?";
	next;
	switch(select("儲存服務:倉庫服務:手推車服務:結束")){

	case 1:
		savepoint getarg(0),getarg(1),getarg(2);
		mes "[准它 企劃社 服務人員]";
		mes "已經儲存好了 謝謝您的使用";
		break;

	case 2:
		mes "[准它 企劃社 服務人員]";
		if(basicskillcheck() && getskilllv(1) < 6){
			mes "你的基本技能要6級以上";
			mes "才能使用倉庫服務";
			emotion e_sry;
			break;
		}
		if(Zeny < 40){
			mes "顧客，您支付的金額不足啊，";
			mes "倉庫使用費是 40金幣";
			emotion e_cash;
			close;
		}
		set Zeny, Zeny - 40;
		set RESRVPTS, RESRVPTS + 4;
		mes "倉庫將打開了，";
		mes "感謝您的使用";
		close2;
		openstorage;
		cutin "",255;
		end;

	case 3:
		mes "[准它 企劃社 服務人員]";
		if(BaseClass != Job_Merchant){
			mes "很抱歉，";
			mes "手推車是商人和鐵匠、鍊金術師專用的服務";
			emotion e_sry;
			break;
		}
		if(getskilllv(39) == 0){
			mes "如果沒學相關技能的話";
			mes "是沒有手推車的";
			break;
		}
		if(checkcart() == 1){
			mes "你已經租過手推車了";
			break;
		}
		if(countitem(7061)){
			delitem 7061,1;
			setcart;
			break;
		}
		mes "手推車租金是800 Zeny";
		mes "如果沒學相關技能的話";
		mes "是沒有手推車的";
		mes "還要租嗎?";
		next;
		if(select("租用手推車","取消") == 1){
			if(Zeny < 800){
				mes "[准它 企劃社 服務人員]";
				mes "你的金錢不足啊";
				mes "需要800 Zeny";
				emotion e_cash;
				break;
			} else {
				set Zeny, Zeny - 800;
				set RESRVPTS, RESRVPTS + 80;
				setcart;
				break;
			}
			break;
		}
		break;

	case 4:
		mes "[准它 企劃社 服務人員]";
		mes "我們准它 企劃社";
		mes "永遠提供最好服務的，若有不便之處的";
		mes "話請隨時告知我們，";
		mes "下一次預計以更好的服務與你見面";
		break;
	}
	close2;
	cutin "",255;
	end;
}

lighthalzen.gat,94,248,4	script	准它 企劃社 服務人員	874,{
	callfunc "ZondaMain","lighthalzen.gat",94,240;
	end;
}

hugel.gat,88,168,4	script	准它 企劃社 服務人員	874,{
	callfunc "ZondaMain","hugel.gat",96,155;
	end;
}

rachel.gat,109,138,5	script	准它 企劃社 服務人員	874,{
	callfunc "ZondaMain","rachel.gat",115,125;
	end;
}

veins,208,128,5	script	准它 企劃社 服務人員	874,{
	callfunc "ZondaMain","veins.gat",204,103;
	end;
}