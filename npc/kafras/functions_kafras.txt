//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 卡普拉腳本 - 改寫自JA
//===================================

function	script	KafraMain	{
	function KafraSpeech {
		if(getarg(0)!=3) {
			mes "[卡普拉服務人員]";
			mes "歡迎光臨，";
			mes "卡普拉公司隨時為您們服務，";
			switch(getarg(0)) {
			case 0:
			mes "需要什麼服務嗎?";
				break;
			case 1:
			case 4:
			mes "需要什麼服務嗎?";
				break;
			case 2:	//克魔島地區
				mes "我們卡普拉西部中心";
				mes "通過改編程序";
				mes "提供更好品質的服務.";
				mes "謝謝大家~";
				next;
				mes "[卡普拉服務人員]";
				mes "需要什麼服務嗎?";
				break;
			}
			next;
		}
		return;
	}

	//位置ЙみйДみжЗ
	function KafraSave {
		if(getarg(5) == "NULL")
			savepoint getarg(2),getarg(3),getarg(4);
		else if(select("城市外面儲存","城市裡面儲存")==1)
			savepoint getarg(2),getarg(3),getarg(4);
		else
			savepoint getarg(5),getarg(6),getarg(7);
		mes "[卡普拉服務人員]";
		mes "已經儲存好了 謝謝您的使用";
		return;
	}

	//空間移動ДみжЗ
	function KafraTrans {
		if(getarg(0)==4) {
			mes "[卡普拉服務人員]";
			mes "我們卡普拉服務在秀發茲發德總公司，";
			mes "因為和秀發茲發德 共和國";
			mes "的協定，";
			mes "'^ff0000在秀發茲發德內限制傳送服務^000000'";
			mes "的關係，";
			mes "目前不做任何傳送服務的";
			next;
			mes "[卡普拉]";
			mes "想要移動到其它區域時，";
			mes "請使用飛空艇吧";
			return;
		}
		copyarray @price,getarg(11),getarraysize(getarg(11));
		if($placename$[0] == "") {
			setarray $placename$,	"普隆德拉","依斯魯得島","吉芬","斐揚","夢羅克","艾爾貝塔",
						"獸人地下洞窟","艾爾帕蘭","妙勒尼 廢棄礦場","克魔島","發樂斯 燈塔島",
						"發樂斯 燈塔島","朱諾","普隆德拉","吉芬","斐揚","艾爾帕蘭",
						"預備1","預備2","預備3";
		}
		for(set @i,0; @i<getarraysize(@price); set @i,@i+1)
			set @place$[@i],$placename$[ getelementofarray(getarg(10),@i)-1 ] +" -> "+ @price[@i] +" z";
		mes "[卡普拉服務人員]";
		mes "請說你想要去的地方.";
		next;
		set @j,select(		@place$[0],@place$[1],@place$[2],@place$[3],@place$[4],@place$[5],@place$[6],@place$[7],
					@place$[8],@place$[9],@place$[10],@place$[11],@place$[12],@place$[13],@place$[14],@place$[15],
					@place$[16],@place$[17],@place$[18],@place$[19],"取消")-1;
		if(@j==20) {
			mes "[卡普拉服務人員]";
			mes "謝謝";
			mes "歡迎再來";
			return;
		}
		if(Zeny<@price[@j]) {
			mes "[卡普拉服務人員]";
			mes "你的金錢不足啊";
			return;
		}
		set Zeny,Zeny-@price[@j];
		set KAFRA_PIT,KAFRA_PIT+(@price[@j]/10);
		switch ( getelementofarray(getarg(10),@j) ) {
			case 1: warp "prontera.gat",116,72; 	break;
			case 2: warp "izlude.gat",91,105; 	break;
			case 3: warp "geffen.gat",120,39; 	break;
			case 4: warp "payon.gat",160,58; 	break;
			case 5: warp "morocc.gat",156,46; 	break;
			case 6: warp "alberta.gat",117,56; 	break;
			case 7: warp "gef_fild10.gat",52,326; 	break;
			case 8: warp "aldebaran.gat",167,112; 	break;
			case 9: warp "mjolnir_02.gat",99,351; 	break;
			case 10: warp "comodo.gat",209,143; 	break;
			case 11:
			case 12:
				 warp "cmd_fild07.gat",127,134; break;
			case 13: warp "yuno.gat",158,124; 	break;

			case 14: warp "prontera.gat",278,211; 	break;
			case 15: warp "geffen.gat",120,39; 	break;
			case 16: warp "payon.gat",158,88; 	break;
			case 17: warp "aldebaran.gat",132,103;	break;
			//預備
			case 18: warp "SavePoint",0,0;		break;
			case 19: warp "SavePoint",0,0;		break;
			case 20: warp "SavePoint",0,0;		break;
		}
		end;
	}

	//ロみЬДみжЗ
	function KafraCart {
		if(baseClass != Job_Merchant){
			mes "[卡普拉服務人員]";
			mes "很抱歉，";
			mes "手推車是商人和鐵匠、鍊金術師專用的服務";
			return;
		}
		if(checkcart()==1) {
			mes "[卡普拉服務人員]";
			mes "你已經租過手推車了";
			return;
		}
		mes "[卡普拉服務人員]";
		mes "手推車租金未"+getarg(9)+"Zeny。";
		mes "如果沒學相關技能的話";
		mes "是沒有手推車的";
		mes "還要租嗎?";
		next;
		if(select("租用手推車","取消")==2) {
			mes "[卡普拉服務人員]";
			mes "謝謝";
			mes "歡迎再來";
			return;
		}
		if(Zeny<getarg(9)) {
			mes "[卡普拉服務人員]";
			mes "你的金錢不足啊";
			mes "需要"+getarg(9)+"Zeny。";
			return;
		}
		set Zeny,Zeny-getarg(9);
		setcart;
		return;
	}

	//點數確認
	function KafraPoint {
		mes "[卡普拉服務人員]";
		mes strcharinfo(0)+" 顧客的儲蓄點數是 " +KAFRA_PIT+ " 點.";
		next;
		mes "[卡普拉服務人員]";
  		mes "在卡普拉本社你可以把儲蓄點數交換成贈品.";
  		mes "請多用方便的卡普拉服務，順便多得到贈品~";
		return;
	}

	//總結
	function KafraEnd {
		mes "[卡普拉服務人員]";
		mes "我們卡普拉公司的服務是為了讓顧客們放心和方便的使用. 我們會盡我們最大的努力做到更好的服務與嚴格的營業管理.";
		mes "謝謝您們使用我們卡普拉公司的服務.";
		return;
	}

	//アアろヘmain
	KafraSpeech getarg(0);
	setarray @serv$,"儲存服務","倉庫服務","傳送服務","手推車服務","點數累積確認","卡普拉服務人員位置分佈","總結";
	for(set @i,0; @i<7; set @i,@i+1) {
		if(getarg(1)&(1<<@i) == 0)
			set @serv$[@i],"";
	}
	switch (select(@serv$[0],@serv$[1],@serv$[2],@serv$[3],@serv$[4],@serv$[5],@serv$[6])) {
		case 1:
			KafraSave 0,1,getarg(2),getarg(3),getarg(4),getarg(5),getarg(6),getarg(7);
			break;
		case 2:
			callfunc "KafraStorage",getarg(8);
			end;
		case 3:
			if(getarg(0) != 4)
				KafraTrans getarg(0),1,2,3,4,5,6,7,8,9,getarg(10),getarg(11);
			else
				KafraTrans getarg(0);
			break;
		case 4:
			KafraCart 0,1,2,3,4,5,6,7,8,getarg(9);
			break;
		case 5:
			KafraPoint;
			break;
		case 6:
			mes "[卡普拉服務人員]";
			mes "小地圖中顯示了卡普拉服務人員分佈";
			return;
		case 7:
			KafraEnd;
			break;
	}
	close2;
	cutin "kafra_01",255;
	end;
}

//倉庫ДみжЗクん分離
function	script	KafraStorage	{
	if(basicskillcheck() && getskilllv(1)<6) {
		mes "[卡普拉服務人員]";
		mes "你的基本技能要6級以上";
		mes "才能使用倉庫服務";
	}
	else if(Zeny<getarg(0)) {
		mes "[卡普拉服務人員]";
		mes "很抱歉,您的金錢不足";
		mes "這至少需要"+getarg(0)+"Zeny。";
	}
	else {
		mes "[卡普拉服務人員]";
		mes "要打開倉庫了";
		mes "謝謝你的使用.";
		close2;
		set Zeny,Zeny-getarg(0);
		openstorage;
		cutin "kafra_01",255;
		end;
	}
	close2;
	cutin "kafra_01",255;
	return;
}



//----------------------------------
// < 普隆德拉 - 北邊卡普拉 >
//----------------------------------

prt_fild01.gat,198,47,0	script	卡普拉服務人員#prt_fild	112,{
	cutin "kafra_06",2;
	setarray @code,2,3,4,5,6,7;
	setarray @price,600,1200,1200,1200,1800,1700;
	callfunc "KafraMain",1,0x5f,"prt_fild01.gat",198,51,"NULL",0,0,40,800,@code,@price;
	end;
}


//----------------------------------
// < 克魔島 室內 >
//----------------------------------

cmd_in02.gat,146,180,4	script	卡普拉服務人員#cmd_in02	721,{
	cutin "kafra_07",2;
	setarray @code,5,12;
	setarray @price,1800,1200;
	callfunc "KafraMain",2,0x5f,"comodo.gat",209,143,"NULL",0,0,40,800,@code,@price;
	end;
}