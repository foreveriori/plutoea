// 吉芬 -- 各種物品製作腳本
// 
// 重整作者:		Dino9021
// 
// 中文版本編號:	20041213.0.1
//
// 原始腳本:		Kingbo 2003/12/30 PM 08:01
//
//===== eAthena Script ======================================= 
//= Item Quest NPCs located in Geffen
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Welding Mask and Headset Quests.
//===== Additional Comments: ================================= 
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//============================================================ 



//==============================================================================================//
// 					Blacksmith: 'Welding Mask' Quest
//==============================================================================================//
geffen_in.gat,144,166,4	script	老鍛工#01	63,{
	mes "[老鍛工]";
	mes "我是誰？我是這裡獨一無二的老鍛工，你聽說過嗎？！";
	mes "在這個城市裡，我從事了30年的鍛造工作，那真是水和火的地獄.....";
	mes "自從我成為了鍛造工人，我便發明了一些讓人難以置信的東西，你想聽聽嗎？";
	next;
	menu "我很樂意",-,"我沒興趣",M_No;

		mes "[老鍛工]";
		mes "我們一直在火熱的工作間裡熔接鋼鐵，製作各種各樣的金屬產品。";
		mes "因此我們不得不面臨著熾熱的空氣和金屬液體的侵害，我們的臉幾乎燒盡並且還在工作期間不斷的遭到傷害...";
		mes "但現在，已經不需為此而擔心了~因為我發明了一種叫^3355FF'鋼鐵面具'^000000的護具！";
		next;
		mes "[老鍛工]";
		mes "這是一件保護你的臉部和頭部的護具，大多數鍛造工人都想要得到的寶貝。";
		mes "你想要一個嗎？只要你給我50個^2244FF'鋼鐵^000000和^4422FF2000 Zeny^000000，我可以幫你打造一個！";
		next;
		menu "那不錯，我要做一個",-,"不要了，那對我沒用",sM_No;

			if(countitem(999) < 50 || Zeny < 2000) goto sL_NotEnuf;
			delitem 999,50;
			set Zeny, Zeny - 2000;
			mes "[老鍛工]";
			mes "我喜歡你，城裡人！好，我馬上幫你做一個！";
			next;
			mes "[老鍛工]";
			mes "你是我見到的最爽快的人。";
			mes "再見！~";
			getitem 2292,1;
			close;

			sL_NotEnuf:
				mes "[老鍛工]";
				mes "嘿，夥計，要是你沒有足夠的鋼鐵和Zeny，那就趕緊走開吧！";
				mes "難道你沒聽說嗎？做一個面具可不是你想像的那麼容易，無論如何我的手藝總得得到一些報酬！";
				emotion e_swt;
				close;

		sM_No:
			mes "[老鍛工]";
			mes "噢！你會覺得遺憾的，我建議你還是好好考慮一下吧。";
			mes "每個人都會變得越來越老，在你變得醜陋之前，你必須好好保護自己的臉。";
			close;

	M_No:
		mes "[老鍛工]";
		mes "我覺得和你說話真是浪費了我寶貴的時間..我無話可說了，隨便你了！";
		emotion e_pif;
		close;
}


//=============================================================================================//
//					Eric: Headset Quest
//=============================================================================================//
geffen_in.gat,30,71,4	script	艾力斯	83,{
	mes "[艾力斯]";
	mes "你要不要聽聽我爺爺說的故事呢?";
	next;
	menu "繼續對話",-,"詢問材料",M_Ask,"製作看看",M_Make,"取消",M_End;
		mes "[艾力斯]";
		mes "前幾年剛過世的爺爺";
		mes "在我的生命中是個相當重要的人物。";
		mes "比起我那忙碌的父親";
		mes "我可以感覺到爺爺依舊在我身邊守候著我。";
		next;
		mes "[艾力斯]";
		mes "爺爺的書房更是最令我感到有趣的地方。";
		mes "就像是' 世外桃源 '一般。";
		mes "每當我想念我爺爺的時候";
		mes "我就會到書房去找些書來看。";
		next;
		mes "[艾力斯]";
		mes "在那裡";
		mes "我可以透過書籍學習到許多的知識。";
		mes "因為爺爺對於煉金術和音樂都相當的喜愛";
		mes "所以書房裡頭幾乎滿滿的都是相關的書籍。";
		next;
		mes "[艾力斯]";
		mes "直到有一天 ..";
		mes "我像往常一樣進到爺爺的書房裡";
		mes "在那裡我偶然發現了一張相當神奇的設計圖。";
		next;
		mes "[艾力斯]";
		mes "在設計圖中有一樣東西，如果將它像耳罩一樣戴上";
		mes "如果將它像耳罩一樣戴，透過一條細線的牽引";
		mes "將可以聽到像留聲機中一樣的聲音。";
		mes "這就是設計圖的內容。";
		next;
		mes "[艾力斯]";
		mes "我 ..一定要完成爺爺的遺願";
		mes "做出這樣東西來。";
		mes "所以我到處去將這設計圖給其他煉金者看";
		mes "請求他們幫忙，但是所有的人都說不知道這是什麼東西。";
		next;
		mes "[艾力斯]";
		mes "所以我才會想要親自動手來做做看。";
		mes "經過我多年的苦心研究..";
		mes "我已經可以完全理解那圖上的結構了。";
		mes "不過 .. 我還沒能收集到所需要的材料。";
		next;
		mes "[艾力斯]";
		mes "如果現在手邊有材料的話";
		mes "我就可以馬上依照設計圖動手製作了。";
		mes "如果 .. 你感興趣的話";
		mes "是不是可以幫我找到所需要的材料呢?";
		next;
		mes "[艾力斯]";			
		mes "我只是想要完成爺爺的遺願";
		mes "做出設計圖上的東西而已。";
		mes "完成後如果你要的話，給你也沒關係。";
		close;
	M_Ask:
		mes "[艾力斯]";
		mes "我需要^3355FF鋼鐵^000000 ^FF555540^000000 片";
		mes "^3355FF神之金屬^000000 ^FF55551^000000 個";
		mes "^3355FF酒精^000000 ^FF55551^000000 瓶";
		mes "^3355FF煤礦^000000 ^FF55551^000000 塊";
		mes "這些東西。";
		close;
	M_Make:
		if(countitem(999) < 40 || countitem(984) < 1 || countitem(970)<1 || countitem(1003) < 1) goto L_NotEnuf;
		delitem 999,40;
		delitem 984,1;
		delitem 970,1;
		delitem 1003,1;
		mes "[艾力斯]";
		mes "滴答滴答 ..";
		next;
		getitem 5001,1;
		mes "[艾力斯]";
		mes "真是太感謝你了。";
		mes "托你的福我才能完成爺爺的遺願。";
		mes "如果爺爺能看到的話一定會很開心的。";
		next;
		mes "[艾力斯]";
		mes "誠如我之前說過的，完成品請你帶走吧！";
		mes "既然已經完成了，我就感到很高興了。";
		close;

		L_NotEnuf:
			mes "[艾力斯]";
			mes "謝謝你的好意 .. 但是材料似乎不足。";
			mes "請你備齊了再來吧！";
			close;
	M_End:
		mes "[艾力斯]";
		mes "請慢走。";
		close;
}