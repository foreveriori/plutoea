//===== By: ==================================================
//= Playtester
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= SVN eA
//===== Description: =========================================
//= Gunslinger guns and bullet quests
//===== Additional Comments: =================================
//= 1.0 Only Garrison Gun done yet [Playtester]
//= 1.1 Added Garrison[2] [Playtester]
//= 1.2 Added Drifter, Destroyer and Inferno [Playtester]
//============================================================
//2007-02-09  黑色小貓  中文化
//============================================================
//===================== Garrison =====================================================

que_ng.gat,182,85,3	script	加立遜	109,{
	set @npcname$,"[加立遜]";

	mes @npcname$;
	if(countitem(13105)>0) goto L_Already;
	if(countitem(13104)>0) goto L_Slot;
	mes "哇哈哈~~ 我製造了一把新的左輪手槍!";
	mes "我將它命名為... 加立遜手槍!";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "這真是個很有創意的名字!";
	mes "(......汗)";
	next;
	if(BaseJob != Job_Gunslinger || BaseLevel < 55){
		mes @npcname$;
		mes "我不認為你有足夠的槍枝知識，";
		mes "去發揮它的美麗!";
		close;
	}
	mes @npcname$;
	mes "你看起來似乎是槍枝方面的專家! 是嗎?";
	next;
	mes "[" +strcharinfo(0) + "]";
	mes "是的! 你可以這麼稱呼我!";
	next;
	mes @npcname$;
	mes "那麼，你要我幫你製造一把^0000FF加立遜手槍^000000嗎?";
	next;

	switch(select("當然，麻煩你了!","免了! 我還是喜歡我原本的槍!"))
	{

		case 1:
			mes @npcname$;
			mes "太好了! 但是你必須先收集材料給我!";
			mes "我才能幫你製造!";
			next;
			mes @npcname$;
			mes "我需要的材料是...";
			mes "^FF000050 個鋼鐵^000000,";
			mes "^FF00003 個鋁^000000,";
			mes "^FF00001 個神之金屬^000000,";
			mes "^FF000050個 煤礦^000000,";
			mes "^FF000020 個生鏽的螺絲^000000";
			mes "最後還需要... ^FF000030000 Zeny^000000!";
			next;
			mes @npcname$;
			if ( (countitem(999)<50) || (countitem(985)<3) || (countitem(984)<1) || (countitem(1003)<50) || (countitem(7317)<20) || (Zeny < 30000) ) {
				mes "材料收集齊全之後再來找我吧!";
				close;
			}
			delitem 999,50;
			delitem 985,3;
			delitem 984,1;
			delitem 1003,50;
			delitem 7317,20;
			set Zeny,Zeny-30000;
			mes "看來你已經找齊所有材料了!";
			mes "讓我看看...";
			next;
			mes @npcname$;
			mes "嗯...等我一下...";
			mes "...";
			mes ".....";
			next;
			getitem 13104,1;
			mes @npcname$;
			mes "做好了! 給你啦~!";
			mes "一把最棒的手槍!";
			close;

		case 2:
			mes @npcname$;
			mes "嗯... 看來一個真正的槍手，";
			mes "絕對離不開他自己最愛的槍!";
			close;
	}

L_Slot:
	mes "喔~ 我看到你有一把我製造的手槍!";
	mes "那是我做過最棒的一把槍!";
	next;
	mes @npcname$;
	mes "嗯... 但是依我所見，";
	mes "我可以再把它改的更精良!";
	next;
	mes @npcname$;
	mes "不如我幫你^0000FF打洞^000000吧?!";
	mes "但是要注意!";
	mes "如果你已經插上卡片的話，";
	mes "它將在打洞完成之後消失!";
	next;
	switch(select("好的! 麻煩你幫我打洞吧!","不，我想維持這樣就好!"))
	{

		case 1:
			mes @npcname$;
			mes "太好了! 但是你要先收集打洞需要的材料!";
			next;
			mes @npcname$;
			mes "我需要的材料有...";
			mes "^FF000010 個鋼鐵^000000,";
			mes "^FF00001 個鋁^000000,";
			mes "^FF000010 個強化武器金屬二級^000000,";
			mes "^FF000030 個煤礦^000000,";
			mes "^FF000010 個生鏽的螺絲^000000";
			mes "當然還要你的^FF0000加立遜手槍^000000!";
			next;
			mes @npcname$;
			if ( (countitem(999)<10) || (countitem(985)<1) || (countitem(1011)<10) ||(countitem(1003)<30) || (countitem(7317)<10) || (countitem(13104)<1) ) {
				mes "等你收集到所有材料再來找我吧!";
				close;
			}
			delitem 999,10;
			delitem 985,1;
			delitem 1011,10;
			delitem 1003,30;
			delitem 7317,10;
			delitem 13104,1;
			mes "看來你已經找齊所有材料了!";
			mes "讓我看看...";
			next;
			mes @npcname$;
			mes "嗯...等我一下...";
			mes "...";
			mes ".....";
			next;
			getitem 13105,1;
			mes @npcname$;
			mes "完成了!";
			mes "它已經比之前的手槍更完美了!";
			mes "好好的使用它吧!";
			close;

		case 2:
			mes @npcname$;
			mes "好吧...";
			mes "如果你改變心意的話再來找我!";
			close;
	}

L_Already:
	mes "你覺得我的槍好用嗎?";
	close;
}

que_ng.gat,187,163,3	script	英格麗	744,{
	set @npcname$,"[英格麗]";

	mes @npcname$;
	mes "我可以只製作所有槍枝中，";
	mes "火力最強大的手槍!";
	mes "它是最終極的榴彈槍!";
	mes "叫做^0000FF地獄榴彈槍^000000!";
	next;
	if(BaseJob != Job_Gunslinger || BaseLevel < 65){
		mes @npcname$;
		mes "但是我不認為你可以使用它!";
		mes "你的經驗還不足!";
		next;
		mes @npcname$;
		mes "現在讓你使用這把槍，";
		mes "可能會因為走火而發生意外!";
		mes "我不能製造一把給你!";
		close;
	}
	mes @npcname$;
	mes "你看起來似乎是個經驗豐富的槍手!";
	mes "我想我可以位你製造一把!";
	next;
	mes @npcname$;
	mes "那麼，你想要一把^0000FF地獄榴彈槍^000000嗎?";
	next;

	switch(select("好的! 我要!","不用了!"))
	{

		case 1:
			mes @npcname$;
			mes "好的! 但是你要先收集製造的材料!";
			next;
			mes @npcname$;
			mes "需要的材料有...";
			mes "^FF0000100 個老舊的鐵板^000000,";
			mes "^FF000010 個神之金屬^000000,";
			mes "^FF000050 個生鏽的螺絲^000000,";
			mes "^FF0000100 燃燒的心臟^000000";
			mes "^FF0000200000 Zeny^000000";
			next;
			mes @npcname$;
			if ( (countitem(7319)<100) || (countitem(984)<10) || (countitem(7317)<50) || (countitem(7097)<100) || (Zeny < 200000) ) {
				mes "收集到全部的材料後再來找我吧!";
				close;
			}
			delitem 7319,100;
			delitem 984,10;
			delitem 7317,50;
			delitem 7097,100;
			set Zeny,Zeny-200000;
			mes "喔! 你已經收集到全部的材料了!";
			mes "那麼就開始製造吧!";
			next;
			mes @npcname$;
			mes "嗯... 我要小心的...";
			mes "...";
			mes ".....";
			next;
			getitem 13162,1;
			mes @npcname$;
			mes "完成了!";
			mes "要小心使用它喔!";
			close;

		case 2:
			mes @npcname$;
			mes "嗯... 這也許是個明智的選擇!";
			mes "畢竟這把槍實在太危險了!";
			close;
	}
}

que_ng.gat,185,180,3	script	凡妮莎	726,{
	set @npcname$,"[凡妮莎]";

	mes @npcname$;
	mes "我可以製造一把火力強大的榴彈槍!";
	mes "它的可以破壞敵人的盔甲!";
	mes "它叫做^0000FF破壞者榴彈槍^000000!";
	next;
	if(BaseJob != Job_Gunslinger || BaseLevel < 52){
		mes @npcname$;
		mes "但是我不認為你有足夠的經驗，";
		mes "可以使用這把槍!";
		next;
		mes @npcname$;
		mes "等你有足夠的資格使用這把槍，";
		mes "再來找我吧!";
		close;
	}
	mes @npcname$;
	mes "看來你似乎很有使用槍枝的經驗!";
	mes "我想我可以為你製造一把!";
	next;
	mes @npcname$;
	mes "你想要製造一把^0000FF破壞者榴彈槍^000000嗎?";
	next;

	switch(select("我要!","免了!"))
	{

		case 1:
			mes @npcname$;
			mes "沒問題! 但是你需要先去收集材料!";
			mes "去收集製造這把槍需要的材料吧!";
			next;
			mes @npcname$;
			mes "材料的清單是...";
			mes "^FF000050 個老舊的鐵板^000000,";
			mes "^FF00005 個神之金屬^000000,";
			mes "^FF000070 個生鏽的螺絲^000000";
			mes "還要^FF0000100000 Zeny^000000";
			next;
			mes @npcname$;
			if ( (countitem(7319)<50) || (countitem(984)<5) || (countitem(7317)<70) || (Zeny < 100000) ) {
				mes "收集到所有材料再回來吧!";
				close;
			}
			delitem 7319,50;
			delitem 984,5;
			delitem 7317,70;
			set Zeny,Zeny-100000;
			mes "喔! 看來你已經找齊材料了!";
			mes "那就趕快開始製作吧!";
			next;
			mes @npcname$;
			mes "製造需要一些時間...";
			mes "...";
			mes ".....";
			next;
			mes "等等...";
			mes ".....";
			mes ".....";
			next;
			getitem 13160,1;
			mes @npcname$;
			mes "完成了!";
			mes "請小心使用它!";
			close;

		case 2:
			mes @npcname$;
			mes "好吧! 那是你的損失!";
			close;
	}
}

que_ng.gat,149,178,5	script	安佐 納塔莎	744,{
	set @npcname$,"[安佐 納塔莎]";

	mes @npcname$;
	mes "如果你喜歡格林機槍，";
	mes "我就是你的最佳人選!";
	mes "我可以為你製造一把^0000FF漂流者格林機槍^000000唷!";
	next;
	if(BaseJob != Job_Gunslinger || BaseLevel < 55){
		mes @npcname$;
		mes "但是你似乎還不夠資格使用呢~";
		mes "真是遺憾...";
		next;
		mes @npcname$;
		mes "等你累積了足夠的經驗的時候，";
		mes "再過來找我吧!";
		close;
	}
	mes @npcname$;
	mes "哇~ 你看起來是個經驗豐富的槍手呢!";
	mes "我想我可以為你製作一把格林機槍唷!";
	next;
	mes @npcname$;
	mes "你想要一把新的^0000FF漂流者格林機槍^000000嗎?";
	next;

	switch(select("是的! 我要!","目前不需要!"))
	{

		case 1:
			mes @npcname$;
			mes "嗯~ 很好!";
			mes "但是我需要你先去收集製作材料!";
			mes "我才能為你製作一把格林機槍!";
			next;
			mes @npcname$;
			mes "我需要的材料有...";
			mes "^FF000070 個鋼鐵^000000,";
			mes "^FF00005 個鋁^000000,";
			mes "^FF00003 個神之金屬^000000,";
			mes "^FF000070 個煤礦^000000,";
			mes "^FF000050 個生鏽的螺絲^000000";
			mes "最後酌收工本費^FF000050000 Zeny^000000!";
			next;
			mes @npcname$;
			if ( (countitem(999)<70) || (countitem(985)<5) || (countitem(984)<3) || (countitem(1003)<70) || (countitem(7317)<50) || (Zeny < 50000) ) {
				mes "收集到所有材料後趕快回來找我唷!";
				close;
			}
			delitem 999,70;
			delitem 985,5;
			delitem 984,3;
			delitem 1003,70;
			delitem 7317,50;
			set Zeny,Zeny-50000;
			mes "看來你已經找齊材料了!";
			mes "那麼就開始製作漂流者格林機槍吧!";
			next;
			mes @npcname$;
			mes "相信我! 你不會後悔的!";
			mes "......";
			mes "(...製作中)";
			next;
			getitem 13157,1;
			mes @npcname$;
			mes "完成了!";
			mes "現在你可以...";
			mes "大開殺戒啦!!";
			close;

		case 2:
			mes @npcname$;
			mes "哼~ 好吧! 那是你的損失!";
			close;
	}
}