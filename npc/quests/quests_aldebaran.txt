// 艾爾帕蘭 -- 各種物品製作腳本
// 
// 重整作者:		Dino9021
// 
// 中文版本編號:	20041213.0.1
//
// 原始腳本:		Kingbo 2003/12/30 PM 08:01
//
//============================================================

//===== eAthena Script ======================================= 
//= Item Quest NPCs located in Aldebaran 
//===== By: ================================================== 
//=
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= 'Doctor Band', 'Feather Bonnet', 'Opera Masque', 'Sakkat Hat' Quests.
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Fixed Doctor Band items [Lupus]
//= 1.2 Removed Duplicates [Silent]
//============================================================ 


//=======================================================================================================//
//			'醫師髮箍', '羽毛帽', '話劇面具', '草帽' 任務
//=======================================================================================================//
aldeba_in.gat,152,166,4	script	猜謎商人#01	86,{
	mes "[猜謎商人]";
	mes "哈哈哈！ 我就是傳說中那個有名的猜謎商！";
	mes "我的行跡不滿整個塵世大陸！";
	mes "呵呵..其實是因為我所販賣的商品是相當獨特的東西，一邊賣一邊也雲遊四海囉！";
	next;
	mes "(1) . ^3355FF醫師髮箍^000000 : 頭巾 1 個+ 鐵 50 塊 + 受損的鑽石 + 3500 金幣";
	mes "(2) . ^3355FF羽毛帽^000000 : 回憶帽 1 個 + 羽毛 300 根 + 500 金幣";
	mes "(3) . ^3355FF話劇面具^000000 : 鋼鐵 20 塊 + 哀唱草 1 株 + 5000 金幣";
	mes "(4) . ^3355FF草帽^000000 : 瑪哪樹枝120 個 + 10000 金幣";
	next;
	menu "醫師髮箍",-,"羽毛帽",M_1,"話劇面具",M_2,"草帽",M_3;

		if(countitem(2275) == 0 || countitem(998) < 50 || countitem(733) == 0 || Zeny < 3500) goto L_CantMake;
		delitem 2275,1;//物品: 頭巾
		delitem 998,50;//物品: 鐵
		delitem 733,1;//物品: 受損的鑽石
		set Zeny,Zeny - 3500;
		mes "[猜謎商人]";
		mes "嗯嗯 ！ 你 .. 有醫師執照嗎 ?！ 不過呢，如果你擁有像怪醫黑捷克那樣的醫術的話，沒有執照應該也無所謂啦..";
		mes "嗯，應該沒什麼太大的問題。那，拿去，這個給你。";
		getitem 2273,1;//物品: 醫師髮箍
		close;
	M_1:
		if(countitem(2247) == 0 || countitem(916) < 300 || Zeny < 500) goto L_CantMake;//物品: 回憶帽, 羽毛
		delitem 2247,1;//物品: 回憶帽
		delitem 916,300;//物品: 羽毛
		set Zeny,Zeny - 500;
		mes "[猜謎商人]";
		mes "嘿黑，你的眼光不賴嘛。";
		mes "雖然這些材料得來不易，但是光看這做出來的工你應該還滿意吧?　那，拿去吧..";
		emotion e_heh;
		getitem 5018,1;//物品: 羽毛帽
		close;
	M_2:
		if(countitem(999) < 20 || countitem(707) == 0 || Zeny < 5000) goto L_CantMake;//物品: 鋼鐵, 哀唱草
		delitem 999,20;//物品: 鋼鐵
		delitem 707,1;//物品: 哀唱草
		set Zeny,Zeny - 5000;
		mes "[猜謎商人]";
		mes "這個應該還可以吧..";
		mes "雖然外觀看起來有點令人毛骨悚然，不過..倒也蠻有它獨特的風味的，你覺得如何呢?";
		mes ".. 看起來你還挺喜歡的嘛.. 那，拿走吧！";
		getitem 2281,1;//物品: 話劇面具
		close;
	M_3:
		if(countitem(1019) < 120 || Zeny < 10000) goto L_CantMake;//物品: 瑪哪樹枝
		delitem 1019,120;//物品: 瑪哪樹枝
		set Zeny,Zeny - 10000;
		mes "[猜謎商人]";
		mes "聽說在斐楊的村落有一位製作草帽的好手..";
		mes "出自那位好手的草帽可是只有在我這裡才有的喔！";
		mes "那 ！ 拿走吧 ！";
		getitem 2280,1;//物品: 草帽
		close;

	L_CantMake:
		mes "[猜謎商人]";
		mes "你看你看，我不是要你再重新確認一下自己需要的物品嘛。";
		mes "年輕人你的頭腦好像不太靈光喔.. 要清醒一點，知道嗎?";
		close;
}
