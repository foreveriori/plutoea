// 普隆德拉 -- 各種物品製作腳本
// 
// 重整作者:		Dino9021
// 
// 中文版本編號:	20041213.0.1
//
// 原始腳本:		Kingbo 2003/12/30 PM 08:01
//
//============================================================

//===== eAthena Script ======================================= 
//= Item Quest NPCs located in Prontera
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Ph.D Hat Quest
//===== Additional Comments: ================================= 
//= Fixed flower name Dreamy -> Illusion
//= 1.2 Fixed exploit [Lupus]
//= 1.3 Removed Duplicates [Silent]
//============================================================ 


// Ph.D Hat Quest-------------------------------------------------------
prt_in.gat,38,108,4	script	教授	53,{
	if(countitem(710) >= 1 && countitem(703) >= 1 && countitem(704) >= 1 && countitem(708) >= 1) goto L_Start;//Items: Illusion Flower, Hinalle, Aloe, Ment,
	mes "[教授]";
	mes "這花... 很美。對吧?";
	next;
	menu "繼續對話",-,"取消",M_End;

		mes "[教授]";
		mes "年輕的時候我很少和同伴一起出去玩，幾乎都埋頭苦讀。";
		mes "雖然有點可惜，但是...我並不後悔。托那時候的福我才能完成那麼高等的教育，要不然我也不會有今日的成就。";
		next;
		mes "[教授]";
		mes "不過說實話... 沒什麼朋友還是令我感到相當的悲傷。";
		mes "讀書讀到厭煩的時候.. 連個可以聽我說話跟我談心的朋友都沒有。";
		mes "值得慶幸的是，後來我終於找到我這輩子最真心的朋友了。";
		next;
		mes "[教授]";
		mes "當我走進花園裡，看到那一朵朵盛開的美麗花朵的那一瞬間...";
		mes "我突然感受到自己與花朵有著相同的命運，於是我將花朵當作我的摯友。";
		next;
		mes "[教授]";
		mes "對其他人來說花朵可能就只是花朵而已..但是對我來說，它給了我相當大的力量。";
		mes "我能夠有今天的成就..可以說都是因為有它的陪伴。";
		next;
		mes "[教授]";
		mes "所以.. 我最近.. 很想報答那些贈與我花的朋友。";
		mes "我們村莊做了很多有關花的研究..甚至還想佈置出一個充滿花的地方，朝世界邁進。";
		next;
		mes "[教授]";
		mes "所以.. 我們需要很多很多的花...";
		mes "因為我不太常出門.. 所以要找尋是相當的困難的。";
		mes "如果你可以為我找到^3355FF幻想花^000000 與 ^3355FF西納雷花^000000 還有 ^3355FF蘆薈^000000 跟 ^3355FF曼特花^000000... 這些花的話，我就把我最重視的東西給你...";
		close;

	M_End:
		mes "[教授]";
		mes "總有一天我一定會用很多很多的花朵來佈置一個美麗無比的地方。";
		close;

L_Start:
	mes "[教授]";
	mes "我的天啊.. 手上拿著的花是..";
	M_Menu:
	next;
	menu "給他看花",-,"給他花",M_Give,"取消",M_End;
		mes "[教授]";
		mes "那.. 那是 .. 我尋尋覓覓的四種花沒錯..";
		mes "你可以把花給我嗎?...";
		next;
		mes "[教授]";
		mes "如果你願意給我的話...我...我就把我最重視的東西給你。";
		goto M_Menu;

	M_Give:
		if(countitem(710) < 1 || countitem(703) < 1 || countitem(704) < 1 || countitem(708) < 1) goto L_Cheater;//Items: Illusion Flower, Hinalle, Aloe, Ment,
		delitem 710,1;//Items: Illusion Flower,
		delitem 703,1;//Items: Hinalle,
		delitem 704,1;//Items: Aloe,
		delitem 708,1;//Items: Ment,
		mes "[教授]";
		mes "真是太感謝你了，從小到大這是我第一次感到人情的溫暖。";
		emotion e_thx;
		next;
		mes "[教授]";
		mes "我就照約定把我最重視的東西給你。";
		mes "這是我畢業的時候所戴過的帽子，裡面載滿了我求學時期的回憶。";
		mes "請你收下..希望你也跟我一樣珍惜它。";
		getitem 5012,1;//Items: Ph.D Hat,
		close;

	L_Cheater:
		mes "[教授]";
		mes "... 仔細一看這並不是我所要尋找的花...";
		mes "哈哈... 都怪我眼睛昏花，沒看清楚...";
		emotion e_bzz;
		close;
}

// ------------------------------------------------------------------------------------------- Culvert -------------------------------------------------------------------------------\\
// Culvert Guardian ----------------------------------------------------
prt_fild05.gat,270,212,5	script	地下水道 管理兵	105,{
	mes "[地下水道 管理兵]";
	if(MISC_QUEST & 8) {

		mes "這裡是進入地下水道的入口。";
		next;
		menu "我要進去地下水道。",-,"不用了！",M_END;

			warp "prt_sewb1.gat",131,247;
		M_END:
			close;
	}
	mes "對不起。這裡只有申請三加地下水道討伐隊的人才可以進入。報名處是^000077在普隆德拉的祭祀壇^000000上報名。詳細的位置是在普隆德拉西北方底的建築物。";
	close;
}

// Recruiter -----------------------------------------------------------
prt_in.gat,88,105,2	script	祭祀壇 集英館	105,{
	mes "[祭祀壇 集英館]";
	if(MISC_QUEST & 8) goto L_WARP;
	mes "從盧恩米德加爾特王國的首都防衛司令部傳來聖旨。以這次的事件為起點，將前往討伐地下水道魔物，再次大量召集智勇雙全的勇士！！";
	next;
	menu "我申請加入地下水道討伐隊。",M_VOL,"這次的事件….？",-,"不用了！",L_END;

		mes "[祭祀壇 集英館]";
		mes "昆蟲的污物和蛆最後沿著水管流了過來。已經引起了很大的騷動，難道您完全都不知道嗎？";
		next;
		mes "[祭祀壇 集英館]";
		mes "從那天之後，普隆德拉防疫局頒布首都飲用水禁止使用令。因此現在普隆德拉市民正遭遇嚴重的飲水災難，並且我們正在解決處理市民對國家的不信任，以免擴大。";
		next;
		mes "[祭祀壇 集英館]";
		mes "因為此事，市民的民心更加渙散...";
		next;
		mes "[祭祀壇 集英館]";
		mes "是的！現在就是我們最後該出去的時候了！因為各位的努力，所以才保障後代能擁有光明的未來以及乾淨的環境！國家和市民希望您的幫忙！！現在請求支援！";
		next;
		menu "我申請加入地下水道討伐隊。",M_VOL,"不用了！",-;

			mes "[祭祀壇 集英館]";
			mes "..............";
			close;
	M_VOL:
		mes "[祭祀壇 集英館]";
		mes "我已接受閣下您的申請。對於各位三與這次關係到國家和後代子孫未來的戰役，本人謹代表首都防衛司令部向您獻上感謝之意！！";
		next;
		mes "[祭祀壇 集英館]";
		mes "這是給這次申請加入討伐隊的支援補給物品。紅色藥水 3 瓶,牛奶 1 瓶。還有危急時所使用的赤色藥水 1 瓶。";
		set MISC_QUEST,MISC_QUEST | 8;
		getitem 501,3;
		getitem 519,1;
		getitem 502,1;
	       	next;
		mes "[祭祀壇 集英管]";
	L_WARP:
		mes "希望您能努力協助我們!";
		next;
		warp "prt_fild05.gat",272,210;
	L_END:
		close;

}
