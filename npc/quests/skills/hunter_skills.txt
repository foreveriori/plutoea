//===== eAthena Script ======================================= 
//= Hunter Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.3d
//===== Compatible With: ===================================== 
//= eAthena Revision 3800+
//===== Description: ========================================= 
//= Temp quests for new skills for 2nd classes
//===== Additional Comments: ================================= 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//= 1.3c Fixed some typos [IVBela]
//= 1.3d Changed NPC Name to the official [Lupus]
//============================================================


//============================================================
// 獵人技能 - 幻影箭
//============================================================
payon_in02.gat,54,13,7	script	卡巴克大師	55,{
	mes "[卡巴克大師]";
	if(BaseJob!=Job_Hunter) goto L_hunterno;
	if(getskilllv(1009)) goto L_alreadyhave;
	if(JobLevel<40) goto L_nojob;

	mes "我告訴你個秘密哦~";
	mes "這個技能是我的家族一代代傳下來的";
	mes "我們家族保守這個秘密很久了";
	mes "但是我能透露點給你";
	mes "作為參考~";
	next;
	mes "[卡巴克大師]";
	mes "我需要點東西~";
	mes "5 詛咒紅寶石";
	mes "5 哈比的羽毛";
	mes "30 寵物飼料";
	next;
	
	mes "[卡巴克大師]";
	mes "我來檢查下你帶的東西.";
	next;
	
	if(countitem(724)<5 || countitem(7115)<5 || countitem(537)<30) goto L_noitems;//Items: Cursed_Ruby, Harpy_Feather, Pet_Food,
	delitem 724, 5;//Items: Cursed_Ruby,
	delitem 7115, 5;//Items: Harpy_Feather,
	delitem 537, 30;//Items: Pet_Food,
	
	mes "[卡巴克大師]";
	mes "都帶來了,";
	mes "我來教你我引以為傲的技能吧！";
	skill 1009,1,0;
	close;

L_noitems:
	mes "[卡巴克大師]";
	mes "你沒帶足東西.";
	mes "等你有了再來找我吧";
	close;
		
L_nojob:
	mes "當你成為個真正的獵人的時候";
	mes "再來吧.";
	mes "最起碼40級！";
	close;

L_alreadyhave:
	mes "哈~你回來我看我了？";
	close;
	
L_hunterno:
	mes "我能幫你什麼忙嗎？";
	mes "我想你大概找錯人了 我只是個老頭";
	close;
}
