// 憂鬱日記腳本 (寵物妖道認養道具)
// 魔物信件製作腳本
// 靈幻道士之劍腳本 (寵物妖道裝飾品)
// 
// 重整作者:		Dino9021
// 
// 中文版本編號:	20041210.0.1
//
// 原始腳本:		Robert 蝴蝶君 2004-1-20 Mailto:robert_st@21cn.com
//
//============================================================
//===== eAthena Script ======================================= 
//= Munak and Bongun accessory / Taming items Quest
//===== By: ================================================== 
//= eAthena Team
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= Quest itms:Sword o'Chinese Exorcist,No Recipient,Her Heart
//===== Additional Comments: ================================= 
//= 1.0. merged 3 sep. files, fixed exploits [Lupus]
//= 1.1	Added missing next;'s [Evera]
//= 1.2 Added another missing next; [Evera]
//============================================================ 


comodo,135,227,5	script	沙莉	93,{
	mes "[沙莉]";
	mes "唉……";
	next;
	mes "[沙莉]";
	mes "你是在和我說話嗎？...嗯，我最近正在為撰寫一個英俊的男孩的而苦惱...";
	next;
	mes "[沙莉]";
	mes "我擔心的是……";
	mes "我不瞭解他的性格、脾氣究竟是什麼樣子的，所以......如果你能幫我找到一本關於描述男孩的'少女日記'，我願意用一本男孩的日記和你交換！";
	next;
	mes "[沙莉]";
	mes "你願意幫我這個忙嗎？";
	next;

	menu "你需要我幫你做什麼呢?",-,"我這正好有你想要的東西",ExCreate,"對不起，我想我幫不上你",ExEnd;

	mes "[沙莉]";
	mes "我需要的是一本關於描寫男孩的'少女日記'...";
	close;

ExCreate:
	mes "[沙莉]";
	mes "真的嗎? 那你願意幫我這個忙嗎?";
	next;

	if(countitem(1558)<1) goto L_NoItem;//Items: Girl's_Diary,
	delitem 1558,1;//Items: Girl's_Diary,
	getitem 659,1;//Items: Her_Heart,
	mes "[沙莉]";
	mes "那真是太好了! 是的，我當然也不會忘記對你的承諾!";
	mes " ";
	mes "這是一本男孩的憂鬱日記。再次感謝你，好心人!";
	next;
	mes "[沙莉]";
	mes "哦，等等。我這裡還有一把劍，也一起送給你吧! 這是我從日記裡發現的，雖然這把劍已經損壞了……我知道在吉芬西部有一個老工匠，你可以去試試找他幫忙，看看能不能將它修復。";
	getitem 7110,1;//Items: Broken_Sword,
	close;

L_NoItem:
	mes "[沙莉]";
	mes "你...別胡鬧了，我可沒心情和你玩! 走開!";
	close;
ExEnd:
	mes "[沙莉]";
	mes "唉……那算了……你走吧。";
	close;    
}

izlude_in,115,76,5	script	埃文	47,{
	mes "[埃文]";
	mes "唉……";
	next;
	mes "[埃文]";
	mes "嗯？你是在和我說話嗎？ ...唉，我最近正在設法裝入一個漂亮女孩的靈魂 ...";
	next;
	mes "[埃文]";
	mes "你不懂我說什麼？";
	mes "我也不知道如何描述它，如果你能幫我找到一幅'破舊畫像'的話，我將給你一個'魔物信件'！";
	next;
	mes "[埃文]";
	mes "明白了嗎？我的朋友！";
	next;

	menu "我需要做什麼嗎？",-,"我這裡有你需要的",ExCreate,"對不起，我幫不上你",ExEnd;

	mes "[埃文]";
	mes "一張描繪她的照片...'破舊畫像'...";
	close;

ExCreate:
	mes "[埃文]";
	mes "真的嗎？那你願意幫我嗎？";
	next;
	if(countitem(7014)<1) goto L_NoItem;//Items: Old_Portrait,
	delitem 7014,1;//Items: Old_Portrait,
	getitem 636,1;//Items: No_Recipient,
	mes "[埃文]";
	mes "對！我不會忘記對你的承諾！";
	mes " ";
	mes "這是'魔物信件'，送給你，再次感謝你！";
	close;

L_NoItem:
	mes "[埃文]";
	mes "別和我開玩笑了，走開！";
	close;

ExEnd:
	mes "[埃文]";
	mes "唉……";
	close;
}

gef_fild07,183,239,5	script	老鐵匠	120,{
	mes "[老鐵匠]";
	mes "唉，我以前曾是這裡著名的鍛造公會一名出色的鐵匠。戰爭已經結束了，我的技巧也隨著時光而漸漸遜色。";
	next;
	if(countitem(7110)<1) goto L_NoItemS;//Items: Broken_Sword,
	mes "[老鐵匠]";
	mes "唔.....看來你碰到了一些麻煩，可以告訴我嗎？";
	next;
	menu "你能幫我修復這把劍嗎？",-,"沒事，只是隨便看看而已",ExEnd;

	mes "[老鐵匠]";
	mes "啊！這是一把非常古老而陳舊的古劍，當然，我可以修好它。但是我需要一些'神之金屬'。";
	next;
	if(countitem(7110)<1 || countitem(984)<1) goto L_NoItem;//Items: Broken_Sword, Oridecon,
	delitem 7110,1;//Items: Broken_Sword,
	delitem 984,1;//Items: Oridecon,
	getitem 10020,1;//Items: Sword_of_Chinese_Exorcist,
	mes "[老鐵匠]";
	mes "對了，就是這些東西！";
	mes " ";
	mes "哈哈！古老魔法的靈氣仍舊在這把'靈幻道士之劍'上遊蕩，原本就是這個樣子了！";
	close;

L_NoItem:
	mes "[老鐵匠]";
	mes "唔？你似乎沒有將需要的東西帶來...正確的說，你必須把'神之金屬'和那把'壞的劍'一起帶來。";
	close;

L_NoItemS:
ExEnd:
	mes "[老鐵匠]";
	mes "年輕人，應該多到外面走走，作用中。否則有一天你也會後悔的。";
	close;
}
